--------------------------------------------------
-- Decision procedure for equivalence of types
--------------------------------------------------

open import MSTT.Parameter.ModeTheory
open import MSTT.Parameter.TypeExtension using (TyExt)

module MSTT.Equivalence (mt : ModeTheory) (ty-ext : TyExt mt) where

open import Data.List hiding (_++_)
open import Data.Product using (_Ã—_; _,_)
open import Data.String
open import Function using (_âˆ˜_)
open import Relation.Binary.PropositionalEquality

open import Model.CwF-Structure
open import Model.Modality as M hiding (âŸ¨_âˆ£_âŸ©; ðŸ™; _â“œ_)
open import Model.Type.Function as M hiding (_â‡›_)
open import Model.Type.Product as M hiding (_âŠ _)

open import MSTT.TCMonad
open import MSTT.Syntax.Type mt ty-ext
open import MSTT.InterpretTypes mt ty-ext

open ModeTheory mt
open TyExt ty-ext
open MSTT.Parameter.TypeExtension mt hiding (TyExt)

private
  variable
    m m' : ModeExpr
    margs : List ModeExpr


-- The idea is to reduce a type expression to a canonical equivalent one in two steps.
-- 1. Reduce all occurrences of âŸ¨ Î¼ âˆ£ âŸ¨ Ï âˆ£ T âŸ© âŸ© to âŸ¨ Î¼ â“œ Ï âˆ£ T âŸ©. The resulting type
--    will not have a subexpression consisting of a double modality application.
-- 2. Reduce all occurrences of âŸ¨ Î¼ âˆ£ T âŸ© for which Î¼ â‰ƒ ðŸ™ to T.
-- Two types that are literally equal (up to equivalence of modalities determined by
-- the mode theory) after this reduction are considered equivalent by the decision
-- procedure _â‰ƒáµ—Ê¸?_.

--------------------------------------------------
-- Reduction step 1, implemented by reduce-comp

reduce-comp-helper : ModalityExpr m m' â†’ TyExpr m â†’ TyExpr m'
reduce-comp-helper Î¼ Nat' = âŸ¨ Î¼ âˆ£ Nat' âŸ©
reduce-comp-helper Î¼ Bool' = âŸ¨ Î¼ âˆ£ Bool' âŸ©
reduce-comp-helper Î¼ (T â‡› S) = âŸ¨ Î¼ âˆ£ T â‡› S âŸ©
reduce-comp-helper Î¼ (T âŠ  S) = âŸ¨ Î¼ âˆ£ T âŠ  S âŸ©
reduce-comp-helper Î¼ âŸ¨ Ï âˆ£ T âŸ© = âŸ¨ Î¼ â“œ Ï âˆ£ T âŸ©
reduce-comp-helper Î¼ (Ext c args) = âŸ¨ Î¼ âˆ£ Ext c args âŸ©

reduce-comp : TyExpr m â†’ TyExpr m
reduce-comp-ext-args : TyExtArgs margs â†’ TyExtArgs margs

reduce-comp Nat' = Nat'
reduce-comp Bool' = Bool'
reduce-comp (T â‡› S) = reduce-comp T â‡› reduce-comp S
reduce-comp (T âŠ  S) = reduce-comp T âŠ  reduce-comp S
reduce-comp âŸ¨ Î¼ âˆ£ T âŸ© = reduce-comp-helper Î¼ (reduce-comp T)
reduce-comp (Ext c args) = Ext c (reduce-comp-ext-args args)

reduce-comp-ext-args {[]}        args       = args
reduce-comp-ext-args {m âˆ· margs} (T , args) = reduce-comp T , reduce-comp-ext-args args

reduce-comp-helper-sound : (Î¼ : ModalityExpr m m') (T : TyExpr m) â†’ âˆ€ {Î“} â†’
                           âŸ¦ reduce-comp-helper Î¼ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ âŸ¨ Î¼ âˆ£ T âŸ© âŸ§ty
reduce-comp-helper-sound Î¼ Nat' = â‰…áµ—Ê¸-refl
reduce-comp-helper-sound Î¼ Bool' = â‰…áµ—Ê¸-refl
reduce-comp-helper-sound Î¼ (T â‡› S) = â‰…áµ—Ê¸-refl
reduce-comp-helper-sound Î¼ (T âŠ  S) = â‰…áµ—Ê¸-refl
reduce-comp-helper-sound Î¼ âŸ¨ Ï âˆ£ T âŸ© = eq-mod-closed (â“œ-interpretation Î¼ Ï) âŸ¦ T âŸ§ty {{âŸ¦âŸ§ty-natural T}}
reduce-comp-helper-sound Î¼ (Ext c args) = â‰…áµ—Ê¸-refl

reduce-comp-sound : (T : TyExpr m) â†’ âˆ€ {Î“} â†’ âŸ¦ reduce-comp T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ T âŸ§ty
reduce-comp-sound-ext-args : {F G : TyConstructor margs m} â†’ TyConstructorEquiv F G â†’ (args : TyExtArgs margs) â†’
                             âˆ€ {Î“} â†’ interpret-ext-ty F (reduce-comp-ext-args args) {Î“} â‰…áµ—Ê¸ interpret-ext-ty G args

reduce-comp-sound Nat' = â‰…áµ—Ê¸-refl
reduce-comp-sound Bool' = â‰…áµ—Ê¸-refl
reduce-comp-sound (T â‡› S) = â‡›-cong (reduce-comp-sound T) (reduce-comp-sound S)
reduce-comp-sound (T âŠ  S) = âŠ -cong (reduce-comp-sound T) (reduce-comp-sound S)
reduce-comp-sound âŸ¨ Î¼ âˆ£ T âŸ© = â‰…áµ—Ê¸-trans (reduce-comp-helper-sound Î¼ (reduce-comp T))
                                        (mod-cong âŸ¦ Î¼ âŸ§modality (reduce-comp-sound T))
reduce-comp-sound (Ext c args) = reduce-comp-sound-ext-args (interpret-code-cong c) args

reduce-comp-sound-ext-args {[]}        is-equiv args       = is-equiv
reduce-comp-sound-ext-args {m âˆ· margs} is-equiv (T , args) = reduce-comp-sound-ext-args (is-equiv (reduce-comp-sound T)) args


--------------------------------------------------
-- Reduction step 2, implemented by reduce-unit

reduce-unit-helper : ModalityExpr m m' â†’ TyExpr m â†’ TyExpr m'
reduce-unit-helper {m} {m'} Î¼ T with m â‰Ÿmode m'
reduce-unit-helper {m} {m'} Î¼ T | type-error _ = âŸ¨ Î¼ âˆ£ T âŸ©
reduce-unit-helper {m} {m'} Î¼ T | ok refl with ðŸ™ â‰ƒáµ? Î¼
reduce-unit-helper {m} {m'} Î¼ T | ok refl | type-error _ = âŸ¨ Î¼ âˆ£ T âŸ©
reduce-unit-helper {m} {m'} Î¼ T | ok refl | ok _ = T

reduce-unit : TyExpr m â†’ TyExpr m
reduce-unit-ext-args : TyExtArgs margs â†’ TyExtArgs margs

reduce-unit Nat' = Nat'
reduce-unit Bool' = Bool'
reduce-unit (T â‡› S) = reduce-unit T â‡› reduce-unit S
reduce-unit (T âŠ  S) = reduce-unit T âŠ  reduce-unit S
reduce-unit âŸ¨ Î¼ âˆ£ T âŸ© = reduce-unit-helper Î¼ (reduce-unit T)
reduce-unit (Ext c args) = Ext c (reduce-unit-ext-args args)

reduce-unit-ext-args {[]}        args       = args
reduce-unit-ext-args {m âˆ· margs} (T , args) = reduce-unit T , reduce-unit-ext-args args

reduce-unit-helper-sound : (Î¼ : ModalityExpr m m') (T : TyExpr m) â†’ âˆ€ {Î“} â†’
                           âŸ¦ reduce-unit-helper Î¼ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ âŸ¨ Î¼ âˆ£ T âŸ© âŸ§ty
reduce-unit-helper-sound {m} {m'} Î¼ T with m â‰Ÿmode m'
reduce-unit-helper-sound {m} {m'} Î¼ T | type-error _ = â‰…áµ—Ê¸-refl
reduce-unit-helper-sound {m} {m'} Î¼ T | ok refl with ðŸ™ â‰ƒáµ? Î¼
reduce-unit-helper-sound {m} {m'} Î¼ T | ok refl | type-error _ = â‰…áµ—Ê¸-refl
reduce-unit-helper-sound {m} {m'} Î¼ T | ok refl | ok ðŸ™=Î¼ = eq-mod-closed (â‰…áµ-trans (â‰…áµ-sym ðŸ™-interpretation) ðŸ™=Î¼)
                                                                         âŸ¦ T âŸ§ty {{âŸ¦âŸ§ty-natural T}}

reduce-unit-sound : (T : TyExpr m) â†’ âˆ€ {Î“} â†’ âŸ¦ reduce-unit T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ T âŸ§ty
reduce-unit-sound-ext-args : {F G : TyConstructor margs m} â†’ TyConstructorEquiv F G â†’ (args : TyExtArgs margs) â†’
                             âˆ€ {Î“} â†’ interpret-ext-ty F (reduce-unit-ext-args args) {Î“} â‰…áµ—Ê¸ interpret-ext-ty G args

reduce-unit-sound Nat' = â‰…áµ—Ê¸-refl
reduce-unit-sound Bool' = â‰…áµ—Ê¸-refl
reduce-unit-sound (T â‡› S) = â‡›-cong (reduce-unit-sound T) (reduce-unit-sound S)
reduce-unit-sound (T âŠ  S) = âŠ -cong (reduce-unit-sound T) (reduce-unit-sound S)
reduce-unit-sound âŸ¨ Î¼ âˆ£ T âŸ© = â‰…áµ—Ê¸-trans (reduce-unit-helper-sound Î¼ (reduce-unit T))
                                        (mod-cong âŸ¦ Î¼ âŸ§modality (reduce-unit-sound T))
reduce-unit-sound (Ext c args) = reduce-unit-sound-ext-args (interpret-code-cong c) args

reduce-unit-sound-ext-args {[]}        is-equiv args       = is-equiv
reduce-unit-sound-ext-args {m âˆ· margs} is-equiv (T , args) = reduce-unit-sound-ext-args (is-equiv (reduce-unit-sound T)) args


--------------------------------------------------
-- The full reduction function

reduce-ty : TyExpr m â†’ TyExpr m
reduce-ty = reduce-unit âˆ˜ reduce-comp

reduce-ty-sound : (T : TyExpr m) â†’ âˆ€ {Î“} â†’ âŸ¦ reduce-ty T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ T âŸ§ty
reduce-ty-sound T = â‰…áµ—Ê¸-trans (reduce-unit-sound (reduce-comp T))
                              (reduce-comp-sound T)


--------------------------------------------------
-- The final decision procedure for type equivalence

_â‰Ÿlist-mode_ : (ms1 ms2 : List ModeExpr) â†’ TCM (ms1 â‰¡ ms2)
[] â‰Ÿlist-mode [] = return refl
[] â‰Ÿlist-mode (_ âˆ· _) = type-error ""
(_ âˆ· _) â‰Ÿlist-mode [] = type-error ""
(m1 âˆ· ms1) â‰Ÿlist-mode (m2 âˆ· ms2) = do
  refl â† m1 â‰Ÿmode m2
  refl â† ms1 â‰Ÿlist-mode ms2
  return refl

-- Are two types identical up to equivalence of modalities?
_â‰Ÿty_ : (T S : TyExpr m) â†’ TCM (âˆ€ {Î“} â†’ âŸ¦ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ S âŸ§ty)
â‰Ÿty-ext-args : {F G : TyConstructor margs m} â†’ TyConstructorEquiv F G â†’ (args1 args2 : TyExtArgs margs) â†’
               TCM (âˆ€ {Î“} â†’ interpret-ext-ty F args1 {Î“} â‰…áµ—Ê¸ interpret-ext-ty G args2)

Nat' â‰Ÿty Nat' = return â‰…áµ—Ê¸-refl
Bool' â‰Ÿty Bool' = return â‰…áµ—Ê¸-refl
(T1 â‡› S1) â‰Ÿty (T2 â‡› S2) = do
  T1=T2 â† T1 â‰Ÿty T2
  S1=S2 â† S1 â‰Ÿty S2
  return (â‡›-cong T1=T2 S1=S2)
(T1 âŠ  S1) â‰Ÿty (T2 âŠ  S2) = do
  T1=T2 â† T1 â‰Ÿty T2
  S1=S2 â† S1 â‰Ÿty S2
  return (âŠ -cong T1=T2 S1=S2)
(âŸ¨_âˆ£_âŸ© {mT} Î¼ T) â‰Ÿty (âŸ¨_âˆ£_âŸ© {mS} Ï S) = do
  refl â† mT â‰Ÿmode mS
  Î¼=Ï â† Î¼ â‰ƒáµ? Ï
  T=S â† T â‰Ÿty S
  return (â‰…áµ—Ê¸-trans (eq-mod-closed Î¼=Ï âŸ¦ T âŸ§ty {{âŸ¦âŸ§ty-natural T}})
                    (mod-cong âŸ¦ Ï âŸ§modality T=S))
(Ext {margs1} c1 args1) â‰Ÿty (Ext {margs2} c2 args2) = do
  refl â† margs1 â‰Ÿlist-mode margs2
  refl â† c1 â‰Ÿcode c2
  â‰Ÿty-ext-args (interpret-code-cong c1) args1 args2
T â‰Ÿty S = type-error ("Type " ++ show-type T ++ " is not equal to " ++ show-type S)

â‰Ÿty-ext-args {[]}        is-equiv args1 args2 = return is-equiv
â‰Ÿty-ext-args {m âˆ· margs} is-equiv (T1 , args1) (T2 , args2) = do
  T1=T2 â† T1 â‰Ÿty T2
  â‰Ÿty-ext-args (is-equiv T1=T2) args1 args2


ty-reflect : (T S : TyExpr m) â†’ (âˆ€ {Î“} â†’ âŸ¦ reduce-ty T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ reduce-ty S âŸ§ty) â†’
             âˆ€ {Î“} â†’ âŸ¦ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ S âŸ§ty
ty-reflect T S e = â‰…áµ—Ê¸-trans (â‰…áµ—Ê¸-trans (â‰…áµ—Ê¸-sym (reduce-ty-sound T))
                                        e)
                             (reduce-ty-sound S)

reduce-compare-ty : (T S : TyExpr m) â†’ TCM (âˆ€ {Î“} â†’ âŸ¦ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ S âŸ§ty)
reduce-compare-ty T S =
  let T' = reduce-ty T
      S' = reduce-ty S
  in with-error-msg ("Type " ++ show-type T ++ " is not equal to " ++ show-type S ++ ", reduced the equality to " ++
                      show-type T' ++ " =?= " ++ show-type S') (
    (T' â‰Ÿty S') >>= Î» T'=S' â†’ return (ty-reflect T S T'=S'))

-- The final decision procedure. Note that we first check whether two types
-- are identical and only compare their reductions if they are not.
-- The reason is that the former condition generates smaller equivalence
-- proofs for the interpretations as presheaves.
_â‰ƒáµ—Ê¸?_ : (T S : TyExpr m) â†’ TCM (âˆ€ {Î“} â†’ âŸ¦ T âŸ§ty {Î“} â‰…áµ—Ê¸ âŸ¦ S âŸ§ty)
T â‰ƒáµ—Ê¸? S = (T â‰Ÿty S) <âˆ£> reduce-compare-ty T S
