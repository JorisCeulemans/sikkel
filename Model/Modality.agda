--------------------------------------------------
-- Definition and properties of modalities
--------------------------------------------------

module Model.Modality where

open import Relation.Binary.PropositionalEquality hiding ([_]; naturality)

open import Model.BaseCategory
open import Model.CwF-Structure
open import Model.Type.Function
open import Model.Type.Product
open import Model.Type.Constant
open import Model.CwF-Structure.Reflection.SubstitutionSequence

private
  variable
    C D E : BaseCategory

infix 1 _â‰…áµ_ _â‰…áµ—á¶œ_
infixl 20 _â“œ_ _â“£-vert_ _â“£-hor_


--------------------------------------------------
-- Definition of a modality as a dependent right adjoint

record Modality (C D : BaseCategory) : Setâ‚ where
  no-eta-equality
  field
    ctx-functor : CtxFunctor D C

  lock : CtxOp D C
  lock = ctx-op ctx-functor

  lock-fmap : {Î” Î“ : Ctx D} â†’ (Î” â‡’ Î“) â†’ (lock Î” â‡’ lock Î“)
  lock-fmap = ctx-fmap ctx-functor

  lock-fmap-cong = ctx-fmap-cong ctx-functor
  lock-fmap-id = ctx-fmap-id ctx-functor
  lock-fmap-âŠš = ctx-fmap-âŠš ctx-functor

  field
    âŸ¨_âˆ£_âŸ© : {Î“ : Ctx D} â†’ Ty (lock Î“) â†’ Ty Î“

    -- The modal type former should respect type morphisms
    -- (i.e. natural transformations between presheaves). This should
    -- be handled in a coherent way, in other words we should get a
    -- functor from the category Ty (lock Î“) to the category Ty Î“.
    -- Previously, we only required equivalence preservation (i.e. of
    -- natural isomorphisms between types). The transition from
    -- equivalence to morphism is partly joint work with Youie Li.
    mod-map : {Î“ : Ctx D} {T S : Ty (lock Î“)} â†’
              (T â†£ S) â†’ âŸ¨_âˆ£_âŸ© T â†£ âŸ¨_âˆ£_âŸ© S
    mod-map-cong : {Î“ : Ctx D} {T S : Ty (lock Î“)} {Ï† Î· : T â†£ S} â†’ Ï† â‰…â¿ Î· â†’
                   mod-map Ï† â‰…â¿ mod-map Î·
    mod-map-id : {Î“ : Ctx D} {T : Ty (lock Î“)} â†’ mod-map (id-trans T) â‰…â¿ id-trans (âŸ¨_âˆ£_âŸ© T)
    mod-map-âŠ™ : {Î“ : Ctx D} {R T S : Ty (lock Î“)} {Ï† : T â†£ S} {Î· : R â†£ T} â†’
                mod-map (Ï† âŠ™ Î·) â‰…â¿ mod-map Ï† âŠ™ mod-map Î·

    -- We can push substitutions under the modal type former but they
    -- get locked. Again, this must happen in a coherent way (i.e. the
    -- modal type former is a pseudonatural transformation from the
    -- pseudofunctor Ty âˆ˜ lock to Ty).
    mod-natural : {Î“ Î” : Ctx D} (Ïƒ : Î“ â‡’ Î”) {T : Ty (lock Î”)} â†’
                  (âŸ¨_âˆ£_âŸ© T) [ Ïƒ ] â‰…áµ—Ê¸ âŸ¨_âˆ£_âŸ© (T [ lock-fmap Ïƒ ])
    mod-natural-map : {Î“ Î” : Ctx D} (Ïƒ : Î“ â‡’ Î”) {T S : Ty (lock Î”)} (Î· : T â†£ S) â†’
                      mod-map (ty-subst-map (lock-fmap Ïƒ) Î·) âŠ™ from (mod-natural Ïƒ {T = T})
                        â‰…â¿
                      from (mod-natural Ïƒ) âŠ™ ty-subst-map Ïƒ (mod-map Î·)
    mod-natural-id-map : {Î“ : Ctx D} {T : Ty (lock Î“)} â†’
                         mod-map (from (ty-subst-id T) âŠ™ ty-subst-eq-subst-morph lock-fmap-id T) âŠ™ from (mod-natural (id-subst Î“))
                           â‰…â¿
                         from (ty-subst-id (âŸ¨_âˆ£_âŸ© T))
    mod-natural-âŠš-map : {Î“ Î” Î˜ : Ctx D} (Ï„ : Î” â‡’ Î˜) (Ïƒ : Î“ â‡’ Î”) {T : Ty (lock Î˜)} â†’
                        mod-map (from (ty-subst-comp T (lock-fmap Ï„) (lock-fmap Ïƒ))) âŠ™ from (mod-natural Ïƒ) âŠ™ ty-subst-map Ïƒ (from (mod-natural Ï„))
                          â‰…â¿
                        mod-map (ty-subst-eq-subst-morph (lock-fmap-âŠš Ï„ Ïƒ) T) âŠ™ from (mod-natural (Ï„ âŠš Ïƒ)) âŠ™ from (ty-subst-comp (âŸ¨_âˆ£_âŸ© T) Ï„ Ïƒ)
    mod-natural-subst-eq-map : {Î“ Î” : Ctx D} {Ïƒ Ï„ : Î“ â‡’ Î”} {T : Ty (lock Î”)} (Îµ : Ïƒ â‰…Ë¢ Ï„) â†’
                               from (mod-natural Ï„) âŠ™ ty-subst-eq-subst-morph Îµ (âŸ¨_âˆ£_âŸ© T)
                                 â‰…â¿
                               mod-map (ty-subst-eq-subst-morph (lock-fmap-cong Îµ) T) âŠ™ from (mod-natural Ïƒ)

    -- Term formers coming with a modality and their laws.
    mod-intro : {Î“ : Ctx D} {T : Ty (lock Î“)} â†’ Tm (lock Î“) T â†’ Tm Î“ (âŸ¨_âˆ£_âŸ© T)
    mod-intro-cong : {Î“ : Ctx D} {T : Ty (lock Î“)} {t t' : Tm (lock Î“) T} â†’
                     t â‰…áµ—áµ t' â†’ mod-intro t â‰…áµ—áµ mod-intro t'
    mod-intro-natural : {Î” Î“ : Ctx D} (Ïƒ : Î” â‡’ Î“) {T : Ty (lock Î“)} (t : Tm (lock Î“) T) â†’
                        (mod-intro t) [ Ïƒ ]' â‰…áµ—áµ Î¹[ mod-natural Ïƒ ] mod-intro (t [ lock-fmap Ïƒ ]')
    mod-intro-convert : {Î“ : Ctx D} {T S : Ty (lock Î“)} {Î· : T â†£ S} (t : Tm (lock Î“) T) â†’
                        convert-term (mod-map Î·) (mod-intro t) â‰…áµ—áµ mod-intro (convert-term Î· t)

    mod-elim : {Î“ : Ctx D} {T : Ty (lock Î“)} â†’ Tm Î“ (âŸ¨_âˆ£_âŸ© T) â†’ Tm (lock Î“) T
    mod-elim-cong : {Î“ : Ctx D} {T : Ty (lock Î“)} {t t' : Tm Î“ (âŸ¨_âˆ£_âŸ© T)} â†’
                    t â‰…áµ—áµ t' â†’ mod-elim t â‰…áµ—áµ mod-elim t'
    -- Naturality of mod-elim and the fact that it commutes with convert-term can be proved
    -- from mod-intro-natural, mod-intro-convert and the Î² and Î· laws (see below).

    mod-Î² : {Î“ : Ctx D} {T : Ty (lock Î“)} (t : Tm (lock Î“) T) â†’
            mod-elim (mod-intro t) â‰…áµ—áµ t
    mod-Î· : {Î“ : Ctx D} {T : Ty (lock Î“)} (t : Tm Î“ (âŸ¨_âˆ£_âŸ© T)) â†’
            mod-intro (mod-elim t) â‰…áµ—áµ t

open Modality public
_,lockâŸ¨_âŸ© : Ctx D â†’ Modality C D â†’ Ctx C
Î“ ,lockâŸ¨ Î¼ âŸ© = lock Î¼ Î“


module _ (Î¼ : Modality C D) where
  mod-cong : {Î“ : Ctx D} {T S : Ty (lock Î¼ Î“)} â†’
             T â‰…áµ—Ê¸ S â†’ âŸ¨ Î¼ âˆ£ T âŸ© â‰…áµ—Ê¸ âŸ¨ Î¼ âˆ£ S âŸ©
  from (mod-cong e) = mod-map Î¼ (from e)
  to (mod-cong e) = mod-map Î¼ (to e)
  isoË¡ (mod-cong e) = transâ¿ (symâ¿ (mod-map-âŠ™ Î¼)) (transâ¿ (mod-map-cong Î¼ (isoË¡ e)) (mod-map-id Î¼))
  isoÊ³ (mod-cong e) = transâ¿ (symâ¿ (mod-map-âŠ™ Î¼)) (transâ¿ (mod-map-cong Î¼ (isoÊ³ e)) (mod-map-id Î¼))

  mod-cong-refl : {Î“ : Ctx D} {T : Ty (lock Î¼ Î“)} â†’ mod-cong (refláµ—Ê¸ {T = T}) â‰…áµ‰ refláµ—Ê¸
  from-eq mod-cong-refl = mod-map-id Î¼

  mod-cong-sym : {Î“ : Ctx D} {T S : Ty (lock Î¼ Î“)} {e : T â‰…áµ—Ê¸ S} â†’ mod-cong (symáµ—Ê¸ e) â‰…áµ‰ symáµ—Ê¸ (mod-cong e)
  from-eq mod-cong-sym = reflâ¿

  mod-cong-trans : {Î“ : Ctx D} {R T S : Ty (lock Î¼ Î“)} {e : R â‰…áµ—Ê¸ T} {e' : T â‰…áµ—Ê¸ S} â†’
                   mod-cong (transáµ—Ê¸ e e') â‰…áµ‰ transáµ—Ê¸ (mod-cong e) (mod-cong e')
  from-eq mod-cong-trans = mod-map-âŠ™ Î¼

  mod-cong-cong : {Î“ : Ctx D} {T S : Ty (lock Î¼ Î“)} {e e' : T â‰…áµ—Ê¸ S} â†’ e â‰…áµ‰ e' â†’ mod-cong e â‰…áµ‰ mod-cong e'
  from-eq (mod-cong-cong ğ‘’) = mod-map-cong Î¼ (from-eq ğ‘’)

  mod-natural-ty-eq : {Î“ Î” : Ctx D} (Ïƒ : Î“ â‡’ Î”) {T S : Ty (lock Î¼ Î”)} (e : T â‰…áµ—Ê¸ S) â†’
                      transáµ—Ê¸ (mod-natural Î¼ Ïƒ {T = T}) (mod-cong (ty-subst-cong-ty (lock-fmap Î¼ Ïƒ) e))
                        â‰…áµ‰
                      transáµ—Ê¸ (ty-subst-cong-ty Ïƒ (mod-cong e)) (mod-natural Î¼ Ïƒ)
  from-eq (mod-natural-ty-eq Ïƒ e) = mod-natural-map Î¼ Ïƒ (from e)

  mod-natural-id : {Î“ : Ctx D} {T : Ty (lock Î¼ Î“)} â†’
                   transáµ—Ê¸ (mod-natural Î¼ _) (mod-cong (transáµ—Ê¸ (ty-subst-cong-subst (lock-fmap-id Î¼) T) (ty-subst-id T)))
                     â‰…áµ‰
                   ty-subst-id âŸ¨ Î¼ âˆ£ T âŸ©
  from-eq mod-natural-id = mod-natural-id-map Î¼

  mod-natural-âŠš : {Î“ Î” Î˜ : Ctx D} (Ï„ : Î” â‡’ Î˜) (Ïƒ : Î“ â‡’ Î”) {T : Ty (lock Î¼ Î˜)} â†’
                  transáµ—Ê¸ (ty-subst-cong-ty Ïƒ (mod-natural Î¼ Ï„)) (transáµ—Ê¸ (mod-natural Î¼ Ïƒ) (mod-cong (ty-subst-comp T _ _)))
                    â‰…áµ‰
                  transáµ—Ê¸ (ty-subst-comp âŸ¨ Î¼ âˆ£ T âŸ© Ï„ Ïƒ) (transáµ—Ê¸ (mod-natural Î¼ (Ï„ âŠš Ïƒ)) (mod-cong (ty-subst-cong-subst (lock-fmap-âŠš Î¼ Ï„ Ïƒ) T)))
  from-eq (mod-natural-âŠš Ï„ Ïƒ) = mod-natural-âŠš-map Î¼ Ï„ Ïƒ

  mod-natural-subst-eq : {Î“ Î” : Ctx D} {Ïƒ Ï„ : Î“ â‡’ Î”} {T : Ty (lock Î¼ Î”)} (Îµ : Ïƒ â‰…Ë¢ Ï„) â†’
                         transáµ—Ê¸ (ty-subst-cong-subst Îµ âŸ¨ Î¼ âˆ£ T âŸ©) (mod-natural Î¼ Ï„)
                           â‰…áµ‰
                         transáµ—Ê¸ (mod-natural Î¼ Ïƒ) (mod-cong (ty-subst-cong-subst (lock-fmap-cong Î¼ Îµ) T))
  from-eq (mod-natural-subst-eq Îµ) = mod-natural-subst-eq-map Î¼ Îµ


  mod-intro-Î¹ : {Î“ : Ctx D} {T S : Ty (lock Î¼ Î“)} {T=S : T â‰…áµ—Ê¸ S} (t : Tm (lock Î¼ Î“) S) â†’
                Î¹[ mod-cong T=S ] mod-intro Î¼ t â‰…áµ—áµ mod-intro Î¼ (Î¹[ T=S ] t)
  mod-intro-Î¹ t = transáµ—áµ Î¹-convert (transáµ—áµ (mod-intro-convert Î¼ t) (mod-intro-cong Î¼ (symáµ—áµ Î¹-convert)))

  mod-elim-natural : {Î” Î“ : Ctx D} (Ïƒ : Î” â‡’ Î“) {T : Ty (lock Î¼ Î“)} (t : Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ©) â†’
                     (mod-elim Î¼ t) [ lock-fmap Î¼ Ïƒ ]' â‰…áµ—áµ mod-elim Î¼ (Î¹â»Â¹[ mod-natural Î¼ Ïƒ ] (t [ Ïƒ ]'))
  mod-elim-natural Ïƒ t = begin
      (mod-elim Î¼ t) [ lock-fmap Î¼ Ïƒ ]'
    â‰…Ë˜âŸ¨ mod-Î² Î¼ _ âŸ©
      mod-elim Î¼ (mod-intro Î¼ ((mod-elim Î¼ t) [ lock-fmap Î¼ Ïƒ ]'))
    â‰…Ë˜âŸ¨ mod-elim-cong Î¼ Î¹-symË¡ âŸ©
      mod-elim Î¼ (Î¹â»Â¹[ mod-natural Î¼ Ïƒ ] (Î¹[ mod-natural Î¼ Ïƒ ] (mod-intro Î¼ ((mod-elim Î¼ t) [ lock-fmap Î¼ Ïƒ ]'))))
    â‰…Ë˜âŸ¨ mod-elim-cong Î¼ (Î¹â»Â¹-cong (mod-intro-natural Î¼ Ïƒ (mod-elim Î¼ t))) âŸ©
      mod-elim Î¼ (Î¹â»Â¹[ mod-natural Î¼ Ïƒ ] (mod-intro Î¼ (mod-elim Î¼ t) [ Ïƒ ]'))
    â‰…âŸ¨ mod-elim-cong Î¼ (Î¹â»Â¹-cong (tm-subst-cong-tm Ïƒ (mod-Î· Î¼ t))) âŸ©
      mod-elim Î¼ (Î¹â»Â¹[ mod-natural Î¼ Ïƒ ] (t [ Ïƒ ]')) âˆ
    where open â‰…áµ—áµ-Reasoning

  mod-elim-Î¹ : {Î“ : Ctx D} {T S : Ty (lock Î¼ Î“)} {T=S : T â‰…áµ—Ê¸ S} (t : Tm Î“ âŸ¨ Î¼ âˆ£ S âŸ©) â†’
               Î¹[ T=S ] mod-elim Î¼ t â‰…áµ—áµ mod-elim Î¼ (Î¹[ mod-cong T=S ] t)
  mod-elim-Î¹ {T = T} {S = S} {T=S = T=S} t = begin
      Î¹[ T=S ] mod-elim Î¼ t
    â‰…Ë˜âŸ¨ mod-Î² Î¼ _ âŸ©
      mod-elim Î¼ (mod-intro Î¼ (Î¹[ T=S ] mod-elim Î¼ t))
    â‰…Ë˜âŸ¨ mod-elim-cong Î¼ (mod-intro-Î¹ _) âŸ©
      mod-elim Î¼ (Î¹[ mod-cong T=S ] mod-intro Î¼ (mod-elim Î¼ t))
    â‰…âŸ¨ mod-elim-cong Î¼ (Î¹-cong (mod-Î· Î¼ t)) âŸ©
      mod-elim Î¼ (Î¹[ mod-cong T=S ] t) âˆ
    where open â‰…áµ—áµ-Reasoning


--------------------------------------------------
-- Behaviour of DRAs with respect to semantic closed types

module _ (Î¼ : Modality C D) {T : ClosedTy C} (clT : IsClosedNatural T) where
  mod-closed : IsClosedNatural âŸ¨ Î¼ âˆ£ T âŸ©
  IsClosedNatural.closed-natural mod-closed Ïƒ =
    transáµ—Ê¸ (mod-natural Î¼ Ïƒ) (mod-cong Î¼ (closed-natural clT (ctx-fmap (ctx-functor Î¼) Ïƒ)))
  IsClosedNatural.closed-id mod-closed =
    transáµ‰ (transáµ—Ê¸-congÊ³ (mod-cong-cong Î¼ (transáµ‰ (symáµ‰ (closed-subst-eq clT (lock-fmap-id Î¼)))
                                                   (transáµ—Ê¸-congÊ³ (closed-id clT)))))
           (mod-natural-id Î¼)
  IsClosedNatural.closed-âŠš mod-closed Ï„ Ïƒ  =
    transáµ‰ (transáµ‰ (transáµ—Ê¸-congË¡ ty-subst-cong-ty-trans) (transáµ‰ transáµ—Ê¸-assoc (transáµ‰ (transáµ—Ê¸-congÊ³ (symáµ‰ transáµ—Ê¸-assoc)) (transáµ‰ (transáµ—Ê¸-congÊ³ (transáµ—Ê¸-congË¡ (symáµ‰ (mod-natural-ty-eq Î¼ Ïƒ _)))) (transáµ‰ (transáµ—Ê¸-congÊ³ transáµ—Ê¸-assoc) (symáµ‰ transáµ—Ê¸-assoc))))))
           (transáµ‰ (transáµ—Ê¸-congÊ³ (transáµ‰ (symáµ‰ (mod-cong-trans Î¼)) (transáµ‰ (mod-cong-cong Î¼ (closed-âŠš clT _ _)) (mod-cong-trans Î¼))))
                   (transáµ‰ (transáµ‰ (transáµ‰ (symáµ‰ transáµ—Ê¸-assoc) (transáµ—Ê¸-congË¡ transáµ—Ê¸-assoc)) (transáµ—Ê¸-congË¡ (mod-natural-âŠš Î¼ Ï„ Ïƒ)))
                           (transáµ‰ (transáµ‰ transáµ—Ê¸-assoc (transáµ—Ê¸-congÊ³ transáµ—Ê¸-assoc)) (transáµ—Ê¸-congÊ³ (transáµ—Ê¸-congÊ³ (transáµ‰ (symáµ‰ (mod-cong-trans Î¼)) (mod-cong-cong Î¼ (closed-subst-eq clT (lock-fmap-âŠš Î¼ Ï„ Ïƒ)))))))))
  IsClosedNatural.closed-subst-eq mod-closed Îµ =
    transáµ‰ (symáµ‰ transáµ—Ê¸-assoc)
           (transáµ‰ (transáµ—Ê¸-congË¡ (mod-natural-subst-eq Î¼ Îµ))
                   (transáµ‰ transáµ—Ê¸-assoc
                           (transáµ—Ê¸-congÊ³ (transáµ‰ (symáµ‰ (mod-cong-trans Î¼))
                                                  (mod-cong-cong Î¼ (closed-subst-eq clT (lock-fmap-cong Î¼ Îµ)))))))

  mod-intro-cl-natural : {Î“ Î” : Ctx D} {Ïƒ : Î“ â‡’ Î”} (t : Tm (Î” ,lockâŸ¨ Î¼ âŸ©) T) â†’
                         (mod-intro Î¼ t) [ mod-closed âˆ£ Ïƒ ]cl â‰…áµ—áµ mod-intro Î¼ (t [ clT âˆ£ lock-fmap Î¼ Ïƒ ]cl)
  mod-intro-cl-natural t =
    transáµ—áµ (Î¹â»Â¹-cong (mod-intro-natural Î¼ _ t))
    (transáµ—áµ Î¹â»Â¹-trans
    (transáµ—áµ (Î¹â»Â¹-cong Î¹-symË¡)
    (transáµ—áµ (Î¹-congáµ‰ (symáµ‰ (mod-cong-sym Î¼))) (mod-intro-Î¹ Î¼ _))))

  mod-elim-cl-natural : {Î“ Î” : Ctx D} {Ïƒ : Î“ â‡’ Î”} (t : Tm Î” âŸ¨ Î¼ âˆ£ T âŸ©) â†’
                        (mod-elim Î¼ t) [ clT âˆ£ lock-fmap Î¼ Ïƒ ]cl â‰…áµ—áµ mod-elim Î¼ (t [ mod-closed âˆ£ Ïƒ ]cl)
  mod-elim-cl-natural t =
    transáµ—áµ (Î¹â»Â¹-cong (mod-elim-natural Î¼ _ t))
    (transáµ—áµ (mod-elim-Î¹ Î¼ _)
    (mod-elim-cong Î¼ (transáµ—áµ (Î¹-congáµ‰ (mod-cong-sym Î¼)) (symáµ—áµ Î¹â»Â¹-trans))))


--------------------------------------------------
-- Properties of modalities with respect to functions, products, ...

module _ (Î¼ : Modality C D) {Î“ : Ctx D} where

  module _ {T S : Ty (Î“ ,lockâŸ¨ Î¼ âŸ©)} where

    -- A modality is a functor.
    modality-map : Tm (Î“ ,lockâŸ¨ Î¼ âŸ©) (T â‡› S) â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ© â†’ Tm Î“ âŸ¨ Î¼ âˆ£ S âŸ©
    modality-map f t = mod-intro Î¼ (f $ mod-elim Î¼ t)

    infixl 12 modality-map
    syntax modality-map Î¼ f t = f âŸ¨$- Î¼ âŸ© t

    -- A modality is also an applicative functor.
    modality-âŠ› : Tm Î“ âŸ¨ Î¼ âˆ£ T â‡› S âŸ© â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ© â†’ Tm Î“ âŸ¨ Î¼ âˆ£ S âŸ©
    modality-âŠ› f t = mod-intro Î¼ (mod-elim Î¼ f $ mod-elim Î¼ t)

    infixl 12 modality-âŠ›
    syntax modality-âŠ› Î¼ f t = f âŠ›âŸ¨ Î¼ âŸ© t

    -- Modalities preserve products (up to isomorphism).
    from-mod-âŠ  : Tm Î“ âŸ¨ Î¼ âˆ£ T âŠ  S âŸ© â†’ Tm Î“ (âŸ¨ Î¼ âˆ£ T âŸ© âŠ  âŸ¨ Î¼ âˆ£ S âŸ©)
    from-mod-âŠ  p = pair (mod-intro Î¼ (fst (mod-elim Î¼ p))) (mod-intro Î¼ (snd (mod-elim Î¼ p)))

    to-mod-âŠ  : Tm Î“ (âŸ¨ Î¼ âˆ£ T âŸ© âŠ  âŸ¨ Î¼ âˆ£ S âŸ©) â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŠ  S âŸ©
    to-mod-âŠ  p = mod-intro Î¼ (pair (mod-elim Î¼ (fst p)) (mod-elim Î¼ (snd p)))

    from-to-mod-âŠ  : (p : Tm Î“ (âŸ¨ Î¼ âˆ£ T âŸ© âŠ  âŸ¨ Î¼ âˆ£ S âŸ©)) â†’ from-mod-âŠ  (to-mod-âŠ  p) â‰…áµ—áµ p
    from-to-mod-âŠ  p = let p' = pair (mod-elim Î¼ (fst p)) (mod-elim Î¼ (snd p)) in
      begin
        pair (mod-intro Î¼ (fst (mod-elim Î¼ (mod-intro Î¼ p'))))
             (mod-intro Î¼ (snd (mod-elim Î¼ (mod-intro Î¼ p'))))
      â‰…âŸ¨ pair-cong (mod-intro-cong Î¼ (fst-cong (mod-Î² Î¼ p')))
                   (mod-intro-cong Î¼ (snd-cong (mod-Î² Î¼ p'))) âŸ©
        pair (mod-intro Î¼ (fst p'))
             (mod-intro Î¼ (snd p'))
      â‰…âŸ¨ pair-cong (mod-intro-cong Î¼ (Î²-âŠ -fst _ (mod-elim Î¼ (snd p))))
                   (mod-intro-cong Î¼ (Î²-âŠ -snd (mod-elim Î¼ (fst p)) _)) âŸ©
        pair (mod-intro Î¼ (mod-elim Î¼ (fst p)))
             (mod-intro Î¼ (mod-elim Î¼ (snd p)))
      â‰…âŸ¨ pair-cong (mod-Î· Î¼ (fst p)) (mod-Î· Î¼ (snd p)) âŸ©
        pair (fst p)
             (snd p)
      â‰…Ë˜âŸ¨ Î·-âŠ  p âŸ©
        p âˆ
      where open â‰…áµ—áµ-Reasoning

    to-from-mod-âŠ  : (p : Tm Î“ âŸ¨ Î¼ âˆ£ T âŠ  S âŸ©) â†’ to-mod-âŠ  (from-mod-âŠ  p) â‰…áµ—áµ p
    to-from-mod-âŠ  p =
      let t = mod-intro Î¼ (fst (mod-elim Î¼ p))
          s = mod-intro Î¼ (snd (mod-elim Î¼ p))
      in begin
        mod-intro Î¼ (pair (mod-elim Î¼ (fst (pair t s)))
                          (mod-elim Î¼ (snd (pair t s))))
      â‰…âŸ¨ mod-intro-cong Î¼ (pair-cong (mod-elim-cong Î¼ (Î²-âŠ -fst t s))
                                     (mod-elim-cong Î¼ (Î²-âŠ -snd t s))) âŸ©
        mod-intro Î¼ (pair (mod-elim Î¼ t)
                          (mod-elim Î¼ s))
      â‰…âŸ¨ mod-intro-cong Î¼ (pair-cong (mod-Î² Î¼ _) (mod-Î² Î¼ _)) âŸ©
        mod-intro Î¼ (pair (fst (mod-elim Î¼ p))
                          (snd (mod-elim Î¼ p)))
      â‰…Ë˜âŸ¨ mod-intro-cong Î¼ (Î·-âŠ  (mod-elim Î¼ p)) âŸ©
        mod-intro Î¼ (mod-elim Î¼ p)
      â‰…âŸ¨ mod-Î· Î¼ p âŸ©
        p âˆ
      where open â‰…áµ—áµ-Reasoning

  -- Modalities preserve the unit type (up to isomorphism).
  mod-unit' : Tm Î“ âŸ¨ Î¼ âˆ£ Unit' âŸ©
  mod-unit' = mod-intro Î¼ tt'

  mod-unit'-Î· : (t : Tm Î“ âŸ¨ Î¼ âˆ£ Unit' âŸ©) â†’ t â‰…áµ—áµ mod-unit'
  mod-unit'-Î· t =
    begin
      t
    â‰…Ë˜âŸ¨ mod-Î· Î¼ t âŸ©
      mod-intro Î¼ (mod-elim Î¼ t)
    â‰…âŸ¨ mod-intro-cong Î¼ (Î·-unit (mod-elim Î¼ t)) âŸ©
      mod-intro Î¼ tt' âˆ
    where open â‰…áµ—áµ-Reasoning


--------------------------------------------------
-- Constructing new modalities

-- The unit modality
ğŸ™ : {C : BaseCategory} â†’ Modality C C
ctx-functor ğŸ™ = id-ctx-functor
âŸ¨ ğŸ™ âˆ£ T âŸ© = T
mod-map ğŸ™ Ï† = Ï†
mod-map-cong ğŸ™ ğ”¢ = ğ”¢
mod-map-id ğŸ™ = reflâ¿
mod-map-âŠ™ ğŸ™ = reflâ¿
mod-natural ğŸ™ Ïƒ = refláµ—Ê¸
mod-natural-map ğŸ™ Ïƒ Î· = transâ¿ id-trans-unitÊ³ (symâ¿ id-trans-unitË¡)
mod-natural-id-map ğŸ™ = transâ¿ id-trans-unitÊ³ (transâ¿ (âŠ™-congÊ³ ty-subst-eq-subst-morph-refl) id-trans-unitÊ³)
mod-natural-âŠš-map ğŸ™ Ï„ Ïƒ = transâ¿
  (transâ¿ (âŠ™-congÊ³ ty-subst-map-id) (transâ¿ id-trans-unitÊ³ id-trans-unitÊ³))
  (symâ¿ (transâ¿ (âŠ™-congË¡ (transâ¿ id-trans-unitÊ³ ty-subst-eq-subst-morph-refl)) id-trans-unitË¡))
mod-natural-subst-eq-map ğŸ™ Îµ = transâ¿ id-trans-unitË¡ (symâ¿ id-trans-unitÊ³)
mod-intro ğŸ™ t = t
mod-intro-cong ğŸ™ t=t' = t=t'
mod-intro-natural ğŸ™ Ïƒ t = symáµ—áµ Î¹-refl
mod-intro-convert ğŸ™ t = refláµ—áµ
mod-elim ğŸ™ t = t
mod-elim-cong ğŸ™ t=t' = t=t'
mod-Î² ğŸ™ t = refláµ—áµ
mod-Î· ğŸ™ t = refláµ—áµ


-- Composition of modalities
_â“œ_ : {C1 C2 C3 : BaseCategory} â†’ Modality C2 C3 â†’ Modality C1 C2 â†’ Modality C1 C3
ctx-functor (Î¼ â“œ Ï) = ctx-functor Ï â“• ctx-functor Î¼
âŸ¨ Î¼ â“œ Ï âˆ£ T âŸ© = âŸ¨ Î¼ âˆ£ âŸ¨ Ï âˆ£ T âŸ© âŸ©
mod-map (Î¼ â“œ Ï) Î· = mod-map Î¼ (mod-map Ï Î·)
mod-map-cong (Î¼ â“œ Ï) ğ”¢ = mod-map-cong Î¼ (mod-map-cong Ï ğ”¢)
mod-map-id (Î¼ â“œ Ï) = transâ¿ (mod-map-cong Î¼ (mod-map-id Ï)) (mod-map-id Î¼)
mod-map-âŠ™ (Î¼ â“œ Ï) = transâ¿ (mod-map-cong Î¼ (mod-map-âŠ™ Ï)) (mod-map-âŠ™ Î¼)
mod-natural (Î¼ â“œ Ï) Ïƒ = transáµ—Ê¸ (mod-natural Î¼ Ïƒ) (mod-cong Î¼ (mod-natural Ï _))
mod-natural-map (Î¼ â“œ Ï) Ïƒ Î· =
    transâ¿ (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (symâ¿ (mod-map-âŠ™ Î¼)))) (
  transâ¿ (âŠ™-congË¡ (mod-map-cong Î¼ (mod-natural-map Ï _ Î·))) (
    transâ¿ (transâ¿ (âŠ™-congË¡ (mod-map-âŠ™ Î¼)) âŠ™-assoc) (
  transâ¿ (âŠ™-congÊ³ (mod-natural-map Î¼ Ïƒ (mod-map Ï Î·)))
    (symâ¿ âŠ™-assoc))))
mod-natural-id-map (Î¼ â“œ Ï) =
  transâ¿ (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (symâ¿ (mod-map-âŠ™ Î¼)))) (
      transâ¿ (âŠ™-congË¡ (mod-map-cong Î¼ (
          transâ¿ (âŠ™-congË¡ (transâ¿ (mod-map-cong Ï (transâ¿ (âŠ™-congÊ³ ty-subst-eq-subst-morph-trans) (symâ¿ âŠ™-assoc))) (mod-map-âŠ™ Ï))) (
        transâ¿ âŠ™-assoc (
          transâ¿ (âŠ™-congÊ³ (symâ¿ (mod-natural-subst-eq-map Ï _))) (
        transâ¿ (symâ¿ âŠ™-assoc) (
          âŠ™-congË¡ (mod-natural-id-map Ï)))))))) (
  mod-natural-id-map Î¼))
mod-natural-âŠš-map (Î¼ â“œ Ï) Ï„ Ïƒ =
    transâ¿ (transâ¿ (âŠ™-congÊ³ ty-subst-map-âŠ™) (transâ¿ (âŠ™-congË¡ (symâ¿ âŠ™-assoc)) (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ âŠ™-assoc)))) (
  transâ¿ (âŠ™-congË¡ (âŠ™-congÊ³ (symâ¿ (mod-natural-map Î¼ Ïƒ _)))) (
    transâ¿ (âŠ™-congË¡ (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (transâ¿ (âŠ™-congË¡ (symâ¿ (mod-map-âŠ™ Î¼))) (symâ¿ (mod-map-âŠ™ Î¼)))))) (
  transâ¿ (âŠ™-congË¡ (âŠ™-congË¡ (mod-map-cong Î¼ (mod-natural-âŠš-map Ï _ _)))) (
    transâ¿ (transâ¿ (âŠ™-congË¡ (âŠ™-congË¡ (mod-map-âŠ™ Î¼))) (transâ¿ (âŠ™-congË¡ âŠ™-assoc) âŠ™-assoc)) (
  transâ¿ (âŠ™-congÊ³ (mod-natural-âŠš-map Î¼ Ï„ Ïƒ)) (
    transâ¿ (transâ¿ (âŠ™-congÊ³ âŠ™-assoc) (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (transâ¿ (symâ¿ (mod-map-âŠ™ Î¼)) (mod-map-cong Î¼ âŠ™-assoc))))) (
  transâ¿ (âŠ™-congË¡ (mod-map-cong Î¼ (âŠ™-congÊ³ (mod-natural-subst-eq-map Ï _)))) (
    transâ¿ (âŠ™-congË¡ (transâ¿ (mod-map-cong Î¼ (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (transâ¿ (symâ¿ (mod-map-âŠ™ Ï)) (mod-map-cong Ï (symâ¿ ty-subst-eq-subst-morph-trans)))))) (mod-map-âŠ™ Î¼)))
    (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ âŠ™-assoc))))))))))
mod-natural-subst-eq-map (Î¼ â“œ Ï) Îµ =
    transâ¿ âŠ™-assoc (
  transâ¿ (âŠ™-congÊ³ (mod-natural-subst-eq-map Î¼ Îµ)) (
    transâ¿ (transâ¿ (symâ¿ âŠ™-assoc) (âŠ™-congË¡ (symâ¿ (mod-map-âŠ™ Î¼)))) (
  transâ¿ (âŠ™-congË¡ (mod-map-cong Î¼ (mod-natural-subst-eq-map Ï _)))
    (transâ¿ (âŠ™-congË¡ (mod-map-âŠ™ Î¼)) âŠ™-assoc))))
mod-intro (Î¼ â“œ Ï) t = mod-intro Î¼ (mod-intro Ï t)
mod-intro-cong (Î¼ â“œ Ï) e = mod-intro-cong Î¼ (mod-intro-cong Ï e)
mod-intro-natural (Î¼ â“œ Ï) Ïƒ t = begin
    (mod-intro Î¼ (mod-intro Ï t)) [ Ïƒ ]'
  â‰…âŸ¨ mod-intro-natural Î¼ Ïƒ (mod-intro Ï t) âŸ©
    Î¹[ mod-natural Î¼ Ïƒ ] mod-intro Î¼ ((mod-intro Ï t) [ lock-fmap Î¼ Ïƒ ]')
  â‰…âŸ¨ Î¹-cong (mod-intro-cong Î¼ (mod-intro-natural Ï (lock-fmap Î¼ Ïƒ) t)) âŸ©
    Î¹[ mod-natural Î¼ Ïƒ ] mod-intro Î¼ (Î¹[ mod-natural Ï _ ] mod-intro Ï (t [ lock-fmap Ï (lock-fmap Î¼ Ïƒ) ]'))
  â‰…Ë˜âŸ¨ Î¹-cong (mod-intro-Î¹ Î¼ _) âŸ©
    Î¹[ mod-natural Î¼ Ïƒ ] (Î¹[ mod-cong Î¼ (mod-natural Ï _) ] mod-intro Î¼ (mod-intro Ï (t [ lock-fmap Ï (lock-fmap Î¼ Ïƒ) ]')))
  â‰…Ë˜âŸ¨ Î¹-trans âŸ©
    Î¹[ transáµ—Ê¸ (mod-natural Î¼ Ïƒ) (mod-cong Î¼ (mod-natural Ï (lock-fmap Î¼ Ïƒ))) ] mod-intro Î¼ (mod-intro Ï (t [ lock-fmap Ï (lock-fmap Î¼ Ïƒ) ]')) âˆ
  where open â‰…áµ—áµ-Reasoning
mod-intro-convert (Î¼ â“œ Ï) t = transáµ—áµ (mod-intro-convert Î¼ (mod-intro Ï t)) (mod-intro-cong Î¼ (mod-intro-convert Ï t))
mod-elim (Î¼ â“œ Ï) t = mod-elim Ï (mod-elim Î¼ t)
mod-elim-cong (Î¼ â“œ Ï) e = mod-elim-cong Ï (mod-elim-cong Î¼ e)
mod-Î² (Î¼ â“œ Ï) t = transáµ—áµ (mod-elim-cong Ï (mod-Î² Î¼ _)) (mod-Î² Ï t)
mod-Î· (Î¼ â“œ Ï) t = transáµ—áµ (mod-intro-cong Î¼ (mod-Î· Ï _)) (mod-Î· Î¼ t)


-- The unit modality or composition of modalities preserve the
-- structure of closed types being natural.
mod-cong-ğŸ™ : {Î“ : Ctx C} {T S : Ty Î“} {e : T â‰…áµ—Ê¸ S} â†’ mod-cong ğŸ™ e â‰…áµ‰ e
from-eq mod-cong-ğŸ™ = reflâ¿

mod-cong-â“œ : {Ï : Modality C D} {Î¼ : Modality D E} {Î“ : Ctx E} {T S : Ty (Î“ ,lockâŸ¨ Î¼ âŸ© ,lockâŸ¨ Ï âŸ©)} {e : T â‰…áµ—Ê¸ S} â†’
             mod-cong (Î¼ â“œ Ï) e â‰…áµ‰ mod-cong Î¼ (mod-cong Ï e)
from-eq mod-cong-â“œ = reflâ¿

ğŸ™-preserves-cl : {A : ClosedTy C} (clA : IsClosedNatural A) â†’ mod-closed ğŸ™ clA â‰…á¶œáµ—Ê¸ clA
closed-natural-eq (ğŸ™-preserves-cl clA) Ïƒ = transáµ‰ refláµ—Ê¸-unitË¡ mod-cong-ğŸ™

â“œ-preserves-cl : (Î¼ : Modality D E) (Ï : Modality C D) {A : ClosedTy C} (clA : IsClosedNatural A) â†’
                 mod-closed (Î¼ â“œ Ï) clA â‰…á¶œáµ—Ê¸ mod-closed Î¼ (mod-closed Ï clA)
closed-natural-eq (â“œ-preserves-cl Î¼ Ï clA) Ïƒ =
  transáµ‰ transáµ—Ê¸-assoc (transáµ—Ê¸-congÊ³ (transáµ‰ (transáµ—Ê¸-congÊ³ mod-cong-â“œ) (symáµ‰ (mod-cong-trans Î¼))))


--------------------------------------------------
-- Equivalence of modalities

record _â‰…áµ_  {C D} (Î¼ Ï : Modality C D) : Setâ‚ where
  field
    eq-lock : (Î“ : Ctx D) â†’ Î“ ,lockâŸ¨ Î¼ âŸ© â‰…á¶œ Î“ ,lockâŸ¨ Ï âŸ©
    eq-lock-natural-to : {Î” Î“ : Ctx D} (Ïƒ : Î” â‡’ Î“) â†’
                         to (eq-lock Î“) âŠš lock-fmap Ï Ïƒ â‰…Ë¢ lock-fmap Î¼ Ïƒ âŠš to (eq-lock Î”)
    eq-mod-tyÊ³ : {Î“ : Ctx D} (T : Ty (Î“ ,lockâŸ¨ Î¼ âŸ©)) â†’ âŸ¨ Î¼ âˆ£ T âŸ© â‰…áµ—Ê¸ âŸ¨ Ï âˆ£ T [ to (eq-lock Î“) ] âŸ©

    -- In the future, we will probably need an equivalence requirement for the modal term former,
    --  such as the following. For simplicity, we currently omit this.
    {-eq-mod-introÊ³ : {Î“ : Ctx D} {T : Ty (lock Î¼ Î“)} (t : Tm (lock Î¼ Î“) T) â†’
                   mod-intro Î¼ t â‰…áµ—áµ Î¹[ eq-mod-tyÊ³ T ] mod-intro Ï (t [ to (eq-lock Î“) ]')-}

  eq-lock-natural-from : {Î” Î“ : Ctx D} (Ïƒ : Î” â‡’ Î“) â†’
                         from (eq-lock Î“) âŠš lock-fmap Î¼ Ïƒ â‰…Ë¢ lock-fmap Ï Ïƒ âŠš from (eq-lock Î”)
  eq-lock-natural-from {Î”} {Î“} Ïƒ = begin
      from (eq-lock Î“) âŠš lock-fmap Î¼ Ïƒ
    â‰…Ë˜âŸ¨ id-subst-unitÊ³ _ âŸ©
      (from (eq-lock Î“) âŠš lock-fmap Î¼ Ïƒ) âŠš id-subst (lock Î¼ Î”)
    â‰…Ë˜âŸ¨ âŠš-congÊ³ (isoË¡ (eq-lock Î”)) âŸ©
      (from (eq-lock Î“) âŠš lock-fmap Î¼ Ïƒ) âŠš (to (eq-lock Î”) âŠš from (eq-lock Î”))
    â‰…Ë˜âŸ¨ âŠš-assoc âŸ©
      ((from (eq-lock Î“) âŠš lock-fmap Î¼ Ïƒ) âŠš to (eq-lock Î”)) âŠš from (eq-lock Î”)
    â‰…âŸ¨ âŠš-congË¡ âŠš-assoc âŸ©
      (from (eq-lock Î“) âŠš (lock-fmap Î¼ Ïƒ âŠš to (eq-lock Î”))) âŠš from (eq-lock Î”)
    â‰…Ë˜âŸ¨ âŠš-congË¡ (âŠš-congÊ³ (eq-lock-natural-to Ïƒ)) âŸ©
      (from (eq-lock Î“) âŠš (to (eq-lock Î“) âŠš lock-fmap Ï Ïƒ)) âŠš from (eq-lock Î”)
    â‰…Ë˜âŸ¨ âŠš-congË¡ âŠš-assoc âŸ©
      ((from (eq-lock Î“) âŠš to (eq-lock Î“)) âŠš lock-fmap Ï Ïƒ) âŠš from (eq-lock Î”)
    â‰…âŸ¨ âŠš-congË¡ (âŠš-congË¡ (isoÊ³ (eq-lock Î“))) âŸ©
      (id-subst (lock Ï Î“) âŠš lock-fmap Ï Ïƒ) âŠš from (eq-lock Î”)
    â‰…âŸ¨ âŠš-congË¡ (id-subst-unitË¡ _) âŸ©
      lock-fmap Ï Ïƒ âŠš from (eq-lock Î”) âˆ
    where open â‰…Ë¢-Reasoning

  eq-mod-tyË¡ : {Î“ : Ctx D} (T : Ty (lock Ï Î“)) â†’ âŸ¨ Î¼ âˆ£ T [ from (eq-lock Î“) ] âŸ© â‰…áµ—Ê¸ âŸ¨ Ï âˆ£ T âŸ©
  eq-mod-tyË¡ {Î“ = Î“} T = begin
      âŸ¨ Î¼ âˆ£ T [ from (eq-lock Î“) ] âŸ©
    â‰…âŸ¨ eq-mod-tyÊ³ (T [ from (eq-lock Î“) ]) âŸ©
      âŸ¨ Ï âˆ£ (T [ from (eq-lock Î“) ]) [ to (eq-lock Î“) ] âŸ©
    â‰…âŸ¨ mod-cong Ï (ty-subst-seq-cong (from (eq-lock Î“) âˆ· to (eq-lock Î“) â—¼) (id-subst _ â—¼) T (isoÊ³ (eq-lock Î“))) âŸ©
      âŸ¨ Ï âˆ£ T [ id-subst (Î“ ,lockâŸ¨ Ï âŸ©) ] âŸ©
    â‰…âŸ¨ mod-cong Ï (ty-subst-id T) âŸ©
      âŸ¨ Ï âˆ£ T âŸ© âˆ
    where open â‰…áµ—Ê¸-Reasoning

  eq-mod-closed : {A : ClosedTy C} â†’ IsClosedNatural A â†’ {Î“ : Ctx D} â†’ âŸ¨ Î¼ âˆ£ A {Î“ ,lockâŸ¨ Î¼ âŸ©} âŸ© â‰…áµ—Ê¸ âŸ¨ Ï âˆ£ A âŸ©
  eq-mod-closed {A = A} clA = begin
      âŸ¨ Î¼ âˆ£ A âŸ©
    â‰…âŸ¨ eq-mod-tyÊ³ A âŸ©
      âŸ¨ Ï âˆ£ A [ to (eq-lock _) ] âŸ©
    â‰…âŸ¨ mod-cong Ï (closed-natural clA (to (eq-lock _))) âŸ©
      âŸ¨ Ï âˆ£ A âŸ© âˆ
    where open â‰…áµ—Ê¸-Reasoning

open _â‰…áµ_ public

refláµ : âˆ€ {C D} â†’ {Î¼ : Modality C D} â†’ Î¼ â‰…áµ Î¼
eq-lock (refláµ {Î¼ = Î¼}) Î“ = reflá¶œ
eq-lock-natural-to (refláµ {Î¼ = Î¼}) Ïƒ = transË¢ (id-subst-unitË¡ _) (symË¢ (id-subst-unitÊ³ _))
eq-mod-tyÊ³ (refláµ {Î¼ = Î¼}) T = mod-cong Î¼ (symáµ—Ê¸ (ty-subst-id T))

symáµ : âˆ€ {C D} {Î¼ Ï : Modality C D} â†’ Î¼ â‰…áµ Ï â†’ Ï â‰…áµ Î¼
eq-lock (symáµ e) Î“ = symá¶œ (eq-lock e Î“)
eq-lock-natural-to (symáµ e) Ïƒ = eq-lock-natural-from e Ïƒ
eq-mod-tyÊ³ (symáµ e) T = symáµ—Ê¸ (eq-mod-tyË¡ e T)

transáµ : âˆ€ {C D} {Î¼ Ï Îº : Modality C D} â†’ Î¼ â‰…áµ Ï â†’ Ï â‰…áµ Îº â†’ Î¼ â‰…áµ Îº
eq-lock (transáµ Î¼=Ï Ï=Îº) Î“ = transá¶œ (eq-lock Î¼=Ï Î“) (eq-lock Ï=Îº Î“)
eq-lock-natural-to (transáµ {Î¼ = Î¼} {Ï} {Îº} Î¼=Ï Ï=Îº) Ïƒ = begin
    (to (eq-lock Î¼=Ï _) âŠš to (eq-lock Ï=Îº _)) âŠš lock-fmap Îº Ïƒ
  â‰…âŸ¨ âŠš-assoc âŸ©
    to (eq-lock Î¼=Ï _) âŠš (to (eq-lock Ï=Îº _) âŠš lock-fmap Îº Ïƒ)
  â‰…âŸ¨ âŠš-congÊ³ (eq-lock-natural-to Ï=Îº Ïƒ) âŸ©
    to (eq-lock Î¼=Ï _) âŠš (lock-fmap Ï Ïƒ âŠš to (eq-lock Ï=Îº _))
  â‰…Ë˜âŸ¨ âŠš-assoc âŸ©
    (to (eq-lock Î¼=Ï _) âŠš lock-fmap Ï Ïƒ) âŠš to (eq-lock Ï=Îº _)
  â‰…âŸ¨ âŠš-congË¡ (eq-lock-natural-to Î¼=Ï Ïƒ) âŸ©
    (lock-fmap Î¼ Ïƒ âŠš to (eq-lock Î¼=Ï _)) âŠš to (eq-lock Ï=Îº _)
  â‰…âŸ¨ âŠš-assoc âŸ©
    lock-fmap Î¼ Ïƒ âŠš (to (eq-lock Î¼=Ï _) âŠš to (eq-lock Ï=Îº _)) âˆ
  where open â‰…Ë¢-Reasoning
eq-mod-tyÊ³ (transáµ {Î¼ = Î¼} {Ï = Ï} {Îº = Îº} Î¼=Ï Ï=Îº) {Î“ = Î“} T = begin
    âŸ¨ Î¼ âˆ£ T âŸ©
  â‰…âŸ¨ eq-mod-tyÊ³ Î¼=Ï T âŸ©
    âŸ¨ Ï âˆ£ T [ to (eq-lock Î¼=Ï Î“) ] âŸ©
  â‰…âŸ¨ eq-mod-tyÊ³ Ï=Îº (T [ to (eq-lock Î¼=Ï Î“) ]) âŸ©
    âŸ¨ Îº âˆ£ (T [ to (eq-lock Î¼=Ï Î“) ]) [ to (eq-lock Ï=Îº Î“) ] âŸ©
  â‰…âŸ¨ mod-cong Îº (ty-subst-comp T _ _) âŸ©
    âŸ¨ Îº âˆ£ T [ to (eq-lock Î¼=Ï Î“) âŠš to (eq-lock Ï=Îº Î“) ] âŸ© âˆ
  where open â‰…áµ—Ê¸-Reasoning

ğŸ™-unitÊ³ : (Î¼ : Modality C D) â†’ Î¼ â“œ ğŸ™ â‰…áµ Î¼
eq-lock (ğŸ™-unitÊ³ Î¼) Î“ = reflá¶œ
eq (eq-lock-natural-to (ğŸ™-unitÊ³ Î¼) Ïƒ) _ = refl
eq-mod-tyÊ³ (ğŸ™-unitÊ³ Î¼) T = symáµ—Ê¸ (mod-cong Î¼ (ty-subst-id T))

ğŸ™-unitË¡ : (Î¼ : Modality C D) â†’ ğŸ™ â“œ Î¼ â‰…áµ Î¼
eq-lock (ğŸ™-unitË¡ Î¼) Î“ = reflá¶œ
eq (eq-lock-natural-to (ğŸ™-unitË¡ Î¼) Ïƒ) _ = refl
eq-mod-tyÊ³ (ğŸ™-unitË¡ Î¼) T = symáµ—Ê¸ (mod-cong Î¼ (ty-subst-id T))

â“œ-assoc : {Câ‚ Câ‚‚ Câ‚ƒ Câ‚„ : BaseCategory}
           (Î¼â‚ƒâ‚„ : Modality Câ‚ƒ Câ‚„) (Î¼â‚‚â‚ƒ : Modality Câ‚‚ Câ‚ƒ) (Î¼â‚â‚‚ : Modality Câ‚ Câ‚‚) â†’
           (Î¼â‚ƒâ‚„ â“œ Î¼â‚‚â‚ƒ) â“œ Î¼â‚â‚‚ â‰…áµ Î¼â‚ƒâ‚„ â“œ (Î¼â‚‚â‚ƒ â“œ Î¼â‚â‚‚)
eq-lock (â“œ-assoc Î¼â‚ƒâ‚„ Î¼â‚‚â‚ƒ Î¼â‚â‚‚) Î“ = reflá¶œ
eq (eq-lock-natural-to (â“œ-assoc Î¼â‚ƒâ‚„ Î¼â‚‚â‚ƒ Î¼â‚â‚‚) Ïƒ) _ = refl
eq-mod-tyÊ³ (â“œ-assoc Î¼â‚ƒâ‚„ Î¼â‚‚â‚ƒ Î¼â‚â‚‚) T = symáµ—Ê¸ (mod-cong Î¼â‚ƒâ‚„ (mod-cong Î¼â‚‚â‚ƒ (mod-cong Î¼â‚â‚‚ (ty-subst-id T))))

â“œ-congÊ³ : (Ï : Modality D E) {Î¼ Î¼' : Modality C D} â†’ Î¼ â‰…áµ Î¼' â†’ Ï â“œ Î¼ â‰…áµ Ï â“œ Î¼'
eq-lock (â“œ-congÊ³ Ï Î¼=Î¼') Î“ = eq-lock Î¼=Î¼' (Î“ ,lockâŸ¨ Ï âŸ©)
eq-lock-natural-to (â“œ-congÊ³ Ï {Î¼} {Î¼'} Î¼=Î¼') Ïƒ = eq-lock-natural-to Î¼=Î¼' (lock-fmap Ï Ïƒ)
eq-mod-tyÊ³ (â“œ-congÊ³ Ï Î¼=Î¼') T = mod-cong Ï (eq-mod-tyÊ³ Î¼=Î¼' T)

â“œ-congË¡ : {Ï Ï' : Modality D E} (Î¼ : Modality C D) â†’ Ï â‰…áµ Ï' â†’ Ï â“œ Î¼ â‰…áµ Ï' â“œ Î¼
from (eq-lock (â“œ-congË¡ Î¼ Ï=Ï') Î“) = lock-fmap Î¼ (from (eq-lock Ï=Ï' Î“))
to (eq-lock (â“œ-congË¡ Î¼ Ï=Ï') Î“) = lock-fmap Î¼ (to (eq-lock Ï=Ï' Î“))
isoË¡ (eq-lock (â“œ-congË¡ Î¼ Ï=Ï') Î“) = ctx-fmap-inverse (ctx-functor Î¼) (isoË¡ (eq-lock Ï=Ï' Î“))
isoÊ³ (eq-lock (â“œ-congË¡ Î¼ Ï=Ï') Î“) = ctx-fmap-inverse (ctx-functor Î¼) (isoÊ³ (eq-lock Ï=Ï' Î“))
eq-lock-natural-to (â“œ-congË¡ {Ï = Ï} {Ï'} Î¼ Ï=Ï') Ïƒ = begin
    lock-fmap Î¼ (to (eq-lock Ï=Ï' _)) âŠš lock-fmap Î¼ (lock-fmap Ï' Ïƒ)
  â‰…Ë˜âŸ¨ lock-fmap-âŠš Î¼ _ _ âŸ©
    lock-fmap Î¼ (to (eq-lock Ï=Ï' _) âŠš lock-fmap Ï' Ïƒ)
  â‰…âŸ¨ lock-fmap-cong Î¼ (eq-lock-natural-to Ï=Ï' Ïƒ) âŸ©
    lock-fmap Î¼ (lock-fmap Ï Ïƒ âŠš to (eq-lock Ï=Ï' _))
  â‰…âŸ¨ lock-fmap-âŠš Î¼ _ _ âŸ©
    lock-fmap Î¼ (lock-fmap Ï Ïƒ) âŠš lock-fmap Î¼ (to (eq-lock Ï=Ï' _)) âˆ
  where open â‰…Ë¢-Reasoning
eq-mod-tyÊ³ (â“œ-congË¡ {Ï = Ï} {Ï' = Ï'} Î¼ Ï=Ï') {Î“ = Î“} T = begin
    âŸ¨ Ï âˆ£ âŸ¨ Î¼ âˆ£ T âŸ© âŸ©
  â‰…âŸ¨ eq-mod-tyÊ³ Ï=Ï' âŸ¨ Î¼ âˆ£ T âŸ© âŸ©
    âŸ¨ Ï' âˆ£ âŸ¨ Î¼ âˆ£ T âŸ© [ to (eq-lock Ï=Ï' Î“) ] âŸ©
  â‰…âŸ¨ mod-cong Ï' (mod-natural Î¼ (to (eq-lock Ï=Ï' Î“))) âŸ©
    âŸ¨ Ï' âˆ£ âŸ¨ Î¼ âˆ£ T [ lock-fmap Î¼ (to (eq-lock Ï=Ï' Î“)) ] âŸ© âŸ© âˆ
  where open â‰…áµ—Ê¸-Reasoning

module â‰…áµ-Reasoning where
  infix  3 _âˆ
  infixr 2 _â‰…âŸ¨âŸ©_ step-â‰… step-â‰…Ë˜
  infix  1 begin_

  begin_ : âˆ€ {Î¼ Ï : Modality C D} â†’ Î¼ â‰…áµ Ï â†’ Î¼ â‰…áµ Ï
  begin_ Î¼=Ï = Î¼=Ï

  _â‰…âŸ¨âŸ©_ : âˆ€ (Î¼ {Ï} : Modality C D) â†’ Î¼ â‰…áµ Ï â†’ Î¼ â‰…áµ Ï
  _ â‰…âŸ¨âŸ© Î¼=Ï = Î¼=Ï

  step-â‰… : âˆ€ (Î¼ {Ï Îº} : Modality C D) â†’ Ï â‰…áµ Îº â†’ Î¼ â‰…áµ Ï â†’ Î¼ â‰…áµ Îº
  step-â‰… _ Ïâ‰…Îº Î¼â‰…Ï = transáµ Î¼â‰…Ï Ïâ‰…Îº

  step-â‰…Ë˜ : âˆ€ (Î¼ {Ï Îº} : Modality C D) â†’ Ï â‰…áµ Îº â†’ Ï â‰…áµ Î¼ â†’ Î¼ â‰…áµ Îº
  step-â‰…Ë˜ _ Ïâ‰…Îº Ïâ‰…Î¼ = transáµ (symáµ Ïâ‰…Î¼) Ïâ‰…Îº

  _âˆ : âˆ€ (Î¼ : Modality C D) â†’ Î¼ â‰…áµ Î¼
  _âˆ _ = refláµ

  syntax step-â‰…  Î¼ Ïâ‰…Îº Î¼â‰…Ï = Î¼ â‰…âŸ¨  Î¼â‰…Ï âŸ© Ïâ‰…Îº
  syntax step-â‰…Ë˜ Î¼ Ïâ‰…Îº Ïâ‰…Î¼ = Î¼ â‰…Ë˜âŸ¨ Ïâ‰…Î¼ âŸ© Ïâ‰…Îº


--------------------------------------------------
-- Two-cells between modalities as natural transformations
--   between the underlying context functors

-- TwoCell is defined as a record type so that Agda can better infer the two endpoint
--   modalities, e.g. in coe-ty.
record TwoCell (Î¼ Ï : Modality C D) : Setâ‚ where
  constructor two-cell
  field
    transf : CtxNatTransf (ctx-functor Ï) (ctx-functor Î¼)

  key-subst : {Î“ : Ctx D} â†’ Î“ ,lockâŸ¨ Ï âŸ© â‡’ Î“ ,lockâŸ¨ Î¼ âŸ©
  key-subst {Î“ = Î“} = transf-op transf Î“

  key-subst-natural : {Î“ Î” : Ctx D} {Ïƒ : Î“ â‡’ Î”} â†’ key-subst {Î”} âŠš lock-fmap Ï Ïƒ â‰…Ë¢ lock-fmap Î¼ Ïƒ âŠš key-subst {Î“}
  key-subst-natural {Ïƒ = Ïƒ} = naturality transf Ïƒ

  coe-ty : {Î“ : Ctx D} â†’ Ty (Î“ ,lockâŸ¨ Î¼ âŸ©) â†’ Ty (Î“ ,lockâŸ¨ Ï âŸ©)
  coe-ty {Î“} T = T [ key-subst ]

  coe : {Î“ : Ctx D} {T : Ty (Î“ ,lockâŸ¨ Î¼ âŸ©)} â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ© â†’ Tm Î“ âŸ¨ Ï âˆ£ coe-ty T âŸ©
  coe t = mod-intro Ï ((mod-elim Î¼ t) [ key-subst ]')

  coe-closed : {T : ClosedTy C} â†’ IsClosedNatural T â†’ {Î“ : Ctx D} â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ© â†’ Tm Î“ âŸ¨ Ï âˆ£ T âŸ©
  coe-closed {T = T} clT t = Î¹â»Â¹[ mod-cong Ï (closed-natural clT key-subst) ] coe t

open TwoCell public


-- The identity 2-cell.
id-cell : {Î¼ : Modality C D} â†’ TwoCell Î¼ Î¼
transf id-cell = id-ctx-transf _

-- Composition of 2-cells (both vertical and horizontal)
_â“£-vert_ : {Î¼ Ï Îº : Modality C D} â†’ TwoCell Î¼ Ï â†’ TwoCell Îº Î¼ â†’ TwoCell Îº Ï
transf (Î± â“£-vert Î²) = transf Î² â“-vert transf Î±

_â“£-hor_ : {Î¼ Î¼' : Modality D E} {Ï Ï' : Modality C D} â†’ TwoCell Î¼ Î¼' â†’ TwoCell Ï Ï' â†’ TwoCell (Î¼ â“œ Ï) (Î¼' â“œ Ï')
transf (Î± â“£-hor Î²) = transf Î² â“-hor transf Î±

-- An equivalence of modalities gives rise to an invertible 2-cell.
â‰…áµ-to-2-cell : {Î¼ Ï : Modality C D} â†’ Î¼ â‰…áµ Ï â†’ TwoCell Î¼ Ï
transf-op (transf (â‰…áµ-to-2-cell Î¼=Ï)) Î“ = to (eq-lock Î¼=Ï Î“)
naturality (transf (â‰…áµ-to-2-cell Î¼=Ï)) = eq-lock-natural-to Î¼=Ï


record _â‰…áµ—á¶œ_ {Î¼ Ï : Modality C D} (Î± Î² : TwoCell Î¼ Ï) : Setâ‚ where
  field
    key-subst-eq : âˆ€ {Î“} â†’ key-subst Î± {Î“} â‰…Ë¢ key-subst Î²
open _â‰…áµ—á¶œ_ public

module _ {Î¼ Ï : Modality C D} where
  refláµ—á¶œ : {Î± : TwoCell Î¼ Ï} â†’ Î± â‰…áµ—á¶œ Î±
  key-subst-eq refláµ—á¶œ = reflË¢

  symáµ—á¶œ : {Î± Î² : TwoCell Î¼ Ï} â†’ Î± â‰…áµ—á¶œ Î² â†’ Î² â‰…áµ—á¶œ Î±
  key-subst-eq (symáµ—á¶œ Î±=Î²) = symË¢ (key-subst-eq Î±=Î²)

  transáµ—á¶œ : {Î±1 Î±2 Î±3 : TwoCell Î¼ Ï} â†’ Î±1 â‰…áµ—á¶œ Î±2 â†’ Î±2 â‰…áµ—á¶œ Î±3 â†’ Î±1 â‰…áµ—á¶œ Î±3
  key-subst-eq (transáµ—á¶œ e e') = transË¢ (key-subst-eq e) (key-subst-eq e')

  â“£-vert-unitË¡ : {Î± : TwoCell Î¼ Ï} â†’ id-cell â“£-vert Î± â‰…áµ—á¶œ Î±
  key-subst-eq â“£-vert-unitË¡ = id-subst-unitÊ³ _

  â“£-vert-unitÊ³ : {Î± : TwoCell Î¼ Ï} â†’ Î± â“£-vert id-cell â‰…áµ—á¶œ Î±
  key-subst-eq â“£-vert-unitÊ³ = id-subst-unitË¡ _

â“£-vert-assoc : {Î¼ Ï Îº Ï† : Modality C D} {Î± : TwoCell Î¼ Ï} {Î² : TwoCell Ï Îº} {Î³ : TwoCell Îº Ï†} â†’
               (Î³ â“£-vert Î²) â“£-vert Î± â‰…áµ—á¶œ Î³ â“£-vert (Î² â“£-vert Î±)
key-subst-eq â“£-vert-assoc = symË¢ âŠš-assoc

â“£-vert-congË¡ : {Î¼ Ï Îº : Modality C D} {Î± Î±' : TwoCell Ï Îº} {Î² : TwoCell Î¼ Ï} â†’
               Î± â‰…áµ—á¶œ Î±' â†’ Î± â“£-vert Î² â‰…áµ—á¶œ Î±' â“£-vert Î²
key-subst-eq (â“£-vert-congË¡ e) = âŠš-congÊ³ (key-subst-eq e)

â“£-vert-congÊ³ : {Î¼ Ï Îº : Modality C D} {Î± : TwoCell Ï Îº} {Î² Î²' : TwoCell Î¼ Ï} â†’
               Î² â‰…áµ—á¶œ Î²' â†’ Î± â“£-vert Î² â‰…áµ—á¶œ Î± â“£-vert Î²'
key-subst-eq (â“£-vert-congÊ³ e) = âŠš-congË¡ (key-subst-eq e)

â“£-hor-congË¡ : {Î¼ Ï : Modality C D} {Îº Ï† : Modality D E} {Î± : TwoCell Î¼ Ï} {Î² Î²' : TwoCell Îº Ï†} â†’
              Î² â‰…áµ—á¶œ Î²' â†’ Î² â“£-hor Î± â‰…áµ—á¶œ Î²' â“£-hor Î±
key-subst-eq (â“£-hor-congË¡ {Ï = Ï} e) = âŠš-congÊ³ (lock-fmap-cong Ï (key-subst-eq e))

â“£-hor-congÊ³ : {Î¼ Ï : Modality C D} {Îº Ï† : Modality D E} {Î± Î±' : TwoCell Î¼ Ï} {Î² : TwoCell Îº Ï†} â†’
              Î± â‰…áµ—á¶œ Î±' â†’ Î² â“£-hor Î± â‰…áµ—á¶œ Î² â“£-hor Î±'
key-subst-eq (â“£-hor-congÊ³ e) = âŠš-congË¡ (key-subst-eq e)

â“£-hor-id : {Î¼ : Modality C D} {Ï : Modality D E} â†’ id-cell {Î¼ = Ï} â“£-hor id-cell {Î¼ = Î¼} â‰…áµ—á¶œ id-cell
key-subst-eq (â“£-hor-id {Î¼ = Î¼}) = transË¢ (id-subst-unitË¡ _) (lock-fmap-id Î¼)

2-cell-interchange : {Î¼ Î¼' Î¼'' : Modality D E} {Ï Ï' Ï'' : Modality C D}
                     {Î± : TwoCell Î¼ Î¼'} {Î² : TwoCell Î¼' Î¼''} {Î³ : TwoCell Ï Ï'} {Î´ : TwoCell Ï' Ï''} â†’
                     (Î² â“£-vert Î±) â“£-hor (Î´ â“£-vert Î³) â‰…áµ—á¶œ (Î² â“£-hor Î´) â“£-vert (Î± â“£-hor Î³)
key-subst-eq (2-cell-interchange {Ï'' = Ï''} {Î´ = Î´}) =
  transË¢ (âŠš-congÊ³ (lock-fmap-âŠš Ï'' _ _)) (
  transË¢ âŠš-assoc (
  transË¢ (âŠš-congÊ³ (transË¢ (symË¢ âŠš-assoc) (âŠš-congË¡ (naturality (transf Î´) _)))) (
  transË¢ (âŠš-congÊ³ âŠš-assoc) (
  symË¢ âŠš-assoc))))

â“£-hor-unitË¡ : {Î¼ Ï : Modality C D} {Î± : TwoCell Î¼ Ï} â†’
              â‰…áµ-to-2-cell (ğŸ™-unitË¡ Ï) â“£-vert (id-cell {Î¼ = ğŸ™} â“£-hor Î±) â‰…áµ—á¶œ Î± â“£-vert â‰…áµ-to-2-cell (ğŸ™-unitË¡ Î¼)
key-subst-eq (â“£-hor-unitË¡ {Ï = Ï}) =
  transË¢ (id-subst-unitÊ³ _) (transË¢ (âŠš-congÊ³ (lock-fmap-id Ï)) (transË¢ (id-subst-unitÊ³ _) (symË¢ (id-subst-unitË¡ _))))

â“£-hor-unitÊ³ : {Î¼ Ï : Modality C D} {Î± : TwoCell Î¼ Ï} â†’
              â‰…áµ-to-2-cell (ğŸ™-unitÊ³ Ï) â“£-vert (Î± â“£-hor id-cell {Î¼ = ğŸ™}) â‰…áµ—á¶œ Î± â“£-vert â‰…áµ-to-2-cell (ğŸ™-unitÊ³ Î¼)
key-subst-eq (â“£-hor-unitÊ³ {Ï = Ï}) = id-subst-unitÊ³ _

â“£-hor-assoc : {F : BaseCategory}
              {Î¼ Î¼' : Modality C D} {Ï Ï' : Modality D E} {Îº Îº' : Modality E F}
              {Î± : TwoCell Î¼ Î¼'} {Î² : TwoCell Ï Ï'} {Î³ : TwoCell Îº Îº'} â†’
              â‰…áµ-to-2-cell (â“œ-assoc _ _ _) â“£-vert ((Î³ â“£-hor Î²) â“£-hor Î±) â‰…áµ—á¶œ (Î³ â“£-hor (Î² â“£-hor Î±)) â“£-vert â‰…áµ-to-2-cell (â“œ-assoc _ _ _)
key-subst-eq (â“£-hor-assoc {Î¼' = Î¼'}) =
  transË¢ (id-subst-unitÊ³ _) (transË¢ (âŠš-congÊ³ (lock-fmap-âŠš Î¼' _ _)) (transË¢ (symË¢ âŠš-assoc) (symË¢ (id-subst-unitË¡ _))))
