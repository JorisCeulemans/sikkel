--------------------------------------------------
-- (Non-dependent) function types
--------------------------------------------------

open import Model.BaseCategory

module Model.Type.Function {C : BaseCategory} where

open import Data.Product using (Î£; Î£-syntax; projâ‚; projâ‚‚; _Ã—_) renaming (_,_ to [_,_])
open import Data.String
open import Function using (_âˆ˜_)
open import Relation.Binary.PropositionalEquality hiding ([_]; naturality)

open import Preliminaries
open import Model.CwF-Structure
open import Model.CwF-Structure.Reflection.SubstitutionSequence
open BaseCategory C

private
  variable
    x y z : Ob
    Î“ Î” : Ctx C
    T T' S S' : Ty Î“

infixr 12 _â‡›_
infixr 4 lam[_âˆˆ_]_


--------------------------------------------------
-- Description of a function type at a specific stage (object of the base category)

record PshFun {Î“ : Ctx C} (T : Ty Î“) (S : Ty Î“) (z : Ob) (Î³ : Î“ âŸ¨ z âŸ©) : Set where
  constructor MkFun
  field
    _$âŸ¨_,_âŸ©_ : âˆ€ {y} (Ï : Hom y z) {Î³' : Î“ âŸ¨ y âŸ©} (eÎ³ : Î“ âŸª Ï âŸ« Î³ â‰¡ Î³') â†’
               T âŸ¨ y , Î³' âŸ© â†’ S âŸ¨ y , Î³' âŸ©
    naturality : âˆ€ {x y} {Ï-xy : Hom x y} {Ï-yz : Hom y z} {Î³x : Î“ âŸ¨ x âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} â†’
                 {eÎ³-zy : Î“ âŸª Ï-yz âŸ« Î³ â‰¡ Î³y} {eÎ³-yx : Î“ âŸª Ï-xy âŸ« Î³y â‰¡ Î³x} {t : T âŸ¨ y , Î³y âŸ©} â†’
                 _$âŸ¨_,_âŸ©_ (Ï-yz âˆ™ Ï-xy) (strong-ctx-comp Î“ eÎ³-zy eÎ³-yx) (T âŸª Ï-xy , eÎ³-yx âŸ« t) â‰¡
                   S âŸª Ï-xy , eÎ³-yx âŸ« (_$âŸ¨_,_âŸ©_ Ï-yz eÎ³-zy t)
  infix 13 _$âŸ¨_,_âŸ©_
open PshFun public

-- Here we make again use of uip by pattern matching on both equality proofs.
$-cong : {T : Ty Î“} {S : Ty Î“}
         {Î³x : Î“ âŸ¨ x âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} (f : PshFun T S y Î³y)
         {Ï Ï' : Hom x y} (eÏ : Ï â‰¡ Ï')
         {eÎ³ : Î“ âŸª Ï âŸ« Î³y â‰¡ Î³x} {eÎ³' : Î“ âŸª Ï' âŸ« Î³y â‰¡ Î³x}
         {t : T âŸ¨ x , Î³x âŸ©} â†’
         f $âŸ¨ Ï , eÎ³ âŸ© t â‰¡ f $âŸ¨ Ï' , eÎ³' âŸ© t
$-cong f refl {eÎ³ = refl} {eÎ³' = refl} = refl

-- This is one of the few places where we use function extensionality.
to-pshfun-eq : {T : Ty Î“} {S : Ty Î“}
               {Î³ : Î“ âŸ¨ y âŸ©} {f g : PshFun T S y Î³} â†’
               (âˆ€ {x} (Ï : Hom x y) {Î³'} (eÎ³ : Î“ âŸª Ï âŸ« Î³ â‰¡ Î³') t â†’
                   f $âŸ¨ Ï , eÎ³ âŸ© t â‰¡ g $âŸ¨ Ï , eÎ³ âŸ© t) â†’
               f â‰¡ g
to-pshfun-eq e = congâ‚‚-d MkFun
  (funextI (funext (Î» Ï â†’ funextI (funext Î» eq â†’ funext Î» t â†’ e Ï eq t))))
  (funextI (funextI (funextI (funextI (funextI (funextI (funextI (funextI (funextI (uip _ _))))))))))

-- This will be used to define the action of a function type on morphisms.
lower-presheaffunc : {T : Ty Î“} {S : Ty Î“} (Ï-yz : Hom y z)
                     {Î³z : Î“ âŸ¨ z âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} (eÎ³ : Î“ âŸª Ï-yz âŸ« Î³z â‰¡ Î³y) â†’
                     PshFun T S z Î³z â†’ PshFun T S y Î³y
lower-presheaffunc {Î“ = Î“}{y = y}{z = z}{T = T}{S = S} Ï-yz {Î³z}{Î³y} eÎ³-zy f = MkFun g g-nat
  where
    g : âˆ€ {x} (Ï-xy : Hom x y) {Î³x} (eÎ³-yx : Î“ âŸª Ï-xy âŸ« Î³y â‰¡ Î³x) â†’
        T âŸ¨ x , Î³x âŸ© â†’ S âŸ¨ x , Î³x âŸ©
    g Ï-xy eÎ³-yx = f $âŸ¨ Ï-yz âˆ™ Ï-xy , strong-ctx-comp Î“ eÎ³-zy eÎ³-yx âŸ©_
    open â‰¡-Reasoning
    g-nat : âˆ€ {w x} {Ï-wx : Hom w x} {Ï-xy : Hom x y} {Î³x : Î“ âŸ¨ x âŸ©} {Î³w : Î“ âŸ¨ w âŸ©}
            {eÎ³-yx : Î“ âŸª Ï-xy âŸ« Î³y â‰¡ Î³x} {eÎ³-xw : Î“ âŸª Ï-wx âŸ« Î³x â‰¡ Î³w} {t : T âŸ¨ x , Î³x âŸ©} â†’ _
    g-nat {Ï-wx = Ï-wx}{Ï-xy}{eÎ³-yx = eÎ³-yx}{eÎ³-xw}{t = t} =
      begin
        f $âŸ¨ Ï-yz âˆ™ (Ï-xy âˆ™ Ï-wx) , strong-ctx-comp Î“ eÎ³-zy (strong-ctx-comp Î“ eÎ³-yx eÎ³-xw) âŸ© (T âŸª Ï-wx , eÎ³-xw âŸ« t)
      â‰¡âŸ¨ $-cong f âˆ™assoc âŸ¨
        f $âŸ¨ (Ï-yz âˆ™ Ï-xy) âˆ™ Ï-wx , strong-ctx-comp Î“ (strong-ctx-comp Î“ eÎ³-zy eÎ³-yx) eÎ³-xw âŸ© (T âŸª Ï-wx , eÎ³-xw âŸ« t)
      â‰¡âŸ¨ naturality f âŸ©
        (S âŸª Ï-wx , eÎ³-xw âŸ« (f $âŸ¨ Ï-yz âˆ™ Ï-xy , strong-ctx-comp Î“ eÎ³-zy eÎ³-yx âŸ© t)) âˆ


--------------------------------------------------
-- Definition of the function type + term constructors

_â‡›_ : {Î“ : Ctx C} â†’ Ty Î“ â†’ Ty Î“ â†’ Ty Î“
_â‡›_ {Î“ = Î“} T S âŸ¨ z , Î³ âŸ© = PshFun T S z Î³
_âŸª_,_âŸ«_ (T â‡› S) = lower-presheaffunc
ty-cong (T â‡› S) refl {t = f} = to-pshfun-eq Î» _ _ _ â†’ $-cong f refl
ty-id (_â‡›_ {Î“ = Î“} T S) {t = f} = to-pshfun-eq (Î» _ eÎ³ _ â†’ $-cong f hom-idË¡)
ty-comp (_â‡›_ {Î“ = Î“} T S) {t = f} = to-pshfun-eq (Î» _ _ _ â†’ $-cong f âˆ™assoc)

-- Lambda abstraction that adds a nameless variable to the context (only accessible by de Bruijn index).
lam : (T : Ty Î“) â†’ Tm (Î“ ,, T) (S [ Ï€ ]) â†’ Tm Î“ (T â‡› S)
lam {S = S} T b âŸ¨ z , Î³z âŸ©' = MkFun (Î» Ï-yz {Î³y} eÎ³ t â†’ b âŸ¨ _ , [ Î³y , t ] âŸ©')
                                    (Î» {x = x}{y}{Ï-xy}{_}{Î³x}{Î³y}{eÎ³-zy}{eÎ³-yx}{t} â†’
  begin
    b âŸ¨ x , [ Î³x , T âŸª Ï-xy , eÎ³-yx âŸ« t ] âŸ©'
  â‰¡âŸ¨ naturality b Ï-xy (to-Î£-ty-eq T eÎ³-yx (ty-cong-2-1 T hom-idÊ³)) âŸ¨
    S âŸª Ï-xy , _ âŸ« b âŸ¨ y , [ Î³y , t ] âŸ©'
  â‰¡âŸ¨ ty-cong S refl âŸ©
    S âŸª Ï-xy , eÎ³-yx âŸ« b âŸ¨ y , [ Î³y , t ] âŸ©' âˆ)
  where open â‰¡-Reasoning
naturality (lam T b) _ _ = to-pshfun-eq (Î» _ _ _ â†’ refl)

-- Version of lambda abstraction that allows to name the bound variable.
lam[_âˆˆ_]_ : (v : String) (T : Ty Î“) â†’ Tm (Î“ ,, v âˆˆ T) (S [ Ï€ ]) â†’ Tm Î“ (T â‡› S)
lam[_âˆˆ_]_ v = lam

-- An operator used to define function application.
_â‚¬âŸ¨_,_âŸ©_ : Tm Î“ (T â‡› S) â†’ (x : Ob) (Î³ : Î“ âŸ¨ x âŸ©) â†’ T âŸ¨ x , Î³ âŸ© â†’ S âŸ¨ x , Î³ âŸ©
_â‚¬âŸ¨_,_âŸ©_ {Î“ = Î“} f x Î³ t = f âŸ¨ x , Î³ âŸ©' $âŸ¨ hom-id , ctx-id Î“ âŸ© t

â‚¬-natural : (f : Tm Î“ (T â‡› S)) {Ï : Hom x y}
            {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} {eÎ³ : Î“ âŸª Ï âŸ« Î³y â‰¡ Î³x}
            {t : T âŸ¨ y , Î³y âŸ©} â†’
            S âŸª Ï , eÎ³ âŸ« (f â‚¬âŸ¨ y , Î³y âŸ© t) â‰¡ f â‚¬âŸ¨ x , Î³x âŸ© (T âŸª Ï , eÎ³ âŸ« t)
â‚¬-natural {Î“ = Î“}{T = T}{S = S} f {Ï}{Î³y}{Î³x}{eÎ³}{t} =
  begin
    S âŸª Ï , eÎ³ âŸ« (f âŸ¨ _ , Î³y âŸ©' $âŸ¨ hom-id , ctx-id Î“ âŸ© t)
  â‰¡âŸ¨ naturality (f âŸ¨ _ , Î³y âŸ©') âŸ¨
    f âŸ¨ _ , Î³y âŸ©' $âŸ¨ hom-id âˆ™ Ï , strong-ctx-comp Î“ (ctx-id Î“) eÎ³ âŸ© (T âŸª Ï , eÎ³ âŸ« t)
  â‰¡âŸ¨ $-cong (f âŸ¨ _ , Î³y âŸ©') (trans hom-idË¡ (sym hom-idÊ³)) âŸ©
    f âŸ¨ _ , Î³y âŸ©' $âŸ¨ Ï âˆ™ hom-id , strong-ctx-comp Î“ eÎ³ (ctx-id Î“) âŸ© (T âŸª Ï , eÎ³ âŸ« t)
  â‰¡âŸ¨ cong (Î» x â†’ x $âŸ¨ _ , _ âŸ© _) (naturality f Ï eÎ³) âŸ©
    f âŸ¨ _ , Î³x âŸ©' $âŸ¨ hom-id , ctx-id Î“ âŸ© (T âŸª Ï , eÎ³ âŸ« t) âˆ
  where open â‰¡-Reasoning

app : Tm Î“ (T â‡› S) â†’ Tm Î“ T â†’ Tm Î“ S
app f t âŸ¨ y , Î³ âŸ©' = f â‚¬âŸ¨ y , Î³ âŸ© (t âŸ¨ y , Î³ âŸ©')
naturality (app {Î“ = Î“}{T = T}{S = S} f t) {Î³y = Î³y}{Î³x} Ï eÎ³ =
  begin
    S âŸª Ï , eÎ³ âŸ« (f â‚¬âŸ¨ _ , Î³y âŸ© (t âŸ¨ _ , Î³y âŸ©'))
  â‰¡âŸ¨ â‚¬-natural f âŸ©
    f â‚¬âŸ¨ _ , Î³x âŸ© (T âŸª Ï , eÎ³ âŸ« (t âŸ¨ _ , Î³y âŸ©'))
  â‰¡âŸ¨ cong (f â‚¬âŸ¨ _ , Î³x âŸ©_) (naturality t Ï eÎ³) âŸ©
    f â‚¬âŸ¨ _ , Î³x âŸ© (t âŸ¨ _ , Î³x âŸ©') âˆ
  where open â‰¡-Reasoning

infixl 10 _$_
_$_ = app

ap : Tm Î“ (T â‡› S) â†’ Tm (Î“ ,, T) (S [ Ï€ ])
ap f âŸ¨ x , [ Î³ , t ] âŸ©' = f â‚¬âŸ¨ x , Î³ âŸ© t
naturality (ap {T = T} f) Ï refl = trans (â‚¬-natural f) (cong (f â‚¬âŸ¨ _ , _ âŸ©_) (ty-cong T refl))


--------------------------------------------------
-- Congruence proofs

pshfun-dimap : {T : Ty Î“} {T' : Ty Î“} {S : Ty Î“} {S' : Ty Î“} â†’
               (T' â†£ T) â†’ (S â†£ S') â†’
               {z : Ob} {Î³ : Î“ âŸ¨ z âŸ©} â†’
               PshFun T S z Î³ â†’ PshFun T' S' z Î³
_$âŸ¨_,_âŸ©_ (pshfun-dimap Î· Ï† f) Ï eÎ³ t' = func Ï† (f $âŸ¨ Ï , eÎ³ âŸ© func Î· t')
naturality (pshfun-dimap {T = T}{T'}{S}{S'} Î· Ï† {z} {Î³} f) {eÎ³-zy = eÎ³-zy} {eÎ³-yx} {t'} =
  begin
    func Ï† (f $âŸ¨ _ , _ âŸ© func Î· (T' âŸª _ , eÎ³-yx âŸ« t'))
  â‰¡âŸ¨ cong (func Ï† âˆ˜ f $âŸ¨ _ , _ âŸ©_) (naturality Î·) âŸ¨
    func Ï† (f $âŸ¨ _ , _ âŸ© (T âŸª _ , eÎ³-yx âŸ« func Î· t'))
  â‰¡âŸ¨ cong (func Ï†) (naturality f) âŸ©
    func Ï† (S âŸª _ , eÎ³-yx âŸ« (f $âŸ¨ _ , eÎ³-zy âŸ© func Î· t'))
  â‰¡âŸ¨ naturality Ï† âŸ¨
    S' âŸª _ , eÎ³-yx âŸ« func Ï† (f $âŸ¨ _ , eÎ³-zy âŸ© func Î· t') âˆ
  where open â‰¡-Reasoning

â‡›-dimap : (T' â†£ T) â†’ (S â†£ S') â†’ (T â‡› S â†£ T' â‡› S')
func (â‡›-dimap Î· Ï†) = pshfun-dimap Î· Ï†
naturality (â‡›-dimap Î· Ï†) = to-pshfun-eq Î» _ _ _ â†’ refl

â‡›-cong : T â‰…áµ—Ê¸ T' â†’ S â‰…áµ—Ê¸ S' â†’ T â‡› S â‰…áµ—Ê¸ T' â‡› S'
from (â‡›-cong T=T' S=S') = â‡›-dimap (to T=T') (from S=S')
to (â‡›-cong T=T' S=S') = â‡›-dimap (from T=T') (to S=S')
eq (isoË¡ (â‡›-cong T=T' S=S')) f = to-pshfun-eq (Î» Ï eÎ³ t â†’
  begin
    func (to S=S') (func (from S=S') (f $âŸ¨ Ï , eÎ³ âŸ© func (to T=T') (func (from T=T') t)))
  â‰¡âŸ¨ eq (isoË¡ S=S') _ âŸ©
    f $âŸ¨ Ï , eÎ³ âŸ© func (to T=T') (func (from T=T') t)
  â‰¡âŸ¨ cong (f $âŸ¨ Ï , eÎ³ âŸ©_) (eq (isoË¡ T=T') t) âŸ©
    f $âŸ¨ Ï , eÎ³ âŸ© t âˆ)
  where open â‰¡-Reasoning
eq (isoÊ³ (â‡›-cong T=T' S=S')) f = to-pshfun-eq (Î» Ï eÎ³ t' â†’
  begin
    func (from S=S') (func (to S=S') (f $âŸ¨ Ï , eÎ³ âŸ© func (from T=T') (func (to T=T') t')))
  â‰¡âŸ¨ eq (isoÊ³ S=S') _ âŸ©
    f $âŸ¨ Ï , eÎ³ âŸ© func (from T=T') (func (to T=T') t')
  â‰¡âŸ¨ cong (f $âŸ¨ Ï , eÎ³ âŸ©_) (eq (isoÊ³ T=T') t') âŸ©
    f $âŸ¨ Ï , eÎ³ âŸ© t' âˆ)
  where open â‰¡-Reasoning

â‡›-cong-sym : {T=T' : T â‰…áµ—Ê¸ T'} {S=S' : S â‰…áµ—Ê¸ S'} â†’
             â‡›-cong (symáµ—Ê¸ T=T') (symáµ—Ê¸ S=S') â‰…áµ‰ symáµ—Ê¸ (â‡›-cong T=T' S=S')
eq (from-eq â‡›-cong-sym) f = to-pshfun-eq (Î» _ _ _ â†’ refl)

â‡›-cong-trans : {T T' T'' S S' S'' : Ty Î“}
               {T=T' : T â‰…áµ—Ê¸ T'} {T'=T'' : T' â‰…áµ—Ê¸ T''} {S=S' : S â‰…áµ—Ê¸ S'} {S'=S'' : S' â‰…áµ—Ê¸ S''} â†’
               â‡›-cong (transáµ—Ê¸ T=T' T'=T'') (transáµ—Ê¸ S=S' S'=S'') â‰…áµ‰ transáµ—Ê¸ (â‡›-cong T=T' S=S') (â‡›-cong T'=T'' S'=S'')
eq (from-eq â‡›-cong-trans) _ = to-pshfun-eq (Î» _ _ _ â†’ refl)

â‡›-cong-cong : {T1 T2 S1 S2 : Ty Î“} {eT eT' : T1 â‰…áµ—Ê¸ T2} {eS eS' : S1 â‰…áµ—Ê¸ S2} â†’
              eT â‰…áµ‰ eT' â†’ eS â‰…áµ‰ eS' â†’ â‡›-cong eT eS â‰…áµ‰ â‡›-cong eT' eS'
eq (from-eq (â‡›-cong-cong {eS' = eS'} ğ‘’T ğ‘’S)) f =
  to-pshfun-eq (Î» Ï eÎ³ _ â†’ trans (eq (from-eq ğ‘’S) _) (cong (Î» x â†’ func (from eS') (f $âŸ¨ Ï , eÎ³ âŸ© x)) (eq (to-eq ğ‘’T) _)))

lam-cong : (T : Ty Î“) {b b' : Tm (Î“ ,, T) (S [ Ï€ ])} â†’
           b â‰…áµ—áµ b' â†’ lam T b â‰…áµ—áµ lam T b'
eq (lam-cong T b=b') Î³ = to-pshfun-eq (Î» _ {Î³'} _ t â†’ eq b=b' [ Î³' , t ])

ap-cong : {f f' : Tm Î“ (T â‡› S)} â†’ f â‰…áµ—áµ f' â†’ ap f â‰…áµ—áµ ap f'
eq (ap-cong f=f') Î³ = cong (_$âŸ¨ hom-id , _ âŸ© _) (eq f=f' (projâ‚ Î³))

â‚¬-cong : {f f' : Tm Î“ (T â‡› S)} {Î³ : Î“ âŸ¨ z âŸ©} {t t' : T âŸ¨ z , Î³ âŸ©} â†’
         f â‰…áµ—áµ f' â†’ t â‰¡ t' â†’ f â‚¬âŸ¨ z , Î³ âŸ© t â‰¡ f' â‚¬âŸ¨ z , Î³ âŸ© t'
â‚¬-cong {z = z}{f = f}{f'}{Î³}{t}{t'} f=f' t=t' =
  begin
    f âŸ¨ z , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ© t
  â‰¡âŸ¨ cong (f âŸ¨ z , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ©_) t=t' âŸ©
    f âŸ¨ z , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ© t'
  â‰¡âŸ¨ cong (_$âŸ¨ hom-id , _ âŸ© t') (eq f=f' Î³) âŸ©
    f' âŸ¨ z , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ© t' âˆ
  where open â‰¡-Reasoning

app-cong : {f f' : Tm Î“ (T â‡› S)} {t t' : Tm Î“ T} â†’
           f â‰…áµ—áµ f' â†’ t â‰…áµ—áµ t' â†’ app f t â‰…áµ—áµ app f' t'
eq (app-cong f=f' t=t') Î³ = â‚¬-cong f=f' (eq t=t' Î³)

module _
  {T : Ty Î“} {T' : Ty Î“} {S : Ty Î“} {S' : Ty Î“}
  {T=T' : T â‰…áµ—Ê¸ T'} {S=S' : S â‰…áµ—Ê¸ S'}
  where

  lam-Î¹ : (b : Tm (Î“ ,, T') (S' [ Ï€ ])) â†’
          Î¹[ â‡›-cong T=T' S=S' ] (lam T' b) â‰…áµ—áµ
            lam T (Î¹[ ty-subst-cong-ty Ï€ S=S' ] (
                   Î¹â»Â¹[ ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ï€ (Î¹â»Â¹[ ty-subst-cong-ty Ï€ T=T' ] Î¾)) S' ] (
                   Î¹â»Â¹[ ty-subst-comp S' Ï€ (ty-eq-to-ext-subst Î“ T=T') ] (
                   b [ ty-eq-to-ext-subst Î“ T=T' ]'))))
  eq (lam-Î¹ b) Î³ = to-pshfun-eq (Î» _ _ _ â†’ sym (cong (func (to S=S')) (strong-ty-id S')))

  lam-Î¹â»Â¹ : (b : Tm (Î“ ,, T) (S [ Ï€ ])) â†’
            Î¹â»Â¹[ â‡›-cong T=T' S=S' ] (lam T b) â‰…áµ—áµ
              lam T' (Î¹â»Â¹[ ty-subst-cong-ty Ï€ S=S' ] (
                      Î¹â»Â¹[ ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ï€ (Î¹[ ty-subst-cong-ty Ï€ T=T' ] Î¾)) S ] (
                      Î¹â»Â¹[ ty-subst-comp S Ï€ _ ] (
                      b [ _ ]'))))
  eq (lam-Î¹â»Â¹ b) Î³ = to-pshfun-eq (Î» _ _ _ â†’ cong (func (from S=S')) (sym (strong-ty-id S)))

  app-Î¹ : (f : Tm Î“ (T' â‡› S')) (t : Tm Î“ T') â†’ app (Î¹[ â‡›-cong T=T' S=S' ] f) (Î¹[ T=T' ] t) â‰…áµ—áµ Î¹[ S=S' ] (app f t)
  eq (app-Î¹ f t) Î³ = cong (func (to S=S') âˆ˜ f âŸ¨ _ , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ©_) (eq (isoÊ³ T=T') (t âŸ¨ _ , Î³ âŸ©'))

  app-Î¹â»Â¹ : (f : Tm Î“ (T â‡› S)) (t : Tm Î“ T) â†’ app (Î¹â»Â¹[ â‡›-cong T=T' S=S' ] f) (Î¹â»Â¹[ T=T' ] t) â‰…áµ—áµ Î¹â»Â¹[ S=S' ] (app f t)
  eq (app-Î¹â»Â¹ f t) Î³ = cong (func (from S=S') âˆ˜ f âŸ¨ _ , Î³ âŸ©' $âŸ¨ hom-id , _ âŸ©_) (eq (isoË¡ T=T') (t âŸ¨ _ , Î³ âŸ©'))

ap-Î¹ : {T S S' : Ty Î“} {S=S' : S â‰…áµ—Ê¸ S'} (f : Tm Î“ (T â‡› S')) â†’
       Î¹[ ty-subst-cong-ty Ï€ S=S' ] (ap f) â‰…áµ—áµ ap (Î¹[ â‡›-cong refláµ—Ê¸ S=S' ] f)
eq (ap-Î¹ f) Î³ = refl


--------------------------------------------------
-- Naturality proofs

module _ (Ïƒ : Î” â‡’ Î“) (T : Ty Î“) (S : Ty Î“) {Î´ : Î” âŸ¨ z âŸ©} where
  pshfun-subst-from : PshFun T S z (func Ïƒ Î´) â†’ PshFun (T [ Ïƒ ]) (S [ Ïƒ ]) z Î´
  _$âŸ¨_,_âŸ©_ (pshfun-subst-from f) Ï-yz eÎ´ t = f $âŸ¨ Ï-yz , trans (naturality Ïƒ) (cong (func Ïƒ) eÎ´) âŸ© t
  naturality (pshfun-subst-from f) = trans ($-cong f refl) (naturality f)

  pshfun-subst-to : PshFun (T [ Ïƒ ]) (S [ Ïƒ ]) z Î´ â†’ PshFun T S z (func Ïƒ Î´)
  _$âŸ¨_,_âŸ©_ (pshfun-subst-to f) Ï-yz {Î³'} eÎ´ t = ty-ctx-subst S proof (
                                                 f $âŸ¨ Ï-yz , refl âŸ©
                                                 ty-ctx-subst T (sym proof) t)
    where
      proof : func Ïƒ (Î” âŸª Ï-yz âŸ« Î´) â‰¡ Î³'
      proof = trans (sym (naturality Ïƒ)) eÎ´
  naturality (pshfun-subst-to f) {Ï-xy = Ï-xy}{Ï-yz} {eÎ³-yx = eÎ³-yx} {t = t} =
    begin
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , refl âŸ© (T âŸª hom-id , _ âŸ« T âŸª Ï-xy , eÎ³-yx âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_ âˆ˜ f $âŸ¨ Ï-yz âˆ™ Ï-xy , refl âŸ©_) (ty-cong-2-2 T (trans hom-idÊ³ (sym hom-idË¡))) âŸ©
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , refl âŸ© (T âŸª Ï-xy , _ âŸ« (T âŸª hom-id , Î² âŸ« t))
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) ($-cong f refl) âŸ©
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , _ âŸ© (T âŸª Ï-xy , _ âŸ« (T âŸª hom-id , Î² âŸ« t))
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) (naturality f {eÎ³-yx = sym (ctx-comp Î”)}) âŸ©
      S âŸª hom-id , Î± âŸ« S âŸª Ï-xy , _ âŸ« f $âŸ¨ Ï-yz , refl âŸ© (T âŸª hom-id , Î² âŸ« t)
    â‰¡âŸ¨ ty-cong-2-2 S (trans hom-idÊ³ (sym hom-idË¡)) âŸ©
      S âŸª Ï-xy , eÎ³-yx âŸ« S âŸª hom-id , _ âŸ« f $âŸ¨ Ï-yz , refl âŸ© (T âŸª hom-id , Î² âŸ« t) âˆ
    where
      open â‰¡-Reasoning
      Î± = _
      Î² = _

module _ {T : Ty Î“} {S : Ty Î“} (Ïƒ : Î” â‡’ Î“) where
  â‡›-natural : (T â‡› S) [ Ïƒ ] â‰…áµ—Ê¸ (T [ Ïƒ ]) â‡› (S [ Ïƒ ])
  func (from â‡›-natural) = pshfun-subst-from Ïƒ T S
  naturality (from â‡›-natural) {t = f} = to-pshfun-eq (Î» _ _ _ â†’ $-cong f refl)
  func (to â‡›-natural) = pshfun-subst-to Ïƒ T S
  naturality (to â‡›-natural) {_} {_} {Ï-yz} {t = f} = to-pshfun-eq Î» Ï-xy eÎ³ t â†’
    let Î± = _
        Î² = _
        Î¶ = _
        Î±' = _
        Î²' = _
        Î¶' = _
    in begin
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , Î² âŸ© (T âŸª hom-id , Î¶ âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_ âˆ˜ f $âŸ¨ Ï-yz âˆ™ Ï-xy , Î² âŸ©_) (ty-cong-2-1 T hom-idË¡) âŸ¨
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , Î² âŸ© (T âŸª hom-id , _ âŸ« (T âŸª hom-id , Î¶' âŸ« t))
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) ($-cong f (sym hom-idÊ³)) âŸ©
      S âŸª hom-id , Î± âŸ« f $âŸ¨ (Ï-yz âˆ™ Ï-xy) âˆ™ hom-id , _ âŸ© (T âŸª hom-id , _ âŸ« (T âŸª hom-id , Î¶' âŸ« t))
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) (naturality f {eÎ³-yx = trans (ctx-id Î”) (sym Î²')}) âŸ©
      S âŸª hom-id , Î± âŸ« S âŸª hom-id , _ âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , Î²' âŸ© (T âŸª hom-id , Î¶' âŸ« t)
    â‰¡âŸ¨ ty-cong-2-1 S hom-idÊ³ âŸ©
      S âŸª hom-id , Î±' âŸ« f $âŸ¨ Ï-yz âˆ™ Ï-xy , Î²' âŸ© (T âŸª hom-id , Î¶' âŸ« t) âˆ
    where open â‰¡-Reasoning
  eq (isoË¡ â‡›-natural) f = to-pshfun-eq (Î» Ï-yz eÎ³ t â†’
    let Î± = _ -- giving a name Î± to the proof makes sure that there are no unsolved metas
    in begin
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz , _ âŸ© (T âŸª hom-id , _ âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) ($-cong f (sym hom-idÊ³)) âŸ©
      S âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz âˆ™ hom-id , _ âŸ© (T âŸª hom-id , _ âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î± âŸ«_) (naturality f) âŸ©
      S âŸª hom-id , Î± âŸ« S âŸª hom-id , _ âŸ« f $âŸ¨ Ï-yz , eÎ³ âŸ© t
    â‰¡âŸ¨ ty-cong-2-1 S hom-idÊ³ âŸ©
      S âŸª hom-id , _ âŸ« f $âŸ¨ Ï-yz , eÎ³ âŸ© t
    â‰¡âŸ¨ ty-id S âŸ©
      f $âŸ¨ Ï-yz , eÎ³ âŸ© t âˆ)
    where open â‰¡-Reasoning
  eq (isoÊ³ â‡›-natural) f = to-pshfun-eq (Î» Ï-yz eÎ´ t â†’
    let Î± = trans (ctx-id Î”) (sym eÎ´)
        Î² = _
    in begin
      S âŸª hom-id , Î² âŸ« f $âŸ¨ Ï-yz , refl âŸ© (T âŸª hom-id , _ âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î² âŸ«_) ($-cong f (sym hom-idÊ³)) âŸ©
      S âŸª hom-id , Î² âŸ« f $âŸ¨ Ï-yz âˆ™ hom-id , _ âŸ© (T âŸª hom-id , _ âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î² âŸ«_ âˆ˜ f $âŸ¨ Ï-yz âˆ™ hom-id , _ âŸ©_) (ty-cong T refl) âŸ©
      S âŸª hom-id , Î² âŸ« f $âŸ¨ Ï-yz âˆ™ hom-id , _ âŸ© (T [ Ïƒ ] âŸª hom-id , Î± âŸ« t)
    â‰¡âŸ¨ cong (S âŸª hom-id , Î² âŸ«_) (naturality f) âŸ©
      S âŸª hom-id , Î² âŸ« S [ Ïƒ ] âŸª hom-id , Î± âŸ« f $âŸ¨ Ï-yz , eÎ´ âŸ© t
    â‰¡âŸ¨ ty-cong-2-1 S hom-idÊ³ âŸ©
      S âŸª hom-id , _ âŸ« f $âŸ¨ Ï-yz , eÎ´ âŸ© t
    â‰¡âŸ¨ ty-id S âŸ©
      f $âŸ¨ Ï-yz , eÎ´ âŸ© t âˆ)
    where open â‰¡-Reasoning

  lam-natural : (b : Tm (Î“ ,, T) (S [ Ï€ ])) â†’
                (lam T b) [ Ïƒ ]' â‰…áµ—áµ
                  Î¹[ â‡›-natural ] (
                  lam (T [ Ïƒ ]) (Î¹â»Â¹[ transáµ—Ê¸ (ty-subst-comp S Ï€ (Ïƒ âŠ¹)) (transáµ—Ê¸ (ty-subst-cong-subst (âŠ¹-Ï€-comm Ïƒ) S) (symáµ—Ê¸ (ty-subst-comp S Ïƒ Ï€))) ] (b [ Ïƒ âŠ¹ ]')))
  eq (lam-natural b) Î´ = to-pshfun-eq (Î» Ï {Î³'} eÎ³ t â†’ sym (
    let Î± : Î“ âŸª hom-id âŸ« func Ïƒ (Î” âŸª Ï âŸ« Î´) â‰¡ Î³'
        Î± = trans (ctx-id Î“) (trans (sym (naturality Ïƒ)) eÎ³)
        Î² = begin
              T âŸª hom-id , _ âŸ« T âŸª hom-id , _ âŸ« T âŸª hom-id , _ âŸ« t
            â‰¡âŸ¨ ty-cong-2-1 T hom-idÊ³ âŸ©
              T âŸª hom-id , Î± âŸ« T âŸª hom-id , _ âŸ« t
            â‰¡âŸ¨ ty-cong-2-1 T hom-idÊ³ âŸ©
              T âŸª hom-id , _ âŸ« t
            â‰¡âŸ¨ ty-id T âŸ©
              t âˆ
    in begin
      S âŸª hom-id , _ âŸ« S âŸª hom-id , _ âŸ« b âŸ¨ _ , [ func Ïƒ (Î” âŸª Ï âŸ« Î´) , T âŸª hom-id , _ âŸ« t ] âŸ©'
    â‰¡âŸ¨ ty-cong-2-1 S hom-idÊ³ âŸ©
      S âŸª hom-id , _ âŸ« b âŸ¨ _ , [ func Ïƒ (Î” âŸª Ï âŸ« Î´) , T âŸª hom-id , _ âŸ« t ] âŸ©'
    â‰¡âŸ¨ naturality b hom-id (to-Î£-ty-eq T Î± Î²) âŸ©
      b âŸ¨ _ , [ Î³' , t ] âŸ©' âˆ))
    where open â‰¡-Reasoning

  ap-natural : (f : Tm Î“ (T â‡› S)) â†’
               (ap f) [ Ïƒ âŠ¹ ]' â‰…áµ—áµ (Î¹[ ty-subst-cong-subst-2-2 S (âŠ¹-Ï€-comm Ïƒ) ] ap (Î¹â»Â¹[ â‡›-natural ] (f [ Ïƒ ]')))
  eq (ap-natural f) Î´ = trans (sym (trans ($-cong (f âŸ¨ _ , _ âŸ©') hom-idÊ³) (cong (f â‚¬âŸ¨ _ , _ âŸ©_) (strong-ty-id T))))
                              (naturality (f âŸ¨ _ , func Ïƒ (projâ‚ Î´) âŸ©'))

  app-natural : (f : Tm Î“ (T â‡› S)) (t : Tm Î“ T) â†’
                (app f t) [ Ïƒ ]' â‰…áµ—áµ app (Î¹â»Â¹[ â‡›-natural ] (f [ Ïƒ ]')) (t [ Ïƒ ]')
  eq (app-natural f t) Î´ = $-cong (f âŸ¨ _ , func Ïƒ Î´ âŸ©') refl

â‡›-natural-cong : {T T' S S' : Ty Î”} {Ïƒ : Î“ â‡’ Î”} {T=T' : T â‰…áµ—Ê¸ T'} {S=S' : S â‰…áµ—Ê¸ S'} â†’
                 transáµ—Ê¸ (ty-subst-cong-ty Ïƒ (â‡›-cong T=T' S=S')) (â‡›-natural Ïƒ)
                   â‰…áµ‰
                 transáµ—Ê¸ (â‡›-natural Ïƒ) (â‡›-cong (ty-subst-cong-ty Ïƒ T=T') (ty-subst-cong-ty Ïƒ S=S'))
eq (from-eq â‡›-natural-cong) _ = to-pshfun-eq (Î» _ _ _ â†’ refl)


--------------------------------------------------
-- Î² and Î· laws for functions

â‡›-Î² : (b : Tm (Î“ ,, T) (S [ Ï€ ])) (t : Tm Î“ T) â†’ app (lam T b) t â‰…áµ—áµ Î¹â»Â¹[ ty-weaken-subst t ] (b [ t /v ]')
eq (â‡›-Î² {S = S} b s) Î³ = sym (strong-ty-id S)

â‡›-Î· : (f : Tm Î“ (T â‡› S)) â†’ f â‰…áµ—áµ lam T (app (Î¹â»Â¹[ â‡›-natural Ï€ ] (f [ Ï€ ]')) Î¾)
eq (â‡›-Î· f) Î³ = to-pshfun-eq (Î» Ï eÎ³ t â†’ trans ($-cong (f âŸ¨ _ , Î³ âŸ©') (sym hom-idÊ³)) (cong (_$âŸ¨ hom-id , _ âŸ© t) (naturality f Ï eÎ³)))


--------------------------------------------------
-- Relation between functions T â‡› S and natural tranformations T â†£ S

â‡›-to-â†£ : Tm Î“ (T â‡› S) â†’ (T â†£ S)
func (â‡›-to-â†£ f) = f â‚¬âŸ¨ _ , _ âŸ©_
naturality (â‡›-to-â†£ f) = â‚¬-natural f

â†£-to-â‡› : (T â†£ S) â†’ Tm Î“ (T â‡› S)
(â†£-to-â‡› Î· âŸ¨ _ , _ âŸ©') $âŸ¨ _ , _ âŸ© t = func Î· t
naturality (â†£-to-â‡› Î· âŸ¨ _ , _ âŸ©') = sym (naturality Î·)
naturality (â†£-to-â‡› Î·) _ _ = to-pshfun-eq (Î» _ _ _ â†’ refl)

â†£-â‡›-iso : (Î· : T â†£ S) â†’ â‡›-to-â†£ (â†£-to-â‡› Î·) â‰…â¿ Î·
eq (â†£-â‡›-iso Î·) _ = refl

â‡›-â†£-iso : (f : Tm Î“ (T â‡› S)) â†’ â†£-to-â‡› (â‡›-to-â†£ f) â‰…áµ—áµ f
eq (â‡›-â†£-iso {Î“ = Î“} f) {x} Î³ = to-pshfun-eq (Î» {y} Ï {Î³'} eÎ³ t â†’
  begin
    f âŸ¨ y , Î³' âŸ©' $âŸ¨ hom-id , ctx-id Î“ âŸ© t
  â‰¡âŸ¨ cong (_$âŸ¨ hom-id , ctx-id Î“ âŸ© t) (naturality f Ï eÎ³) âŸ¨
    f âŸ¨ x , Î³ âŸ©' $âŸ¨ Ï âˆ™ hom-id , strong-ctx-comp Î“ eÎ³ (ctx-id Î“) âŸ© t
  â‰¡âŸ¨ $-cong (f âŸ¨ x , Î³ âŸ©') hom-idÊ³ âŸ©
    f âŸ¨ x , Î³ âŸ©' $âŸ¨ Ï , eÎ³ âŸ© t âˆ)
  where open â‰¡-Reasoning


--------------------------------------------------
-- Relating function types and closed types

module _ {A : Ty Î“} {B : ClosedTy C} (clB : IsClosedNatural B ) where
  lamcl : Tm (Î“ ,, A) B â†’ Tm Î“ (A â‡› B)
  lamcl b = lam _ (Î¹[ closed-natural clB Ï€ ] b)

  lamcl-cong : {b b' : Tm (Î“ ,, A) B} â†’ b â‰…áµ—áµ b' â†’ lamcl b â‰…áµ—áµ lamcl b'
  lamcl-cong ğ’† = lam-cong A (Î¹-cong ğ’†)

module _ {A B : ClosedTy C} (clA : IsClosedNatural A) (clB : IsClosedNatural B) where
  fun-closed : IsClosedNatural (A â‡› B)
  closed-natural fun-closed Ïƒ = transáµ—Ê¸ (â‡›-natural Ïƒ) (â‡›-cong (closed-natural clA Ïƒ) (closed-natural clB Ïƒ))
  eq (from-eq (closed-id fun-closed)) f = to-pshfun-eq (Î» Ï eÎ³ a â†’
    trans (eq (from-eq (closed-id clB)) _)
          (trans ($-cong f refl)
                 (cong (f $âŸ¨ Ï , eÎ³ âŸ©_) (eq (to-eq (closed-id clA)) a))))
  eq (from-eq (closed-âŠš fun-closed Ïƒ Ï„)) f = to-pshfun-eq (Î» Ï eÎ³ a â†’
    trans (eq (from-eq (closed-âŠš clB Ïƒ Ï„)) _)
          (cong (func (from (closed-natural clB (Ïƒ âŠš Ï„))))
            (trans ($-cong f refl)
                   (cong (f $âŸ¨ Ï , _ âŸ©_) (eq (to-eq (closed-âŠš clA Ïƒ Ï„)) a)))))
  eq (from-eq (closed-subst-eq fun-closed {Î” = Î”} {Ïƒ = Ïƒ} {Ï„} Îµ)) f = to-pshfun-eq (Î» Ï eÎ³ a â†’
    trans (cong (func (from (closed-natural clB Ï„))) (sym (trans (ty-cong-2-1 B hom-idË¡ {eg = trans (ctx-id Î”) (sym (eq Îµ _))}) (ty-id B))))
          (trans (eq (from-eq (closed-subst-eq clB Îµ)) _)
                 (cong (func (from (closed-natural clB Ïƒ)))
                       (trans (sym (naturality f))
                              (trans ($-cong f (trans hom-idÊ³ hom-idË¡))
                                     (cong (f $âŸ¨ Ï , _ âŸ©_) (eq (to-eq (closed-subst-eq clA Îµ)) a)))))))

  lamcl-natural : {Ïƒ : Î“ â‡’ Î”} {b : Tm (Î” ,, A) B} â†’ (lamcl clB b) [ fun-closed âˆ£ Ïƒ ]cl â‰…áµ—áµ lamcl clB (b [ clB âˆ£ lift-cl-subst clA Ïƒ ]cl)
  lamcl-natural {Ïƒ = Ïƒ} =
    transáµ—áµ (Î¹â»Â¹-cong (lam-natural _ _))
    (transáµ—áµ Î¹â»Â¹-trans
    (transáµ—áµ (Î¹â»Â¹-cong Î¹-symË¡)
    (transáµ—áµ (lam-Î¹â»Â¹ _)
    (lam-cong A (transáµ—áµ (Î¹â»Â¹-cong (Î¹â»Â¹-cong (Î¹â»Â¹-cong (transáµ—áµ (symáµ—áµ Î¹â»Â¹-subst-commute) (transáµ—áµ (Î¹â»Â¹-cong (tm-subst-comp _ _ _)) (Î¹â»Â¹-cong (Î¹-cong (symáµ—áµ Î¹-subst-commute))))))))
                (transáµ—áµ (symáµ—áµ Î¹â»Â¹-trans)
                (transáµ—áµ (symáµ—áµ Î¹â»Â¹-trans)
                (transáµ—áµ (symáµ—áµ Î¹â»Â¹-trans)
                (transáµ—áµ (symáµ—áµ Î¹-trans)
                (transáµ—áµ (Î¹-congáµ‰-2-2 e-proof)
                (transáµ—áµ (Î¹-cong (symáµ—áµ (tm-subst-cong-subst _ (transË¢ s-proof (symË¢ (ctx-ext-subst-comp _ _ _)))))) Î¹-trans)))))))))))
    where
      e-proof : _ â‰…áµ‰ _
      eq (from-eq e-proof) t =
        let Î± = _
        in sym
          (trans (cong (Î» x â†’ ty-ctx-subst B Î± (func (to (closed-natural clB (lift-cl-subst clA Ïƒ))) (func (from (closed-natural clB Ï€)) x)))
                       (sym (eq (isoÊ³ (closed-natural clB Ïƒ)) t)))
          (trans (cong (Î» x â†’ ty-ctx-subst B Î± (func (to (closed-natural clB (lift-cl-subst clA Ïƒ))) x))
                 (trans (eq (from-eq (closed-âŠš clB Ïƒ Ï€)) _)
                 (trans (sym (eq (from-eq (closed-subst-eq clB (symË¢ (lift-cl-subst-Ï€-commute clA)))) _)) (sym (eq (from-eq (closed-âŠš clB Ï€ (lift-cl-subst clA Ïƒ))) _)))))
          (trans (cong (ty-ctx-subst B Î±) (eq (isoË¡ (closed-natural clB (lift-cl-subst clA Ïƒ))) _))
          (trans (naturality (from (closed-natural clB Ï€)))
          (cong (func (from (closed-natural clB Ï€))) (ty-cong-2-2 B refl))))))

      s-proof : _ â‰…Ë¢ _
      s-proof = transË¢ (symË¢ (ctx-ext-subst-cong-subst (transË¢ âŠš-assoc (âŠš-congÊ³ (ctx-ext-subst-Î²â‚ Ï€ _))) _))
                       (ctx-ext-subst-cong-tm _ (record { eq = Î» Î³ â†’ trans (strong-ty-id A)
                                                                     (trans (sym (eq (to-eq (closed-âŠš clA Ïƒ Ï€)) _))
                                                                     (cong (func (to (closed-natural clA Ïƒ))) (eq (isoË¡ (closed-natural clA Ï€)) _))) }))

  app-cl-natural : {Ïƒ : Î“ â‡’ Î”} {f : Tm Î” (A â‡› B)} {a : Tm Î” A} â†’
                   (app f a) [ clB âˆ£ Ïƒ ]cl â‰…áµ—áµ app (f [ fun-closed âˆ£ Ïƒ ]cl) (a [ clA âˆ£ Ïƒ ]cl)
  app-cl-natural = transáµ—áµ (Î¹â»Â¹-cong (app-natural _ _ _)) (transáµ—áµ (symáµ—áµ (app-Î¹â»Â¹ _ _)) (app-cong (symáµ—áµ Î¹â»Â¹-trans) refláµ—áµ))

  â‡›-cl-Î² : (b : Tm (Î“ ,, A) B) (a : Tm Î“ A) â†’ app (lamcl clB b) a â‰…áµ—áµ b [ clB âˆ£ a /clâŸ¨ clA âŸ© ]cl
  â‡›-cl-Î² b a =
    transáµ—áµ (â‡›-Î² _ a) (
    transáµ—áµ (Î¹â»Â¹-cong (transáµ—áµ (symáµ—áµ Î¹-subst-commute) (Î¹-cong (tm-subst-cong-subst b (/v-/cl clA a))))) (
    transáµ—áµ (symáµ—áµ Î¹-trans) (
    transáµ—áµ (symáµ—áµ Î¹-trans) (
    Î¹-congáµ‰ (to-symáµ—Ê¸-eq (
      transáµ‰ transáµ—Ê¸-assoc (
      transáµ‰ (transáµ—Ê¸-congÊ³ (closed-subst-eq clB _)) (
      transáµ‰ transáµ—Ê¸-assoc (
      transáµ‰ (transáµ—Ê¸-congÊ³ (closed-âŠš clB Ï€ (a /v))) (
      transáµ‰ (transáµ—Ê¸-congË¡ (transáµ‰ symáµ—Ê¸-transáµ—Ê¸ (transáµ—Ê¸-congÊ³ symáµ—Ê¸-transáµ—Ê¸))) (
      transáµ‰ (transáµ‰ transáµ—Ê¸-assoc (transáµ—Ê¸-congÊ³ (transáµ‰ transáµ—Ê¸-assoc (transáµ—Ê¸-congÊ³ (symáµ‰ transáµ—Ê¸-assoc))))) (
      transáµ‰ (transáµ—Ê¸-congÊ³ (transáµ—Ê¸-congÊ³ transáµ—Ê¸-cancelË¡-symË¡)) (
      transáµ‰ (transáµ—Ê¸-congÊ³ (transáµ—Ê¸-congË¡ (symáµ‰ ty-subst-cong-subst-sym))) (
      transáµ‰ (transáµ—Ê¸-congÊ³ (closed-subst-eq clB _)) (
      transáµ‰ (transáµ—Ê¸-congÊ³ (closed-id clB)) symáµ—Ê¸-invË¡)))))))))))))))

  â‡›-cl-Î· : (f : Tm Î“ (A â‡› B)) â†’ f â‰…áµ—áµ lamcl clB (app (f [ fun-closed âˆ£ Ï€ ]cl) (Î¾cl clA))
  â‡›-cl-Î· f = transáµ—áµ (â‡›-Î· f) (
    lam-cong A (transáµ—áµ (app-cong (symáµ—áµ (Î¹-congáµ‰-2-1 (transáµ‰ (transáµ—Ê¸-congÊ³ symáµ—Ê¸-transáµ—Ê¸) (
                                                       transáµ‰ (symáµ‰ transáµ—Ê¸-assoc) (
                                                       transáµ‰ (transáµ—Ê¸-congË¡ symáµ—Ê¸-invÊ³)
                                                       refláµ—Ê¸-unitË¡)))))
                                  (symáµ—áµ Î¹-symÊ³))
                        (app-Î¹ _ _)))

fun-closed-congá¶œâ¿ : {A B : ClosedTy C}
                    {clA clA' : IsClosedNatural A} {clB clB' : IsClosedNatural B} â†’
                    clA â‰…á¶œâ¿ clA' â†’ clB â‰…á¶œâ¿ clB' â†’
                    fun-closed clA clB â‰…á¶œâ¿ fun-closed clA' clB'
closed-natural-eq (fun-closed-congá¶œâ¿ eA eB) Ïƒ =
  transáµ—Ê¸-congÊ³ (â‡›-cong-cong (closed-natural-eq eA Ïƒ) (closed-natural-eq eB Ïƒ))

â‡›-closed-cong : {A A' B B' : ClosedTy C}
                {clA : IsClosedNatural A} {clA' : IsClosedNatural A'} {clB : IsClosedNatural B} {clB' : IsClosedNatural B'} â†’
                clA â‰…á¶œáµ—Ê¸ clA' â†’ clB â‰…á¶œáµ—Ê¸ clB' â†’ fun-closed clA clB â‰…á¶œáµ—Ê¸ fun-closed clA' clB'
closed-ty-eq (â‡›-closed-cong eA eB) = â‡›-cong (closed-ty-eq eA) (closed-ty-eq eB)
closed-ty-eq-natural (â‡›-closed-cong eA eB) _ =
  transáµ‰ (symáµ‰ transáµ—Ê¸-assoc) (
  transáµ‰ (transáµ—Ê¸-congË¡ â‡›-natural-cong) (
  transáµ‰ transáµ—Ê¸-assoc (
  transáµ‰ (transáµ—Ê¸-congÊ³ (transáµ‰ (symáµ‰ â‡›-cong-trans) (
                         transáµ‰ (â‡›-cong-cong (closed-ty-eq-natural eA _) (closed-ty-eq-natural eB _))
                         â‡›-cong-trans))) (
  symáµ‰ transáµ—Ê¸-assoc))))
