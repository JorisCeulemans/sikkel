--------------------------------------------------
-- Terms
--------------------------------------------------

open import Model.BaseCategory

module Model.CwF-Structure.Term {C : BaseCategory}  where

open import Function using (id)
open import Relation.Binary.PropositionalEquality hiding ([_]; naturality)
open import Relation.Binary.Reasoning.Syntax
open import Preliminaries

open import Model.Helpers
open import Model.CwF-Structure.Context
open import Model.CwF-Structure.ContextEquivalence
open import Model.CwF-Structure.Type

open BaseCategory C

infix 1 _â‰…áµ—áµ_

private
  variable
    x : Ob
    Î“ Î” Î˜ : Ctx C
    T S S' R : Ty Î“


--------------------------------------------------
-- Definition of terms

record Tm (Î“ : Ctx C) (T : Ty Î“) : Set where
  constructor MkTm
  no-eta-equality

  field
    term : (x : Ob) (Î³ : Î“ âŸ¨ x âŸ©) â†’ T âŸ¨ x , Î³ âŸ©
    naturality : âˆ€ {x y} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} (f : Hom x y) (eÎ³ : Î“ âŸª f âŸ« Î³y â‰¡ Î³x) â†’
                 T âŸª f , eÎ³ âŸ« (term y Î³y) â‰¡ term x Î³x
open Tm public renaming (term to infix 15 _âŸ¨_,_âŸ©')

private
  variable
    t t' : Tm Î“ T
    s s' : Tm Î“ S


--------------------------------------------------
-- Equivalence of terms

record _â‰…áµ—áµ_ {Î“ : Ctx C} {T : Ty Î“} (t s : Tm Î“ T) : Set where
  field
    eq : âˆ€ {x} Î³ â†’ t âŸ¨ x , Î³ âŸ©' â‰¡ s âŸ¨ x , Î³ âŸ©'
open _â‰…áµ—áµ_ public

refláµ—áµ : t â‰…áµ—áµ t
eq refláµ—áµ _ = refl

symáµ—áµ : t â‰…áµ—áµ t' â†’ t' â‰…áµ—áµ t
eq (symáµ—áµ t=t') Î³ = sym (eq t=t' Î³)

transáµ—áµ : {t1 t2 t3 : Tm Î“ T} â†’ t1 â‰…áµ—áµ t2 â†’ t2 â‰…áµ—áµ t3 â†’ t1 â‰…áµ—áµ t3
eq (transáµ—áµ t1=t2 t2=t3) Î³ = trans (eq t1=t2 Î³) (eq t2=t3 Î³)

module â‰…áµ—áµ-Reasoning {Î“}{T} where
  open begin-syntax {A = Tm Î“ T} _â‰…áµ—áµ_ id public
  open â‰…-syntax {A = Tm Î“ T} _â‰…áµ—áµ_ _â‰…áµ—áµ_ transáµ—áµ symáµ—áµ public
  open end-syntax {A = Tm Î“ T} _â‰…áµ—áµ_ refláµ—áµ public


--------------------------------------------------
-- Reindexing maps (cf. Dybjer's internal type theory)

convert-tm : (T â†£ S) â†’ Tm Î“ T â†’ Tm Î“ S
convert-tm Î· t âŸ¨ x , Î³ âŸ©' = func Î· (t âŸ¨ x , Î³ âŸ©')
naturality (convert-tm {T = T}{S = S} Î· t) f eÎ³ =
  begin
    S âŸª f , eÎ³ âŸ« func Î· (t âŸ¨ _ , _ âŸ©')
  â‰¡âŸ¨ naturality Î· âŸ©
    func Î· (T âŸª f , eÎ³ âŸ« (t âŸ¨ _ , _ âŸ©'))
  â‰¡âŸ¨ cong (func Î·) (naturality t f eÎ³) âŸ©
    func Î· (t âŸ¨ _ , _ âŸ©') âˆ
  where open â‰¡-Reasoning

-- Note that instead of the definition below, we could have just
-- written Î¹[ T=S ] s = convert-tm (to T=S) s. However, the
-- following version enables Agda to infer the type equality proofs
-- when we prove a proposition containing Î¹[_]_.
Î¹[_]_ : T â‰…áµ—Ê¸ S â†’ Tm Î“ S â†’ Tm Î“ T
(Î¹[ T=S ] s) âŸ¨ x , Î³ âŸ©' = convert-tm (to T=S) s âŸ¨ x , Î³ âŸ©'
naturality (Î¹[_]_ {T = T} {S = S} T=S s) f eÎ³ = naturality (convert-tm (to T=S) s) f eÎ³

Î¹-convert : {e : T â‰…áµ—Ê¸ S} {s : Tm Î“ S} â†’ Î¹[ e ] s â‰…áµ—áµ convert-tm (to e) s
eq Î¹-convert Î³ = refl

convert-tm-Î¹-2-2 : {T T' S R : Ty Î“} {e : T â‰…áµ—Ê¸ S} {e' : R â‰…áµ—Ê¸ T'} {Ï† : T â†£ R} {Ï†' : S â†£ T'} {s : Tm Î“ S} â†’
                   Ï† âŠ™ to e â‰…â¿ to e' âŠ™ Ï†' â†’
                   convert-tm Ï† (Î¹[ e ] s) â‰…áµ—áµ Î¹[ e' ] convert-tm Ï†' s
eq (convert-tm-Î¹-2-2 ğ”¢) Î³ = eq ğ”¢ _

convert-tm-cong-tm : {Ï† : T â†£ S} {t t' : Tm Î“ T} â†’ t â‰…áµ—áµ t' â†’ convert-tm Ï† t â‰…áµ—áµ convert-tm Ï† t'
eq (convert-tm-cong-tm {Ï† = Ï†} e) Î³ = cong (func Ï†) (eq e Î³)

convert-tm-cong-trans : {Ï† Ï†' : T â†£ S} {t : Tm Î“ T} â†’ Ï† â‰…â¿ Ï†' â†’ convert-tm Ï† t â‰…áµ—áµ convert-tm Ï†' t
eq (convert-tm-cong-trans ğ”¢) Î³ = eq ğ”¢ _

Î¹-cong : {T=S : T â‰…áµ—Ê¸ S} â†’
         s â‰…áµ—áµ s' â†’ Î¹[ T=S ] s â‰…áµ—áµ Î¹[ T=S ] s'
eq (Î¹-cong {T=S = T=S} s=s') Î³ = cong (func (to T=S)) (eq s=s' Î³)

Î¹-congáµ‰ : {e e' : T â‰…áµ—Ê¸ S} {t : Tm Î“ S} â†’ e â‰…áµ‰ e' â†’ Î¹[ e ] t â‰…áµ—áµ Î¹[ e' ] t
eq (Î¹-congáµ‰ ğ‘’) Î³ = eq (to-eq ğ‘’) _

Î¹-refl : {t : Tm Î“ T} â†’ Î¹[ refláµ—Ê¸ ] t â‰…áµ—áµ t
eq Î¹-refl _ = refl

Î¹-symË¡ : {T=S : T â‰…áµ—Ê¸ S} {s : Tm Î“ S} â†’
         Î¹[ symáµ—Ê¸ T=S ] (Î¹[ T=S ] s) â‰…áµ—áµ s
eq (Î¹-symË¡ {T=S = T=S} {s}) Î³ = eq (isoÊ³ T=S) (s âŸ¨ _ , Î³ âŸ©')

Î¹-symÊ³ : {T=S : T â‰…áµ—Ê¸ S} {t : Tm Î“ T} â†’
         Î¹[ T=S ] (Î¹[ symáµ—Ê¸ T=S ] t) â‰…áµ—áµ t
eq (Î¹-symÊ³ {T=S = T=S} {t}) Î³ = eq (isoË¡ T=S) (t âŸ¨ _ , Î³ âŸ©')

Î¹-trans : {T=S : T â‰…áµ—Ê¸ S} {S=R : S â‰…áµ—Ê¸ R} {r : Tm Î“ R} â†’
          Î¹[ transáµ—Ê¸ T=S S=R ] r â‰…áµ—áµ Î¹[ T=S ] (Î¹[ S=R ] r)
eq Î¹-trans Î³ = refl

Î¹â»Â¹[_]_ : T â‰…áµ—Ê¸ S â†’ Tm Î“ T â†’ Tm Î“ S
Î¹â»Â¹[ T=S ] t = Î¹[ symáµ—Ê¸ T=S ] t

Î¹â»Â¹-cong : {T=S : T â‰…áµ—Ê¸ S} â†’
           t â‰…áµ—áµ t' â†’ Î¹â»Â¹[ T=S ] t â‰…áµ—áµ Î¹â»Â¹[ T=S ] t'
Î¹â»Â¹-cong = Î¹-cong

Î¹â»Â¹-congáµ‰ : {e e' : T â‰…áµ—Ê¸ S} {t : Tm Î“ T} â†’ e â‰…áµ‰ e' â†’ Î¹â»Â¹[ e ] t â‰…áµ—áµ Î¹â»Â¹[ e' ] t
eq (Î¹â»Â¹-congáµ‰ ğ‘’) Î³ = eq (from-eq ğ‘’) _

Î¹â»Â¹-trans : {T=S : T â‰…áµ—Ê¸ S} {S=R : S â‰…áµ—Ê¸ R} {t : Tm Î“ T} â†’
            Î¹â»Â¹[ transáµ—Ê¸ T=S S=R ] t â‰…áµ—áµ Î¹â»Â¹[ S=R ] (Î¹â»Â¹[ T=S ] t)
eq Î¹â»Â¹-trans _ = refl

move-Î¹-right : {T=S : T â‰…áµ—Ê¸ S} {t : Tm Î“ T} {s : Tm Î“ S} â†’
               Î¹â»Â¹[ T=S ] t â‰…áµ—áµ s â†’ t â‰…áµ—áµ Î¹[ T=S ] s
move-Î¹-right t=s = transáµ—áµ (symáµ—áµ Î¹-symÊ³) (Î¹-cong t=s)

move-Î¹-left : {S=T : S â‰…áµ—Ê¸ T} {t : Tm Î“ T} {s : Tm Î“ S} â†’
              t â‰…áµ—áµ Î¹â»Â¹[ S=T ] s â†’ Î¹[ S=T ] t â‰…áµ—áµ s
move-Î¹-left t=s = transáµ—áµ (Î¹-cong t=s) Î¹-symÊ³

move-Î¹â»Â¹-right : {S=T : S â‰…áµ—Ê¸ T} {t : Tm Î“ T} {s : Tm Î“ S} â†’
                 Î¹[ S=T ] t â‰…áµ—áµ s â†’ t â‰…áµ—áµ Î¹â»Â¹[ S=T ] s
move-Î¹â»Â¹-right t=s = transáµ—áµ (symáµ—áµ Î¹-symË¡) (Î¹â»Â¹-cong t=s)

move-Î¹â»Â¹-left : {T=S : T â‰…áµ—Ê¸ S} {t : Tm Î“ T} {s : Tm Î“ S} â†’
                t â‰…áµ—áµ Î¹[ T=S ] s â†’ Î¹â»Â¹[ T=S ] t â‰…áµ—áµ s
move-Î¹â»Â¹-left t=s = transáµ—áµ (Î¹â»Â¹-cong t=s) Î¹-symË¡

Î¹-congáµ‰-2-1 : {R=S : R â‰…áµ—Ê¸ S} {S=T : S â‰…áµ—Ê¸ T} {R=T : R â‰…áµ—Ê¸ T} {t : Tm Î“ T} â†’
              transáµ—Ê¸ R=S S=T â‰…áµ‰ R=T â†’
              Î¹[ R=S ] (Î¹[ S=T ] t) â‰…áµ—áµ Î¹[ R=T ] t
Î¹-congáµ‰-2-1 ğ‘’ = transáµ—áµ (symáµ—áµ Î¹-trans) (Î¹-congáµ‰ ğ‘’)

Î¹â»Â¹-congáµ‰-2-1 : {R=S : R â‰…áµ—Ê¸ S} {S=T : S â‰…áµ—Ê¸ T} {R=T : R â‰…áµ—Ê¸ T} {r : Tm Î“ R} â†’
                transáµ—Ê¸ R=S S=T â‰…áµ‰ R=T â†’
                Î¹â»Â¹[ S=T ] (Î¹â»Â¹[ R=S ] r) â‰…áµ—áµ Î¹â»Â¹[ R=T ] r
Î¹â»Â¹-congáµ‰-2-1 ğ‘’ = transáµ—áµ (symáµ—áµ Î¹â»Â¹-trans) (Î¹â»Â¹-congáµ‰ ğ‘’)

Î¹-congáµ‰-2-2 : {R=S : R â‰…áµ—Ê¸ S} {S=T : S â‰…áµ—Ê¸ T} {R=S' : R â‰…áµ—Ê¸ S'} {S'=T : S' â‰…áµ—Ê¸ T} {t : Tm Î“ T} â†’
              transáµ—Ê¸ R=S S=T â‰…áµ‰ transáµ—Ê¸ R=S' S'=T â†’
              Î¹[ R=S ] (Î¹[ S=T ] t) â‰…áµ—áµ Î¹[ R=S' ] (Î¹[ S'=T ] t)
Î¹-congáµ‰-2-2 ğ‘’ = transáµ—áµ (symáµ—áµ Î¹-trans) (transáµ—áµ (Î¹-congáµ‰ ğ‘’) Î¹-trans)

Î¹â»Â¹-congáµ‰-2-2 : {R=S : R â‰…áµ—Ê¸ S} {S=T : S â‰…áµ—Ê¸ T} {R=S' : R â‰…áµ—Ê¸ S'} {S'=T : S' â‰…áµ—Ê¸ T} {r : Tm Î“ R} â†’
                transáµ—Ê¸ R=S S=T â‰…áµ‰ transáµ—Ê¸ R=S' S'=T â†’
                Î¹â»Â¹[ S=T ] (Î¹â»Â¹[ R=S ] r) â‰…áµ—áµ Î¹â»Â¹[ S'=T ] (Î¹â»Â¹[ R=S' ] r)
Î¹â»Â¹-congáµ‰-2-2 ğ‘’ = transáµ—áµ (symáµ—áµ Î¹â»Â¹-trans) (transáµ—áµ (Î¹â»Â¹-congáµ‰ ğ‘’) Î¹â»Â¹-trans)


--------------------------------------------------
-- Substitution of terms

_[_]' : Tm Î“ T â†’ (Ïƒ : Î” â‡’ Î“) â†’ Tm Î” (T [ Ïƒ ])
t [ Ïƒ ]' âŸ¨ x , Î´ âŸ©' = t âŸ¨ x , func Ïƒ Î´ âŸ©'
naturality (t [ Ïƒ ]') f eÎ³ = naturality t f _

tm-subst-cong-tm : (Ïƒ : Î” â‡’ Î“) â†’ t â‰…áµ—áµ s â†’ t [ Ïƒ ]' â‰…áµ—áµ s [ Ïƒ ]'
eq (tm-subst-cong-tm Ïƒ t=s) Î´ = eq t=s (func Ïƒ Î´)

convert-tm-subst-commute : {Ïƒ : Î” â‡’ Î“} {Ï† : T â†£ S} {t : Tm Î“ T} â†’
                           convert-tm (ty-subst-map Ïƒ Ï†) (t [ Ïƒ ]') â‰…áµ—áµ (convert-tm Ï† t) [ Ïƒ ]'
eq convert-tm-subst-commute _ = refl

Î¹-subst-commute : {Ïƒ : Î” â‡’ Î“} {T=S : T â‰…áµ—Ê¸ S} {s : Tm Î“ S} â†’
                  Î¹[ ty-subst-cong-ty Ïƒ T=S ] (s [ Ïƒ ]') â‰…áµ—áµ (Î¹[ T=S ] s) [ Ïƒ ]'
eq Î¹-subst-commute _ = refl

Î¹â»Â¹-subst-commute : {Ïƒ : Î” â‡’ Î“} {T=S : T â‰…áµ—Ê¸ S} {t : Tm Î“ T} â†’
                    Î¹â»Â¹[ ty-subst-cong-ty Ïƒ T=S ] (t [ Ïƒ ]') â‰…áµ—áµ (Î¹â»Â¹[ T=S ] t) [ Ïƒ ]'
eq Î¹â»Â¹-subst-commute _ = refl

tm-subst-cong-subst : {Ïƒ Ï„ : Î” â‡’ Î“} (t : Tm Î“ T) â†’
                      (Ïƒ=Ï„ : Ïƒ â‰…Ë¢ Ï„) â†’ t [ Ïƒ ]' â‰…áµ—áµ Î¹[ ty-subst-cong-subst Ïƒ=Ï„ T ] (t [ Ï„ ]')
eq (tm-subst-cong-subst t Ïƒ=Ï„) Î´ = sym (naturality t hom-id _)

tm-subst-id : (t : Tm Î“ T) â†’ t [ id-subst Î“ ]' â‰…áµ—áµ Î¹[ ty-subst-id T ] t
eq (tm-subst-id t) _ = refl

tm-subst-comp : (t : Tm Î˜ T) (Ï„ : Î“ â‡’ Î˜) (Ïƒ : Î” â‡’ Î“) â†’
                t [ Ï„ ]' [ Ïƒ ]' â‰…áµ—áµ Î¹[ ty-subst-comp T Ï„ Ïƒ ] (t [ Ï„ âŠš Ïƒ ]')
eq (tm-subst-comp t Ï„ Ïƒ) _ = refl

tm-subst-cong-subst-2-2 : {Î”' : Ctx C} {Ïƒ1 : Î“ â‡’ Î”} {Ïƒ2 : Î” â‡’ Î˜} {Ï„1 : Î“ â‡’ Î”'} {Ï„2 : Î”' â‡’ Î˜}
                          {T : Ty Î˜} (t : Tm Î˜ T) (Îµ : Ïƒ2 âŠš Ïƒ1 â‰…Ë¢ Ï„2 âŠš Ï„1) â†’
                          t [ Ïƒ2 ]' [ Ïƒ1 ]' â‰…áµ—áµ Î¹[ ty-subst-cong-subst-2-2 T Îµ ] (t [ Ï„2 ]' [ Ï„1 ]')
eq (tm-subst-cong-subst-2-2 t Îµ) Î³ = sym (naturality t _ _)

-- Nicer syntax for substitutions coming from context equality
Î¹c[_]'_ : {S : Ty Î”} â†’ (Î“=Î” : Î“ â‰…á¶œ Î”) â†’ Tm Î” S â†’ Tm Î“ (Î¹c[ Î“=Î” ] S)
Î¹c[ Î“=Î” ]' s = s [ from Î“=Î” ]'

Î¹câ»Â¹[_]'_ : {T : Ty Î“} â†’ (Î“=Î” : Î“ â‰…á¶œ Î”) â†’ Tm Î“ T â†’ Tm Î” (Î¹câ»Â¹[ Î“=Î” ] T)
Î¹câ»Â¹[ Î“=Î” ]' t = t [ to Î“=Î” ]'
