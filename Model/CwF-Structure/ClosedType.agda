--------------------------------------------------
-- Closed types (i.e. types that can be defined in any context)
--------------------------------------------------

module Model.CwF-Structure.ClosedType where

open import Data.Product
open import Relation.Binary.PropositionalEquality hiding ([_])

open import Model.BaseCategory
open import Model.CwF-Structure.Context
open import Model.CwF-Structure.Type
open import Model.CwF-Structure.Term
open import Model.CwF-Structure.ContextExtension
open import Model.CwF-Structure.ContextEquivalence

private
  variable
    C : BaseCategory
    Î“ Î” Î˜ : Ctx C

infix 1 _â‰…á¶œáµ—Ê¸_


--------------------------------------------------
-- Definition of closed types

ClosedTy : BaseCategory â†’ Setâ‚
ClosedTy C = {Î“ : Ctx C} â†’ Ty Î“

-- A "genuine" closed type should be natural.
-- I.e. it is a pseudonatural transformation from the terminal
-- pseudofunctor (from Ctx to Groupoids) to the pseudofunctor Ty.
record IsClosedNatural {C} (U : ClosedTy C) : Setâ‚ where
  no-eta-equality
  field
    closed-natural : {Î” : Ctx C} {Î“ : Ctx C} (Ïƒ : Î” â‡’ Î“) â†’
                     U [ Ïƒ ] â‰…áµ—Ê¸ U
    closed-id : {Î“ : Ctx C} â†’ closed-natural (id-subst Î“) â‰…áµ‰ ty-subst-id U
    closed-âŠš : {Î“ Î” Î˜ : Ctx C} (Ïƒ : Î” â‡’ Î˜) (Ï„ : Î“ â‡’ Î”) â†’
               (transáµ—Ê¸ (ty-subst-cong-ty Ï„ (closed-natural Ïƒ)) (closed-natural Ï„))
                 â‰…áµ‰
               (transáµ—Ê¸ (ty-subst-comp U Ïƒ Ï„) (closed-natural (Ïƒ âŠš Ï„)))
    closed-subst-eq : {Î“ Î” : Ctx C} {Ïƒ Ï„ : Î“ â‡’ Î”} (Îµ : Ïƒ â‰…Ë¢ Ï„) â†’
                      transáµ—Ê¸ (ty-subst-cong-subst Îµ U) (closed-natural Ï„) â‰…áµ‰ closed-natural Ïƒ

open IsClosedNatural public


--------------------------------------------------
-- A type in the empty context gives rise to a closed type.

From-â—‡-ty : Ty {C = C} â—‡ â†’ ClosedTy C
From-â—‡-ty T {Î“ = Î“} = T [ !â—‡ Î“ ]

From-â—‡-ty-natural : (T : Ty {C = C} â—‡) â†’ IsClosedNatural (From-â—‡-ty T)
IsClosedNatural.closed-natural (From-â—‡-ty-natural T) Ïƒ = transáµ—Ê¸ (ty-subst-comp T _ Ïƒ) (ty-subst-cong-subst (â—‡-terminal _ _ _) T)
eq (from-eq (IsClosedNatural.closed-id (From-â—‡-ty-natural T))) _ = ty-id T
eq (from-eq (IsClosedNatural.closed-âŠš (From-â—‡-ty-natural {C} T) Ïƒ Ï„)) _ = ty-cong-2-1 T (BaseCategory.hom-idÊ³ C)
eq (from-eq (IsClosedNatural.closed-subst-eq (From-â—‡-ty-natural {C} T) Îµ)) _ = ty-cong-2-1 T (BaseCategory.hom-idÊ³ C)


--------------------------------------------------
-- A better version of substitution for terms of closed types, in
-- which no substitution is applied to the type.

_[_âˆ£_]cl : {T : ClosedTy C} â†’ Tm Î” T â†’ IsClosedNatural T â†’ (Î“ â‡’ Î”) â†’ Tm Î“ T
t [ clT âˆ£ Ïƒ ]cl = Î¹â»Â¹[ closed-natural clT Ïƒ ] (t [ Ïƒ ]')

module _ {T : ClosedTy C} (clT : IsClosedNatural T) where
  cl-tm-subst-id : (t : Tm Î“ T) â†’ t [ clT âˆ£ id-subst Î“ ]cl â‰…áµ—áµ t
  cl-tm-subst-id t =
    begin
      Î¹â»Â¹[ closed-natural clT (id-subst _) ] (t [ id-subst _ ]')
    â‰…âŸ¨ Î¹â»Â¹-cong (tm-subst-id t) âŸ©
      Î¹â»Â¹[ closed-natural clT (id-subst _) ] (Î¹[ ty-subst-id T ] t)
    â‰…âŸ¨ Î¹-congáµ‰-2-1 (transáµ‰ (transáµ—Ê¸-congË¡ (symáµ—Ê¸-cong (closed-id clT))) symáµ—Ê¸-invË¡) âŸ©
      Î¹[ refláµ—Ê¸ ] t
    â‰…âŸ¨ Î¹-refl âŸ©
      t âˆ
    where open â‰…áµ—áµ-Reasoning

  cl-tm-subst-âŠš : {Ï„ : Î” â‡’ Î˜} {Ïƒ : Î“ â‡’ Î”} (t : Tm Î˜ T) â†’
                  (t [ clT âˆ£ Ï„ ]cl) [ clT âˆ£ Ïƒ ]cl â‰…áµ—áµ t [ clT âˆ£ Ï„ âŠš Ïƒ ]cl
  cl-tm-subst-âŠš {Ï„ = Ï„} {Ïƒ} t =
    begin
      Î¹â»Â¹[ closed-natural clT Ïƒ ] ((Î¹â»Â¹[ closed-natural clT Ï„ ] (t [ Ï„ ]')) [ Ïƒ ]')
    â‰…Ë˜âŸ¨ Î¹â»Â¹-cong Î¹â»Â¹-subst-commute âŸ©
      Î¹â»Â¹[ closed-natural clT Ïƒ ] (Î¹â»Â¹[ ty-subst-cong-ty Ïƒ (closed-natural clT Ï„) ] ((t [ Ï„ ]') [ Ïƒ ]'))
    â‰…âŸ¨ Î¹â»Â¹-cong (Î¹â»Â¹-cong (tm-subst-comp t Ï„ Ïƒ)) âŸ©
      Î¹â»Â¹[ closed-natural clT Ïƒ ] (Î¹â»Â¹[ ty-subst-cong-ty Ïƒ (closed-natural clT Ï„) ] (Î¹[ ty-subst-comp T Ï„ Ïƒ ] (t [ Ï„ âŠš Ïƒ ]')))
    â‰…âŸ¨ Î¹â»Â¹-congáµ‰-2-2 (closed-âŠš clT Ï„ Ïƒ) âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï„ âŠš Ïƒ) ] (Î¹â»Â¹[ ty-subst-comp T Ï„ Ïƒ ] (Î¹[ ty-subst-comp T Ï„ Ïƒ ] (t [ Ï„ âŠš Ïƒ ]')))
    â‰…âŸ¨ Î¹â»Â¹-cong Î¹-symË¡ âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï„ âŠš Ïƒ) ] (t [ Ï„ âŠš Ïƒ ]') âˆ
    where open â‰…áµ—áµ-Reasoning

  cl-tm-subst-cong-subst : {Ïƒ Ï„ : Î“ â‡’ Î”} {t : Tm Î” T} â†’
                           Ïƒ â‰…Ë¢ Ï„ â†’ t [ clT âˆ£ Ïƒ ]cl â‰…áµ—áµ t [ clT âˆ£ Ï„ ]cl
  cl-tm-subst-cong-subst {Ïƒ = Ïƒ} {Ï„} {t} Îµ =
    begin
      Î¹â»Â¹[ closed-natural clT Ïƒ ] (t [ Ïƒ ]')
    â‰…âŸ¨ Î¹â»Â¹-cong (tm-subst-cong-subst t Îµ) âŸ©
      Î¹â»Â¹[ closed-natural clT Ïƒ ] (Î¹[ ty-subst-cong-subst Îµ T ] (t [ Ï„ ]'))
    â‰…Ë˜âŸ¨ Î¹â»Â¹-congáµ‰-2-1 (closed-subst-eq clT Îµ) âŸ©
      Î¹â»Â¹[ closed-natural clT Ï„ ] (Î¹â»Â¹[ ty-subst-cong-subst Îµ T ] (Î¹[ ty-subst-cong-subst Îµ T ] (t [ Ï„ ]')))
    â‰…âŸ¨ Î¹â»Â¹-cong Î¹-symË¡ âŸ©
      Î¹â»Â¹[ closed-natural clT Ï„ ] (t [ Ï„ ]') âˆ
    where open â‰…áµ—áµ-Reasoning

  cl-tm-subst-cong-tm : {Ïƒ : Î“ â‡’ Î”} {t s : Tm Î” T} â†’
                        t â‰…áµ—áµ s â†’ t [ clT âˆ£ Ïƒ ]cl â‰…áµ—áµ s [ clT âˆ£ Ïƒ ]cl
  cl-tm-subst-cong-tm t=s = Î¹â»Â¹-cong (tm-subst-cong-tm _ t=s)

  cl-tm-subst-cong-subst-2-2 : {Î”' : Ctx C} {Ïƒ1 : Î“ â‡’ Î”} {Ïƒ2 : Î” â‡’ Î˜} {Ï„1 : Î“ â‡’ Î”'} {Ï„2 : Î”' â‡’ Î˜} {t : Tm Î˜ T} â†’
                               Ïƒ2 âŠš Ïƒ1 â‰…Ë¢ Ï„2 âŠš Ï„1 â†’
                               t [ clT âˆ£ Ïƒ2 ]cl [ clT âˆ£ Ïƒ1 ]cl â‰…áµ—áµ t [ clT âˆ£ Ï„2 ]cl [ clT âˆ£ Ï„1 ]cl
  cl-tm-subst-cong-subst-2-2 e-subst =
    transáµ—áµ (cl-tm-subst-âŠš _) (transáµ—áµ (cl-tm-subst-cong-subst e-subst) (symáµ—áµ (cl-tm-subst-âŠš _)))

  Î¾cl : Tm (Î“ ,, T) T
  Î¾cl = Î¹â»Â¹[ closed-natural clT Ï€ ] Î¾

_,clâŸ¨_âŸ©_ : (Î“ â‡’ Î”) â†’ {T : ClosedTy C} â†’ IsClosedNatural T â†’ Tm Î“ T â†’ (Î“ â‡’ Î” ,, T)
Ïƒ ,clâŸ¨ clT âŸ© t = to-ext-subst _ Ïƒ (Î¹[ closed-natural clT Ïƒ ] t)

module _ {T : ClosedTy C} (clT : IsClosedNatural T) where
  ,cl-Î²1 : (Ïƒ : Î“ â‡’ Î”) (t : Tm Î“ T) â†’ Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t) â‰…Ë¢ Ïƒ
  ,cl-Î²1 Ïƒ t = ctx-ext-subst-Î²â‚ _ _

  ,cl-Î²2 : (Ïƒ : Î“ â‡’ Î”) (t : Tm Î“ T) â†’ (Î¾cl clT [ clT âˆ£ Ïƒ ,clâŸ¨ clT âŸ© t ]cl) â‰…áµ—áµ t
  ,cl-Î²2 Ïƒ t =
    begin
      Î¹â»Â¹[ closed-natural clT (Ïƒ ,clâŸ¨ clT âŸ© t) ] ((Î¹â»Â¹[ closed-natural clT Ï€ ] Î¾) [ Ïƒ ,clâŸ¨ clT âŸ© t ]')
    â‰…Ë˜âŸ¨ Î¹â»Â¹-cong Î¹â»Â¹-subst-commute âŸ©
      Î¹â»Â¹[ closed-natural clT (Ïƒ ,clâŸ¨ clT âŸ© t) ] (Î¹â»Â¹[ ty-subst-cong-ty (Ïƒ ,clâŸ¨ clT âŸ© t) (closed-natural clT Ï€) ] (Î¾ [ Ïƒ ,clâŸ¨ clT âŸ© t ]'))
    â‰…âŸ¨ Î¹â»Â¹-cong (Î¹â»Â¹-cong (ctx-ext-subst-Î²â‚‚ Ïƒ _)) âŸ©
      Î¹â»Â¹[ closed-natural clT (Ïƒ ,clâŸ¨ clT âŸ© t) ] (Î¹â»Â¹[ ty-subst-cong-ty (Ïƒ ,clâŸ¨ clT âŸ© t) (closed-natural clT Ï€) ]
         (Î¹[ transáµ—Ê¸ (ty-subst-comp T Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t))
                     (ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ (Î¹[ closed-natural clT Ïƒ ] t)) T)
           ] (Î¹[ closed-natural clT Ïƒ ] t)))
    â‰…âŸ¨ Î¹â»Â¹-congáµ‰-2-2 (closed-âŠš clT Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t)) âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t)) ] (Î¹â»Â¹[ ty-subst-comp T Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t) ]
         (Î¹[ transáµ—Ê¸ (ty-subst-comp T Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t))
                     (ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ (Î¹[ closed-natural clT Ïƒ ] t)) T)
           ] (Î¹[ closed-natural clT Ïƒ ] t)))
    â‰…âŸ¨ Î¹â»Â¹-cong (Î¹â»Â¹-cong Î¹-trans) âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t)) ] (Î¹â»Â¹[ ty-subst-comp T Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t) ] (Î¹[ ty-subst-comp T Ï€ (Ïƒ ,clâŸ¨ clT âŸ© t) ]
         (Î¹[ ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ (Î¹[ closed-natural clT Ïƒ ] t)) T
           ] (Î¹[ closed-natural clT Ïƒ ] t))))
    â‰…âŸ¨ Î¹â»Â¹-cong Î¹-symË¡ âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t)) ] (Î¹[ ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ (Î¹[ closed-natural clT Ïƒ ] t)) T ] (Î¹[ closed-natural clT Ïƒ ] t))
    â‰…âŸ¨ Î¹â»Â¹-cong (Î¹-congáµ‰-2-1 (closed-subst-eq clT _)) âŸ©
      Î¹â»Â¹[ closed-natural clT (Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t)) ] (Î¹[ closed-natural clT (Ï€ âŠš (Ïƒ ,clâŸ¨ clT âŸ© t)) ] t)
    â‰…âŸ¨ Î¹-symË¡ âŸ©
      t âˆ
    where open â‰…áµ—áµ-Reasoning

  ,cl-Î· : (Ïƒ : Î“ â‡’ Î” ,, T) â†’ Ïƒ â‰…Ë¢ ((Ï€ âŠš Ïƒ) ,clâŸ¨ clT âŸ© (Î¾cl clT [ clT âˆ£ Ïƒ ]cl))
  ,cl-Î· Ïƒ = transË¢ (symË¢ (ctx-ext-subst-Î· Ïƒ)) (ctx-ext-subst-cong-tm (Ï€ âŠš Ïƒ) (move-Î¹-right tm-proof))
    where
      open â‰…áµ—áµ-Reasoning
      tm-proof =
        begin
          Î¹â»Â¹[ closed-natural clT (Ï€ âŠš Ïƒ) ] (Î¹â»Â¹[ ty-subst-comp T Ï€ Ïƒ ] (Î¾ [ Ïƒ ]'))
        â‰…Ë˜âŸ¨ Î¹â»Â¹-congáµ‰-2-2 (closed-âŠš clT Ï€ Ïƒ) âŸ©
          Î¹â»Â¹[ closed-natural clT Ïƒ ] (Î¹â»Â¹[ ty-subst-cong-ty Ïƒ (closed-natural clT Ï€) ] (Î¾ [ Ïƒ ]'))
        â‰…âŸ¨ Î¹â»Â¹-cong Î¹â»Â¹-subst-commute âŸ©
          Î¹â»Â¹[ closed-natural clT Ïƒ ] ((Î¹â»Â¹[ closed-natural clT Ï€ ] Î¾) [ Ïƒ ]') âˆ

  ,cl-cong-tm : {Ïƒ : Î“ â‡’ Î”} {t s : Tm Î“ T} â†’ t â‰…áµ—áµ s â†’ Ïƒ ,clâŸ¨ clT âŸ© t â‰…Ë¢ Ïƒ ,clâŸ¨ clT âŸ© s
  ,cl-cong-tm e = ctx-ext-subst-cong-tm _ (Î¹-cong e)

  ,cl-cong-subst : {Ïƒ Ï„ : Î“ â‡’ Î”} {t : Tm Î“ T} â†’ Ïƒ â‰…Ë¢ Ï„ â†’ Ïƒ ,clâŸ¨ clT âŸ© t â‰…Ë¢ Ï„ ,clâŸ¨ clT âŸ© t
  ,cl-cong-subst e = transË¢ (ctx-ext-subst-cong-tm _ (symáµ—áµ (Î¹-congáµ‰-2-1 (closed-subst-eq clT e)))) (ctx-ext-subst-cong-subst e _)

  ,cl-cong : {Ïƒ Ï„ : Î“ â‡’ Î”} {t s : Tm Î“ T} â†’ Ïƒ â‰…Ë¢ Ï„ â†’ t â‰…áµ—áµ s â†’ Ïƒ ,clâŸ¨ clT âŸ© t â‰…Ë¢ Ï„ ,clâŸ¨ clT âŸ© s
  ,cl-cong eÏƒ et = transË¢ (,cl-cong-tm et) (,cl-cong-subst eÏƒ)

  lift-cl-subst : (Î“ â‡’ Î”) â†’ (Î“ ,, T â‡’ Î” ,, T)
  lift-cl-subst Ïƒ = (Ïƒ âŠš Ï€) ,clâŸ¨ clT âŸ© Î¾cl clT

  lift-cl-subst-Ï€-commute : {Ïƒ : Î“ â‡’ Î”} â†’ Ï€ âŠš (lift-cl-subst Ïƒ) â‰…Ë¢ Ïƒ âŠš Ï€
  lift-cl-subst-Ï€-commute = ctx-ext-subst-Î²â‚ _ _

  lift-cl-subst-âŠ¹ : (Ïƒ : Î“ â‡’ Î”) â†’ (Ïƒ âŠ¹) â‰…Ë¢ lift-cl-subst Ïƒ âŠš from (,,-cong (closed-natural clT Ïƒ))
  eq (lift-cl-subst-âŠ¹ Ïƒ) (Î³ , t) =
    cong (func Ïƒ Î³ ,_) (sym (trans (cong (func (to (closed-natural clT (Ïƒ âŠš Ï€)))) (eq (from-eq (closed-âŠš clT Ïƒ Ï€)) t))
                                   (eq (isoË¡ (closed-natural clT (Ïƒ âŠš Ï€))) t)))

  ,cl-âŠš : (Ïƒ : Î” â‡’ Î˜) (t : Tm Î” T) (Ï„ : Î“ â‡’ Î”) â†’ (Ïƒ ,clâŸ¨ clT âŸ© t) âŠš Ï„ â‰…Ë¢ (Ïƒ âŠš Ï„) ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ï„ ]cl)
  ,cl-âŠš Ïƒ t Ï„ = begin
      (Ïƒ ,clâŸ¨ clT âŸ© t) âŠš Ï„
    â‰…âŸ¨ ,cl-Î· _ âŸ©
      (Ï€ âŠš ((Ïƒ ,clâŸ¨ clT âŸ© t) âŠš Ï„)) ,clâŸ¨ clT âŸ©
        (Î¾cl clT [ clT âˆ£ (Ïƒ ,clâŸ¨ clT âŸ© t) âŠš Ï„ ]cl)
    â‰…âŸ¨ ,cl-cong (transË¢ (symË¢ âŠš-assoc) (âŠš-congË¡ (,cl-Î²1 Ïƒ t)))
                (symáµ—áµ (cl-tm-subst-âŠš clT (Î¾cl clT))) âŸ©
      (Ïƒ âŠš Ï„) ,clâŸ¨ clT âŸ© ((Î¾cl clT [ clT âˆ£ Ïƒ ,clâŸ¨ clT âŸ© t ]cl) [ clT âˆ£ Ï„ ]cl)
    â‰…âŸ¨ ,cl-cong-tm (cl-tm-subst-cong-tm clT (,cl-Î²2 Ïƒ t)) âŸ©
      (Ïƒ âŠš Ï„) ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ï„ ]cl) âˆ
    where open â‰…Ë¢-Reasoning

  /-âŠš : (Ïƒ : Î“ â‡’ Î”) (t : Tm Î” T) â†’ (id-subst Î” ,clâŸ¨ clT âŸ© t) âŠš Ïƒ â‰…Ë¢ lift-cl-subst Ïƒ âŠš (id-subst Î“ ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ïƒ ]cl))
  /-âŠš Ïƒ t = begin
      (id-subst _ ,clâŸ¨ clT âŸ© t) âŠš Ïƒ
    â‰…âŸ¨ ,cl-âŠš _ t Ïƒ âŸ©
      (id-subst _ âŠš Ïƒ) ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ïƒ ]cl)
    â‰…âŸ¨ ,cl-cong (transË¢ (id-subst-unitË¡ _) (symË¢ (transË¢ (transË¢ âŠš-assoc (âŠš-congÊ³ (,cl-Î²1 _ _))) (id-subst-unitÊ³ _))))
                (symáµ—áµ (,cl-Î²2 _ _)) âŸ©
      (Ïƒ âŠš Ï€ âŠš (id-subst _ ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ïƒ ]cl))) ,clâŸ¨ clT âŸ©
        (Î¾cl clT [ clT âˆ£ id-subst _ ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ïƒ ]cl) ]cl)
    â‰…Ë˜âŸ¨ ,cl-âŠš _ _ _ âŸ©
      lift-cl-subst Ïƒ âŠš (id-subst _ ,clâŸ¨ clT âŸ© (t [ clT âˆ£ Ïƒ ]cl)) âˆ
    where open â‰…Ë¢-Reasoning

  /cl : (t : Tm Î” T) â†’ (t /v) â‰…Ë¢ (id-subst Î” ,clâŸ¨ clT âŸ© t)
  /cl t = ctx-ext-subst-cong-tm _ (transáµ—áµ (tm-subst-id t) (Î¹-congáµ‰ (symáµ‰ (closed-id clT))))


--------------------------------------------------
-- Since Ty Î“ is a groupoid (and not a setoid), naturality of a closed
-- type is in fact structure and not a property. Occasionally we will
-- need to express equivalence of such naturality proofs (e.g. when
-- comparing âŸ¨ ğŸ™ âˆ£ T âŸ© and T).

record _â‰…á¶œáµ—Ê¸_ {A : ClosedTy C} (clA clA' : IsClosedNatural A) : Setâ‚ where
  field
    closed-natural-eq : (Ïƒ : Î“ â‡’ Î”) â†’ closed-natural clA Ïƒ â‰…áµ‰ closed-natural clA' Ïƒ
open _â‰…á¶œáµ—Ê¸_ public

module _ {A : ClosedTy C} {clA clA' : IsClosedNatural A} (e : clA â‰…á¶œáµ—Ê¸ clA') where
  cl-tm-subst-cl-cong : {Ïƒ : Î“ â‡’ Î”} {t : Tm Î” A} â†’
                        t [ clA âˆ£ Ïƒ ]cl â‰…áµ—áµ t [ clA' âˆ£ Ïƒ ]cl
  cl-tm-subst-cl-cong {Ïƒ = Ïƒ} = Î¹â»Â¹-congáµ‰ (closed-natural-eq e Ïƒ)

  Î¾cl-cong-cl : {Î“ : Ctx C} â†’ Î¾cl clA {Î“ = Î“} â‰…áµ—áµ Î¾cl clA'
  Î¾cl-cong-cl = Î¹â»Â¹-congáµ‰ (closed-natural-eq e Ï€)

  ,cl-cong-cl : {Ïƒ : Î“ â‡’ Î”} {t : Tm Î“ A} â†’ Ïƒ ,clâŸ¨ clA âŸ© t â‰…Ë¢ Ïƒ ,clâŸ¨ clA' âŸ© t
  ,cl-cong-cl = ctx-ext-subst-cong-tm _ (Î¹-congáµ‰ (closed-natural-eq e _))
