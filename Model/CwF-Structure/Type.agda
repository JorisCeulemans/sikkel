--------------------------------------------------
-- Types
--------------------------------------------------

open import Model.BaseCategory

module Model.CwF-Structure.Type {C : BaseCategory} where

open import Data.Product renaming (_,_ to [_,_])
open import Function hiding (_âŸ¨_âŸ©_; _â†£_)
open import Relation.Binary.PropositionalEquality hiding ([_]; naturality)

open import Model.Helpers
open import Model.CwF-Structure.Context
open import Model.CwF-Structure.ContextEquivalence

open BaseCategory C

infix 10 _â†£_
infix 1 _â‰…â¿_ _â‰…áµ—Ê¸_
infixl 20 _âŠ™_

private
  variable
    x y z w : Ob
    Î” Î“ Î˜ : Ctx C


--------------------------------------------------
-- Definition of types in a context

-- A type in context Î“ is defined as a presheaf over the category of elements of Î“.
-- A morphism in the category of elements of Î“ from (x, Î³x) to (y, Î³y) consists of
--   a morphism f : Hom x y together with a proof that Î“ âŸª f âŸ« Î³y â‰¡ Î³x. This explains
--   the type of the field morph (representing the action of the presheaf on morphisms).

record Ty (Î“ : Ctx C) : Setâ‚ where
  constructor MkTy
  no-eta-equality

  field
    ty-cell : (x : Ob) (Î³ : Î“ âŸ¨ x âŸ©) â†’ Set
    ty-hom : âˆ€ {x y} (f : Hom x y) {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} â†’ Î“ âŸª f âŸ« Î³y â‰¡ Î³x â†’ ty-cell y Î³y â†’ ty-cell x Î³x
    ty-cong : {f f' : Hom x y} (e-hom : f â‰¡ f')
              {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} {eÎ³ : Î“ âŸª f âŸ« Î³y â‰¡ Î³x} {eÎ³' : Î“ âŸª f' âŸ« Î³y â‰¡ Î³x}
              {t : ty-cell y Î³y} â†’
              ty-hom f eÎ³ t â‰¡ ty-hom f' eÎ³' t
    ty-id : âˆ€ {x} {Î³ : Î“ âŸ¨ x âŸ©} {t : ty-cell x Î³} â†’ ty-hom hom-id (ctx-id Î“) t â‰¡ t
    ty-comp : âˆ€ {x y z} {f : Hom x y} {g : Hom y z} {Î³z : Î“ âŸ¨ z âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} â†’
              {eÎ³-zy : Î“ âŸª g âŸ« Î³z â‰¡ Î³y} {eÎ³-yx : Î“ âŸª f âŸ« Î³y â‰¡ Î³x} {t : ty-cell z Î³z} â†’
              ty-hom (g âˆ™ f) (strong-ctx-comp Î“ eÎ³-zy eÎ³-yx) t â‰¡ ty-hom f eÎ³-yx (ty-hom g eÎ³-zy t)
open Ty public renaming (ty-cell to infix 15 _âŸ¨_,_âŸ©; ty-hom to infixr 11 _âŸª_,_âŸ«_)

private
  variable
    T S R : Ty Î“
    T1 T2 T3 T4 : Ty Î“

strong-ty-id : (T : Ty Î“) {Î³ : Î“ âŸ¨ x âŸ©} {eÎ³ : Î“ âŸª hom-id âŸ« Î³ â‰¡ Î³} {t : T âŸ¨ x , Î³ âŸ©} â†’
               T âŸª hom-id , eÎ³ âŸ« t â‰¡ t
strong-ty-id T = trans (ty-cong T refl) (ty-id T)

strong-ty-comp : (T : Ty Î“) {f : Hom x y} {g : Hom y z} {Î³z : Î“ âŸ¨ z âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©}
                {eÎ³-zx : Î“ âŸª g âˆ™ f âŸ« Î³z â‰¡ Î³x} {eÎ³-zy : Î“ âŸª g âŸ« Î³z â‰¡ Î³y} {eÎ³-yx : Î“ âŸª f âŸ« Î³y â‰¡ Î³x}
                {t : T âŸ¨ z , Î³z âŸ©} â†’
                T âŸª g âˆ™ f , eÎ³-zx âŸ« t â‰¡ T âŸª f , eÎ³-yx âŸ« T âŸª g , eÎ³-zy âŸ« t
strong-ty-comp T = trans (ty-cong T refl) (ty-comp T)

ty-cong-2-1 : (T : Ty Î“)
              {f : Hom x y} {g : Hom y z} {h : Hom x z} (e-hom : g âˆ™ f â‰¡ h)
              {Î³z : Î“ âŸ¨ z âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©}
              {ef : Î“ âŸª f âŸ« Î³y â‰¡ Î³x} {eg : Î“ âŸª g âŸ« Î³z â‰¡ Î³y} {eh : Î“ âŸª h âŸ« Î³z â‰¡ Î³x}
              {t : T âŸ¨ z , Î³z âŸ©} â†’
              T âŸª f , ef âŸ« (T âŸª g , eg âŸ« t) â‰¡ T âŸª h , eh âŸ« t
ty-cong-2-1 T {f}{g}{h} e-hom {t = t} =
  begin
    T âŸª f , _ âŸ« T âŸª g , _ âŸ« t
  â‰¡Ë˜âŸ¨ ty-comp T âŸ©
    T âŸª g âˆ™ f , _ âŸ« t
  â‰¡âŸ¨ ty-cong T e-hom âŸ©
    T âŸª h , _ âŸ« t âˆ
  where open â‰¡-Reasoning

ty-cong-2-2 : (T : Ty Î“)
              {f : Hom x y} {f' : Hom x z} {g : Hom y w} {g' : Hom z w} (e-hom : g âˆ™ f â‰¡ g' âˆ™ f')
              {Î³w : Î“ âŸ¨ w âŸ©} {Î³z : Î“ âŸ¨ z âŸ©} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©}
              {ef : Î“ âŸª f âŸ« Î³y â‰¡ Î³x} {ef' : Î“ âŸª f' âŸ« Î³z â‰¡ Î³x}
              {eg : Î“ âŸª g âŸ« Î³w â‰¡ Î³y} {eg' : Î“ âŸª g' âŸ« Î³w â‰¡ Î³z}
              {t : T âŸ¨ w , Î³w âŸ©} â†’
              T âŸª f , ef âŸ« (T âŸª g , eg âŸ« t) â‰¡ T âŸª f' , ef' âŸ« (T âŸª g' , eg' âŸ« t)
ty-cong-2-2 T {f}{f'}{g}{g'} e-hom {t = t} =
  begin
    T âŸª f , _ âŸ« T âŸª g , _ âŸ« t
  â‰¡Ë˜âŸ¨ ty-comp T âŸ©
    T âŸª g âˆ™ f , _ âŸ« t
  â‰¡âŸ¨ ty-cong T e-hom âŸ©
    T âŸª g' âˆ™ f' , _ âŸ« t
  â‰¡âŸ¨ ty-comp T âŸ©
    T âŸª f' , _ âŸ« T âŸª g' , _ âŸ« t âˆ
  where open â‰¡-Reasoning

ty-ctx-subst : (T : Ty Î“) {Î³ Î³' : Î“ âŸ¨ x âŸ©} â†’ Î³ â‰¡ Î³' â†’ T âŸ¨ x , Î³ âŸ© â†’ T âŸ¨ x , Î³' âŸ©
ty-ctx-subst {Î“ = Î“} T eÎ³ = T âŸª hom-id , trans (ctx-id Î“) eÎ³ âŸ«_

ty-ctx-subst-inverseË¡ : (T : Ty Î“) {Î³ Î³' : Î“ âŸ¨ x âŸ©} {eÎ³ : Î³ â‰¡ Î³'} {t : T âŸ¨ x , Î³ âŸ©} â†’
                        ty-ctx-subst T (sym eÎ³) (ty-ctx-subst T eÎ³ t) â‰¡ t
ty-ctx-subst-inverseË¡ T = trans (ty-cong-2-1 T hom-idË¡) (ty-id T)

ty-ctx-subst-inverseÊ³ : (T : Ty Î“) {Î³ Î³' : Î“ âŸ¨ x âŸ©} {eÎ³ : Î³ â‰¡ Î³'} {t : T âŸ¨ x , Î³' âŸ©} â†’
                        ty-ctx-subst T eÎ³ (ty-ctx-subst T (sym eÎ³) t) â‰¡ t
ty-ctx-subst-inverseÊ³ T = trans (ty-cong-2-1 T hom-idË¡) (ty-id T)

-- The following definition is needed when defining context extension.
to-Î£-ty-eq : âˆ€ {â„“} {A : Set â„“} (T : Ty Î“)
             {a b : A} (e : a â‰¡ b)
             {Î³ : A â†’ Î“ âŸ¨ x âŸ©}
             {ta : T âŸ¨ x , Î³ a âŸ©} {tb : T âŸ¨ x , Î³ b âŸ©} â†’ ty-ctx-subst T (cong Î³ e) ta â‰¡ tb â†’
             [ a , ta ] â‰¡ [ b , tb ]
to-Î£-ty-eq T {a = a} refl et = cong [ a ,_] (trans (sym (strong-ty-id T)) et)

from-Î£-ty-eq : âˆ€ {â„“} {A : Set â„“} (T : Ty Î“)
               {a b : A} {Î³ : A â†’ Î“ âŸ¨ x âŸ©}
               {ta : T âŸ¨ x , Î³ a âŸ©} {tb : T âŸ¨ x , Î³ b âŸ©} â†’
               [ a , ta ] â‰¡ [ b , tb ] â†’
               Î£[ e âˆˆ a â‰¡ b ] ty-ctx-subst T (cong Î³ e) ta â‰¡ tb
from-Î£-ty-eq T refl = [ refl , strong-ty-id T ]


--------------------------------------------------
-- Natural transformations between types

record _â†£_ {Î“ : Ctx C} (T : Ty Î“) (S : Ty Î“) : Set where
  no-eta-equality
  field
    func : âˆ€ {x} {Î³} â†’ T âŸ¨ x , Î³ âŸ© â†’ S âŸ¨ x , Î³ âŸ©
    naturality : âˆ€ {x y} {f : Hom x y} {Î³y : Î“ âŸ¨ y âŸ©} {Î³x : Î“ âŸ¨ x âŸ©} {eÎ³ : Î“ âŸª f âŸ« Î³y â‰¡ Î³x} {t : T âŸ¨ y , Î³y âŸ©} â†’
                 S âŸª f , eÎ³ âŸ« (func t) â‰¡ func (T âŸª f , eÎ³ âŸ« t)
open _â†£_ public

record _â‰…â¿_ {Î“ : Ctx C} {T : Ty Î“} {S : Ty Î“} (Î· Ï† : T â†£ S) : Set where
  field
    eq : âˆ€ {x Î³} (t : T âŸ¨ x , Î³ âŸ©) â†’ func Î· t â‰¡ func Ï† t
open _â‰…â¿_ public

reflâ¿ : {Î· : T â†£ S} â†’ Î· â‰…â¿ Î·
eq reflâ¿ _ = refl

symâ¿ : {Î· Ï† : T â†£ S} â†’ Î· â‰…â¿ Ï† â†’ Ï† â‰…â¿ Î·
eq (symâ¿ Î·=Ï†) t = sym (eq Î·=Ï† t)

transâ¿ : {Î· Ï† Âµ : T â†£ S} â†’ Î· â‰…â¿ Ï† â†’ Ï† â‰…â¿ Âµ â†’ Î· â‰…â¿ Âµ
eq (transâ¿ Î·=Ï† Ï†=Âµ) t = trans (eq Î·=Ï† t) (eq Ï†=Âµ t)

module â‰…â¿-Reasoning where
  infix  3 _âˆ
  infixr 2 _â‰…âŸ¨âŸ©_ step-â‰… step-â‰…Ë˜
  infix  1 begin_

  begin_ : âˆ€ {Î· Ï† : T â†£ S} â†’ Î· â‰…â¿ Ï† â†’ Î· â‰…â¿ Ï†
  begin_ Î·=Ï† = Î·=Ï†

  _â‰…âŸ¨âŸ©_ : âˆ€ (Î· {Ï†} : T â†£ S) â†’ Î· â‰…â¿ Ï† â†’ Î· â‰…â¿ Ï†
  _ â‰…âŸ¨âŸ© Î·=Ï† = Î·=Ï†

  step-â‰… : âˆ€ (Î· {Ï† Âµ} : T â†£ S) â†’ Ï† â‰…â¿ Âµ â†’ Î· â‰…â¿ Ï† â†’ Î· â‰…â¿ Âµ
  step-â‰… _ Ï†â‰…Âµ Î·â‰…Ï† = transâ¿ Î·â‰…Ï† Ï†â‰…Âµ

  step-â‰…Ë˜ : âˆ€ (Î· {Ï† Âµ} : T â†£ S) â†’ Ï† â‰…â¿ Âµ â†’ Ï† â‰…â¿ Î· â†’ Î· â‰…â¿ Âµ
  step-â‰…Ë˜ _ Ï†â‰…Âµ Ï†â‰…Î· = transâ¿ (symâ¿ Ï†â‰…Î·) Ï†â‰…Âµ

  _âˆ : âˆ€ (Î· : T â†£ S) â†’ Î· â‰…â¿ Î·
  _âˆ _ = reflâ¿

  syntax step-â‰…  Î· Ï†â‰…Âµ Î·â‰…Ï† = Î· â‰…âŸ¨  Î·â‰…Ï† âŸ© Ï†â‰…Âµ
  syntax step-â‰…Ë˜ Î· Ï†â‰…Âµ Ï†â‰…Î· = Î· â‰…Ë˜âŸ¨ Ï†â‰…Î· âŸ© Ï†â‰…Âµ

id-trans : (T : Ty Î“) â†’ T â†£ T
func (id-trans T) = id
naturality (id-trans T) = refl

_âŠ™_ : S â†£ T â†’ R â†£ S â†’ R â†£ T
func (Ï† âŠ™ Î·) = func Ï† âˆ˜ func Î·
naturality (_âŠ™_ {S = S}{T = T}{R = R} Ï† Î·) {f = f}{eÎ³ = eÎ³} {t = r} =
  begin
    T âŸª f , eÎ³ âŸ« func Ï† (func Î· r)
  â‰¡âŸ¨ naturality Ï† âŸ©
    func Ï† (S âŸª f , eÎ³ âŸ« func Î· r)
  â‰¡âŸ¨ cong (func Ï†) (naturality Î·) âŸ©
    func Ï† (func Î· (R âŸª f , eÎ³ âŸ« r)) âˆ
  where open â‰¡-Reasoning

id-trans-unitÊ³ : (Î· : T â†£ S) â†’ Î· âŠ™ id-trans T â‰…â¿ Î·
eq (id-trans-unitÊ³ Î·) _ = refl

id-trans-unitË¡ : (Î· : T â†£ S) â†’ id-trans S âŠ™ Î· â‰…â¿ Î·
eq (id-trans-unitË¡ Î·) _ = refl

âŠ™-assoc : {Tâ‚ : Ty Î“} {Tâ‚‚ : Ty Î“} {Tâ‚ƒ : Ty Î“} {Tâ‚„ : Ty Î“}
          (Î·â‚ƒâ‚„ : Tâ‚ƒ â†£ Tâ‚„) (Î·â‚‚â‚ƒ : Tâ‚‚ â†£ Tâ‚ƒ) (Î·â‚â‚‚ : Tâ‚ â†£ Tâ‚‚) â†’
          (Î·â‚ƒâ‚„ âŠ™ Î·â‚‚â‚ƒ) âŠ™ Î·â‚â‚‚ â‰…â¿ Î·â‚ƒâ‚„ âŠ™ (Î·â‚‚â‚ƒ âŠ™ Î·â‚â‚‚)
eq (âŠ™-assoc Î·â‚ƒâ‚„ Î·â‚‚â‚ƒ Î·â‚â‚‚) _ = refl

âŠ™-congÊ³ : (Ï† : S â†£ T) {Î· Î·' : R â†£ S} â†’ Î· â‰…â¿ Î·' â†’ Ï† âŠ™ Î· â‰…â¿ Ï† âŠ™ Î·'
eq (âŠ™-congÊ³ Ï† Î·=Î·') Î´ = cong (func Ï†) (eq Î·=Î·' Î´)

âŠ™-congË¡ : {Ï† Ï†' : S â†£ T} (Î· : R â†£ S) â†’ Ï† â‰…â¿ Ï†' â†’ Ï† âŠ™ Î· â‰…â¿ Ï†' âŠ™ Î·
eq (âŠ™-congË¡ Î· Ï†=Ï†') Î´ = eq Ï†=Ï†' (func Î· Î´)


--------------------------------------------------
-- Equivalence of types

-- Two types are said to be equivalent if they are naturally isomorphic as presheaves.
-- This turns out to be easier to work with than standard propositional equality.
record _â‰…áµ—Ê¸_ {Î“ : Ctx C} (T : Ty Î“) (S : Ty Î“) : Set where
  no-eta-equality

  field
    from : T â†£ S
    to : S â†£ T
    isoË¡ : to âŠ™ from â‰…â¿ id-trans T
    isoÊ³ : from âŠ™ to â‰…â¿ id-trans S
open _â‰…áµ—Ê¸_ public

refláµ—Ê¸ : T â‰…áµ—Ê¸ T
from (refláµ—Ê¸ {T = T}) = id-trans T
to (refláµ—Ê¸ {T = T}) = id-trans T
eq (isoË¡ refláµ—Ê¸) _ = refl
eq (isoÊ³ refláµ—Ê¸) _ = refl

symáµ—Ê¸ : S â‰…áµ—Ê¸ T â†’ T â‰…áµ—Ê¸ S
from (symáµ—Ê¸ S=T) = to S=T
to (symáµ—Ê¸ S=T) = from S=T
isoË¡ (symáµ—Ê¸ S=T) = isoÊ³ S=T
isoÊ³ (symáµ—Ê¸ S=T) = isoË¡ S=T

transáµ—Ê¸ : S â‰…áµ—Ê¸ T â†’ T â‰…áµ—Ê¸ R â†’ S â‰…áµ—Ê¸ R
from (transáµ—Ê¸ S=T T=R) = from T=R âŠ™ from S=T
to (transáµ—Ê¸ S=T T=R) = to S=T âŠ™ to T=R
isoË¡ (transáµ—Ê¸ S=T T=R) =
  begin
    (to S=T âŠ™ to T=R) âŠ™ (from T=R âŠ™ from S=T)
  â‰…âŸ¨ âŠ™-assoc (to S=T) (to T=R) _ âŸ©
    to S=T âŠ™ (to T=R âŠ™ (from T=R âŠ™ from S=T))
  â‰…Ë˜âŸ¨ âŠ™-congÊ³ (to S=T) (âŠ™-assoc (to T=R) (from T=R) (from S=T)) âŸ©
    to S=T âŠ™ ((to T=R âŠ™ from T=R) âŠ™ from S=T)
  â‰…âŸ¨ âŠ™-congÊ³ (to S=T) (âŠ™-congË¡ (from S=T) (isoË¡ T=R)) âŸ©
    to S=T âŠ™ (id-trans _ âŠ™ from S=T)
  â‰…âŸ¨ âŠ™-congÊ³ (to S=T) (id-trans-unitË¡ (from S=T)) âŸ©
    to S=T âŠ™ from S=T
  â‰…âŸ¨ isoË¡ S=T âŸ©
    id-trans _ âˆ
  where open â‰…â¿-Reasoning
isoÊ³ (transáµ—Ê¸ S=T T=R) =
  begin
    (from T=R âŠ™ from S=T) âŠ™ (to S=T âŠ™ to T=R)
  â‰…âŸ¨ âŠ™-assoc (from T=R) (from S=T) _ âŸ©
    from T=R âŠ™ (from S=T âŠ™ (to S=T âŠ™ to T=R))
  â‰…Ë˜âŸ¨ âŠ™-congÊ³ (from T=R) (âŠ™-assoc (from S=T) (to S=T) (to T=R)) âŸ©
    from T=R âŠ™ ((from S=T âŠ™ to S=T) âŠ™ to T=R)
  â‰…âŸ¨ âŠ™-congÊ³ (from T=R) (âŠ™-congË¡ (to T=R) (isoÊ³ S=T)) âŸ©
    from T=R âŠ™ (id-trans _ âŠ™ to T=R)
  â‰…âŸ¨ âŠ™-congÊ³ (from T=R) (id-trans-unitË¡ (to T=R)) âŸ©
    from T=R âŠ™ to T=R
  â‰…âŸ¨ isoÊ³ T=R âŸ©
    id-trans _ âˆ
  where open â‰…â¿-Reasoning

module â‰…áµ—Ê¸-Reasoning where
  infix  3 _âˆ
  infixr 2 _â‰…âŸ¨âŸ©_ step-â‰… step-â‰…Ë˜
  infix  1 begin_

  begin_ : T â‰…áµ—Ê¸ S â†’ T â‰…áµ—Ê¸ S
  begin_ T=S = T=S

  _â‰…âŸ¨âŸ©_ : (T : Ty Î“) â†’ T â‰…áµ—Ê¸ S â†’ T â‰…áµ—Ê¸ S
  _ â‰…âŸ¨âŸ© T=S = T=S

  step-â‰… : (T : Ty Î“) â†’ S â‰…áµ—Ê¸ R â†’ T â‰…áµ—Ê¸ S â†’ T â‰…áµ—Ê¸ R
  step-â‰… _ Sâ‰…R Tâ‰…S = transáµ—Ê¸ Tâ‰…S Sâ‰…R

  step-â‰…Ë˜ : (T : Ty Î“) â†’ S â‰…áµ—Ê¸ R â†’ S â‰…áµ—Ê¸ T â†’ T â‰…áµ—Ê¸ R
  step-â‰…Ë˜ _ Sâ‰…R Sâ‰…T = transáµ—Ê¸ (symáµ—Ê¸ Sâ‰…T) Sâ‰…R

  _âˆ : (T : Ty Î“) â†’ T â‰…áµ—Ê¸ T
  _âˆ _ = refláµ—Ê¸

  syntax step-â‰…  T Sâ‰…R Tâ‰…S = T â‰…âŸ¨  Tâ‰…S âŸ© Sâ‰…R
  syntax step-â‰…Ë˜ T Sâ‰…R Sâ‰…T = T â‰…Ë˜âŸ¨ Sâ‰…T âŸ© Sâ‰…R


-- Ty Î“ is a groupoid and not a setoid (i.e. T â‰…áµ—Ê¸ S is not necessarily a proposition).
-- Therefore, we want to express equalities of natural isomorphisms of types.
record _â‰…áµ‰_ {T S : Ty Î“} (e1 e2 : T â‰…áµ—Ê¸ S) : Set where
  no-eta-equality
  field
    from-eq : from e1 â‰…â¿ from e2
open _â‰…áµ‰_ public

to-eq : {e1 e2 : T â‰…áµ—Ê¸ S} â†’ e1 â‰…áµ‰ e2 â†’ to e1 â‰…â¿ to e2
to-eq {e1 = e1} {e2} Îµ = begin
    to e1
  â‰…Ë˜âŸ¨ id-trans-unitÊ³ (to e1) âŸ©
    to e1 âŠ™ id-trans _
  â‰…Ë˜âŸ¨ âŠ™-congÊ³ _ (isoÊ³ e2) âŸ©
    to e1 âŠ™ (from e2 âŠ™ to e2)
  â‰…Ë˜âŸ¨ âŠ™-assoc _ _ _ âŸ©
    (to e1 âŠ™ from e2) âŠ™ to e2
  â‰…âŸ¨ âŠ™-congË¡ _ (âŠ™-congÊ³ _ (symâ¿ (from-eq Îµ))) âŸ©
    (to e1 âŠ™ from e1) âŠ™ to e2
  â‰…âŸ¨ âŠ™-congË¡ _ (isoË¡ e1) âŸ©
    id-trans _ âŠ™ to e2
  â‰…âŸ¨ id-trans-unitË¡ _ âŸ©
    to e2 âˆ
  where open â‰…â¿-Reasoning

refláµ‰ : {e : T â‰…áµ—Ê¸ S} â†’ e â‰…áµ‰ e
from-eq refláµ‰ = reflâ¿

symáµ‰ : {e1 e2 : T â‰…áµ—Ê¸ S} â†’ e1 â‰…áµ‰ e2 â†’ e2 â‰…áµ‰ e1
from-eq (symáµ‰ Îµ) = symâ¿ (from-eq Îµ)

transáµ‰ : {e1 e2 e3 : T â‰…áµ—Ê¸ S} â†’ e1 â‰…áµ‰ e2 â†’ e2 â‰…áµ‰ e3 â†’ e1 â‰…áµ‰ e3
from-eq (transáµ‰ Îµ Îµ') = transâ¿ (from-eq Îµ) (from-eq Îµ')

-- symáµ—Ê¸ and transáµ—Ê¸ respect equality of natural isomorphisms.
symáµ—Ê¸-cong : {e e' : T â‰…áµ—Ê¸ S} â†’ e â‰…áµ‰ e' â†’ symáµ—Ê¸ e â‰…áµ‰ symáµ—Ê¸ e'
from-eq (symáµ—Ê¸-cong ğ‘’) = to-eq ğ‘’

transáµ—Ê¸-congË¡ : {e1 e1' : T â‰…áµ—Ê¸ S} {e2 : S â‰…áµ—Ê¸ R} â†’ e1 â‰…áµ‰ e1' â†’ transáµ—Ê¸ e1 e2 â‰…áµ‰ transáµ—Ê¸ e1' e2
from-eq (transáµ—Ê¸-congË¡ ğ‘’) = âŠ™-congÊ³ _ (from-eq ğ‘’)

transáµ—Ê¸-congÊ³ : {e1 : T â‰…áµ—Ê¸ S} {e2 e2' : S â‰…áµ—Ê¸ R} â†’ e2 â‰…áµ‰ e2' â†’ transáµ—Ê¸ e1 e2 â‰…áµ‰ transáµ—Ê¸ e1 e2'
from-eq (transáµ—Ê¸-congÊ³ ğ‘’) = âŠ™-congË¡ _ (from-eq ğ‘’)

-- Groupoid laws for the groupoid Ty Î“ and some consequences
transáµ—Ê¸-assoc : {e : T1 â‰…áµ—Ê¸ T2} {e' : T2 â‰…áµ—Ê¸ T3} {e'' : T3 â‰…áµ—Ê¸ T4} â†’
                transáµ—Ê¸ (transáµ—Ê¸ e e') e'' â‰…áµ‰ transáµ—Ê¸ e (transáµ—Ê¸ e' e'')
from-eq transáµ—Ê¸-assoc = symâ¿ (âŠ™-assoc _ _ _)

refláµ—Ê¸-unitË¡ : {e : T â‰…áµ—Ê¸ S} â†’ transáµ—Ê¸ refláµ—Ê¸ e â‰…áµ‰ e
from-eq refláµ—Ê¸-unitË¡ = id-trans-unitÊ³ _

refláµ—Ê¸-unitÊ³ : {e : T â‰…áµ—Ê¸ S} â†’ transáµ—Ê¸ e refláµ—Ê¸ â‰…áµ‰ e
from-eq refláµ—Ê¸-unitÊ³ = id-trans-unitË¡ _

symáµ—Ê¸-invË¡ : {e : T â‰…áµ—Ê¸ S} â†’ transáµ—Ê¸ (symáµ—Ê¸ e) e â‰…áµ‰ refláµ—Ê¸
from-eq (symáµ—Ê¸-invË¡ {e = e}) = isoÊ³ e

symáµ—Ê¸-invÊ³ : {e : T â‰…áµ—Ê¸ S} â†’ transáµ—Ê¸ e (symáµ—Ê¸ e) â‰…áµ‰ refláµ—Ê¸
from-eq (symáµ—Ê¸-invÊ³ {e = e}) = isoË¡ e

symáµ—Ê¸-transáµ—Ê¸ : {e : T â‰…áµ—Ê¸ S} {e' : S â‰…áµ—Ê¸ R} â†’ symáµ—Ê¸ (transáµ—Ê¸ e e') â‰…áµ‰ transáµ—Ê¸ (symáµ—Ê¸ e') (symáµ—Ê¸ e)
from-eq symáµ—Ê¸-transáµ—Ê¸ = reflâ¿

to-symáµ—Ê¸-eq : {e : T â‰…áµ—Ê¸ S} {e' : S â‰…áµ—Ê¸ T} â†’ transáµ—Ê¸ e e' â‰…áµ‰ refláµ—Ê¸ â†’ e â‰…áµ‰ symáµ—Ê¸ e'
to-symáµ—Ê¸-eq ğ‘’ = transáµ‰ (symáµ‰ refláµ—Ê¸-unitÊ³) (transáµ‰ (transáµ—Ê¸-congÊ³ (symáµ‰ symáµ—Ê¸-invÊ³)) (transáµ‰ (symáµ‰ transáµ—Ê¸-assoc) (transáµ‰ (transáµ—Ê¸-congË¡ ğ‘’) refláµ—Ê¸-unitË¡)))

transáµ—Ê¸-cancelÊ³ : {e : T â‰…áµ—Ê¸ S} {e' : R â‰…áµ—Ê¸ S} â†’ transáµ—Ê¸ e (transáµ—Ê¸ (symáµ—Ê¸ e') e') â‰…áµ‰ e
transáµ—Ê¸-cancelÊ³ = transáµ‰ (transáµ—Ê¸-congÊ³ symáµ—Ê¸-invË¡) refláµ—Ê¸-unitÊ³

transáµ—Ê¸-cancelË¡ : {e : T â‰…áµ—Ê¸ S} {e' : S â‰…áµ—Ê¸ R} â†’ transáµ—Ê¸ (transáµ—Ê¸ (symáµ—Ê¸ e) e) e' â‰…áµ‰ e'
transáµ—Ê¸-cancelË¡ = transáµ‰ (transáµ—Ê¸-congË¡ symáµ—Ê¸-invË¡) refláµ—Ê¸-unitË¡


--------------------------------------------------
-- Substitution of types

_[_] : Ty Î“ â†’ Î” â‡’ Î“ â†’ Ty Î”
T [ Ïƒ ] âŸ¨ x , Î´ âŸ© = T âŸ¨ x , func Ïƒ Î´ âŸ©
_âŸª_,_âŸ«_ (_[_] {Î“ = Î“} T Ïƒ) f {Î´y}{Î´x} eÎ³-yx t = T âŸª f , proof âŸ« t
  where
    proof : Î“ âŸª f âŸ« func Ïƒ Î´y â‰¡ func Ïƒ Î´x
    proof = trans (naturality Ïƒ) (cong (func Ïƒ) eÎ³-yx)
ty-cong (T [ Ïƒ ]) f = ty-cong T f
ty-id (T [ Ïƒ ]) = strong-ty-id T
ty-comp (T [ Ïƒ ]) = strong-ty-comp T

ty-subst-id : (T : Ty Î“) â†’ T [ id-subst Î“ ] â‰…áµ—Ê¸ T
func (from (ty-subst-id T)) = id
naturality (from (ty-subst-id T)) = ty-cong T refl
func (to (ty-subst-id T)) = id
naturality (to (ty-subst-id T)) = ty-cong T refl
eq (isoË¡ (ty-subst-id T)) _ = refl
eq (isoÊ³ (ty-subst-id T)) _ = refl

ty-subst-comp : (T : Ty Î˜) (Ï„ : Î“ â‡’ Î˜) (Ïƒ : Î” â‡’ Î“) â†’ T [ Ï„ ] [ Ïƒ ] â‰…áµ—Ê¸ T [ Ï„ âŠš Ïƒ ]
func (from (ty-subst-comp T Ï„ Ïƒ)) = id
naturality (from (ty-subst-comp T Ï„ Ïƒ)) = ty-cong T refl
func (to (ty-subst-comp T Ï„ Ïƒ)) = id
naturality (to (ty-subst-comp T Ï„ Ïƒ)) = ty-cong T refl
eq (isoË¡ (ty-subst-comp T Ï„ Ïƒ)) _ = refl
eq (isoÊ³ (ty-subst-comp T Ï„ Ïƒ)) _ = refl

ty-subst-map : (Ïƒ : Î” â‡’ Î“) â†’ (T â†£ S) â†’ T [ Ïƒ ] â†£ S [ Ïƒ ]
func (ty-subst-map Ïƒ Î·) t = func Î· t
naturality (ty-subst-map Ïƒ Î·) = naturality Î·

ty-subst-map-cong : {Ïƒ : Î” â‡’ Î“} {Î· Ï† : T â†£ S} â†’
                    Î· â‰…â¿ Ï† â†’ ty-subst-map Ïƒ Î· â‰…â¿ ty-subst-map Ïƒ Ï†
eq (ty-subst-map-cong e) t = eq e t

ty-subst-map-id : (Ïƒ : Î” â‡’ Î“) â†’ ty-subst-map Ïƒ (id-trans T) â‰…â¿ id-trans (T [ Ïƒ ])
eq (ty-subst-map-id Ïƒ) t = refl

ty-subst-map-comp : (Ïƒ : Î” â‡’ Î“) (Ï† : S â†£ T) (Î· : R â†£ S) â†’
                    ty-subst-map Ïƒ (Ï† âŠ™ Î·) â‰…â¿ ty-subst-map Ïƒ Ï† âŠ™ ty-subst-map Ïƒ Î·
eq (ty-subst-map-comp Ïƒ Ï† Î·) t = refl

ty-subst-cong-ty : (Ïƒ : Î” â‡’ Î“) â†’ T â‰…áµ—Ê¸ S â†’ T [ Ïƒ ] â‰…áµ—Ê¸ S [ Ïƒ ]
from (ty-subst-cong-ty Ïƒ T=S) = ty-subst-map Ïƒ (from T=S)
to (ty-subst-cong-ty Ïƒ T=S) = ty-subst-map Ïƒ (to T=S)
eq (isoË¡ (ty-subst-cong-ty Ïƒ T=S)) t = eq (isoË¡ T=S) t
eq (isoÊ³ (ty-subst-cong-ty Ïƒ T=S)) t = eq (isoÊ³ T=S) t

ty-subst-cong-subst : {Ïƒ Ï„ : Î” â‡’ Î“} â†’ Ïƒ â‰…Ë¢ Ï„ â†’ (T : Ty Î“) â†’ T [ Ïƒ ] â‰…áµ—Ê¸ T [ Ï„ ]
func (from (ty-subst-cong-subst Ïƒ=Ï„ T)) {_}{Î´} t = ty-ctx-subst T (eq Ïƒ=Ï„ Î´) t
naturality (from (ty-subst-cong-subst Ïƒ=Ï„ T)) = ty-cong-2-2 T (trans hom-idË¡ (sym hom-idÊ³))
func (to (ty-subst-cong-subst Ïƒ=Ï„ T)) {_}{Î´} t = ty-ctx-subst T (sym (eq Ïƒ=Ï„ Î´)) t
naturality (to (ty-subst-cong-subst Ïƒ=Ï„ T)) = ty-cong-2-2 T (trans hom-idË¡ (sym hom-idÊ³))
eq (isoË¡ (ty-subst-cong-subst {Î“ = Î“} Ïƒ=Ï„ T)) t =
  -- Here we cannot use ty-id T twice because the omitted equality proofs are not ctx-id Î“ _
  -- (i.e. T âŸª_âŸ« t is not applied to the identity morphism in the category of elements of Î“).
  begin
    T âŸª hom-id , _ âŸ« T âŸª hom-id , _ âŸ« t
  â‰¡âŸ¨ ty-cong-2-1 T hom-idË¡ âŸ©
    T âŸª hom-id , ctx-id Î“ âŸ« t
  â‰¡âŸ¨ ty-id T âŸ©
    t âˆ
  where open â‰¡-Reasoning
eq (isoÊ³ (ty-subst-cong-subst Ïƒ=Ï„ T)) t =
  begin
    T âŸª hom-id , _ âŸ« T âŸª hom-id , _ âŸ« t
  â‰¡âŸ¨ ty-cong-2-1 T hom-idË¡ âŸ©
    T âŸª hom-id , _ âŸ« t
  â‰¡âŸ¨ ty-id T âŸ©
    t âˆ
  where open â‰¡-Reasoning

-- Substitution by Ïƒ : Î“ â‡’ Î” constitutes a groupoid morphism from Ty Î” to Ty Î“.
ty-subst-cong-ty-cong : {T S : Ty Î”} {e e' : T â‰…áµ—Ê¸ S} â†’ e â‰…áµ‰ e' â†’ (Ïƒ : Î“ â‡’ Î”) â†’
                        ty-subst-cong-ty Ïƒ e â‰…áµ‰ ty-subst-cong-ty Ïƒ e'
from-eq (ty-subst-cong-ty-cong ğ‘’ Ïƒ) = ty-subst-map-cong (from-eq ğ‘’)

ty-subst-cong-ty-refl : {Ïƒ : Î“ â‡’ Î”} {T : Ty Î”} â†’ ty-subst-cong-ty Ïƒ (refláµ—Ê¸ {T = T}) â‰…áµ‰ refláµ—Ê¸
from-eq ty-subst-cong-ty-refl = ty-subst-map-id _

ty-subst-cong-ty-sym : {Ïƒ : Î“ â‡’ Î”} {e : T â‰…áµ—Ê¸ S} â†’ ty-subst-cong-ty Ïƒ (symáµ—Ê¸ e) â‰…áµ‰ symáµ—Ê¸ (ty-subst-cong-ty Ïƒ e)
from-eq ty-subst-cong-ty-sym = reflâ¿

ty-subst-cong-ty-trans : {Ïƒ : Î“ â‡’ Î”} {e : T1 â‰…áµ—Ê¸ T2} {e' : T2 â‰…áµ—Ê¸ T3} â†’
                         ty-subst-cong-ty Ïƒ (transáµ—Ê¸ e e') â‰…áµ‰ transáµ—Ê¸ (ty-subst-cong-ty Ïƒ e) (ty-subst-cong-ty Ïƒ e')
from-eq ty-subst-cong-ty-trans = ty-subst-map-comp _ _ _

ty-subst-cong-natural : {Ïƒ Ï„ : Î“ â‡’ Î”} (Îµ : Ïƒ â‰…Ë¢ Ï„) (e : T â‰…áµ—Ê¸ S) â†’
                        transáµ—Ê¸ (ty-subst-cong-subst Îµ T) (ty-subst-cong-ty Ï„ e) â‰…áµ‰ transáµ—Ê¸ (ty-subst-cong-ty Ïƒ e) (ty-subst-cong-subst Îµ S)
eq (from-eq (ty-subst-cong-natural Îµ e)) _ = sym (naturality (from e))

ty-subst-cong-subst-refl : {Ïƒ : Î“ â‡’ Î”} â†’ ty-subst-cong-subst (reflË¢ {Ïƒ = Ïƒ}) T â‰…áµ‰ refláµ—Ê¸
eq (from-eq (ty-subst-cong-subst-refl {T = T})) _ = strong-ty-id T

ty-subst-cong-subst-sym : {Ïƒ Ï„ : Î“ â‡’ Î”} {Îµ : Ïƒ â‰…Ë¢ Ï„} â†’ ty-subst-cong-subst (symË¢ Îµ) T â‰…áµ‰ symáµ—Ê¸ (ty-subst-cong-subst Îµ T)
eq (from-eq ty-subst-cong-subst-sym) _ = refl

ty-subst-cong-subst-trans : {Ïƒ1 Ïƒ2 Ïƒ3 : Î“ â‡’ Î”} {Îµ : Ïƒ1 â‰…Ë¢ Ïƒ2} {Îµ' : Ïƒ2 â‰…Ë¢ Ïƒ3} â†’
                            ty-subst-cong-subst (transË¢ Îµ Îµ') T â‰…áµ‰ transáµ—Ê¸ (ty-subst-cong-subst Îµ T) (ty-subst-cong-subst Îµ' T)
eq (from-eq (ty-subst-cong-subst-trans {T = T})) _ = sym (ty-cong-2-1 T hom-idÊ³)

ty-subst-cong-subst-2-1 : {Ïƒ1 : Î“ â‡’ Î”} {Ïƒ2 : Î” â‡’ Î˜} {Ï„ : Î“ â‡’ Î˜}
                          (T : Ty Î˜) â†’ Ïƒ2 âŠš Ïƒ1 â‰…Ë¢ Ï„ â†’
                          T [ Ïƒ2 ] [ Ïƒ1 ] â‰…áµ—Ê¸ T [ Ï„ ]
ty-subst-cong-subst-2-1 T Îµ = transáµ—Ê¸ (ty-subst-comp T _ _) (ty-subst-cong-subst Îµ T)

ty-subst-cong-subst-2-2 : {Î”' : Ctx C} {Ïƒ1 : Î“ â‡’ Î”} {Ïƒ2 : Î” â‡’ Î˜} {Ï„1 : Î“ â‡’ Î”'} {Ï„2 : Î”' â‡’ Î˜}
                          (T : Ty Î˜) â†’ Ïƒ2 âŠš Ïƒ1 â‰…Ë¢ Ï„2 âŠš Ï„1 â†’
                          T [ Ïƒ2 ] [ Ïƒ1 ] â‰…áµ—Ê¸ T [ Ï„2 ] [ Ï„1 ]
ty-subst-cong-subst-2-2 T Îµ =
  transáµ—Ê¸ (ty-subst-comp T _ _) (transáµ—Ê¸ (ty-subst-cong-subst Îµ T) (symáµ—Ê¸ (ty-subst-comp T _ _)))

-- Nicer syntax for substitutions coming from context equality
Î¹c[_]_ : Î“ â‰…á¶œ Î” â†’ Ty Î” â†’ Ty Î“
Î¹c[ Î“=Î” ] T = T [ from Î“=Î” ]

Î¹câ»Â¹[_]_ : Î“ â‰…á¶œ Î” â†’ Ty Î“ â†’ Ty Î”
Î¹câ»Â¹[ Î“=Î” ] T = T [ to Î“=Î” ]
