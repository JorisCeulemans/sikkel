--------------------------------------------------
-- Context extension
--------------------------------------------------

open import Model.BaseCategory

module Model.CwF-Structure.ContextExtension {C : BaseCategory} where

open import Data.Product using (Î£; Î£-syntax; projâ‚; projâ‚‚; _Ã—_) renaming (_,_ to [_,_])
open import Data.String
open import Relation.Binary.PropositionalEquality hiding ([_]; naturality)

open import Model.Helpers
open import Model.CwF-Structure.Context
open import Model.CwF-Structure.ContextEquivalence
open import Model.CwF-Structure.Type
open import Model.CwF-Structure.Term

open BaseCategory C

infixl 15 _,,_
infixl 15 _,,_âˆˆ_

private
  variable
    Î“ Î” Î˜ : Ctx C
    T S R : Ty Î“


-- Definition of the extension of a context Î“ with a type T.
-- In MLTT, this would be written as Î“, x : T.
_,,_ : (Î“ : Ctx C) (T : Ty Î“) â†’ Ctx C
(Î“ ,, T) âŸ¨ x âŸ© = Î£[ Î³ âˆˆ Î“ âŸ¨ x âŸ© ] (T âŸ¨ x , Î³ âŸ©)
(Î“ ,, T) âŸª f âŸ« [ Î³ , t ] = [ Î“ âŸª f âŸ« Î³ , T âŸª f , refl âŸ« t ]
ctx-id (Î“ ,, T) = to-Î£-ty-eq T (ctx-id Î“) (trans (ty-cong-2-1 T hom-idË¡) (ty-id T))
ctx-comp (Î“ ,, T) = to-Î£-ty-eq T (ctx-comp Î“) (ty-cong-2-2 T hom-idÊ³)

Ï€ : Î“ ,, T â‡’ Î“
func Ï€ = projâ‚
naturality Ï€ = refl

-- A term corresponding to the last variable in the context. In MLTT, this would be
-- written as Î“, x : T âŠ¢ x : T. Note that the type of the term is T [ Ï€ ] instead of
-- T because the latter is not a type in context Î“ ,, T.
Î¾ : Tm (Î“ ,, T) (T [ Ï€ ])
Î¾ âŸ¨ _ , [ _ , t ] âŸ©' = t
naturality Î¾ _ refl = refl

-- In any cwf, there is by definition a one-to-one correspondence between substitutions
-- Î” â‡’ Î“ ,, T and pairs of type Î£[ Ïƒ : Î” â‡’ Î“ ] (Tm Î” (T [ Ïƒ ])). This is worked out
-- in the following functions.
ext-subst-to-subst : (Î” â‡’ Î“ ,, T) â†’ (Î” â‡’ Î“)
ext-subst-to-subst Ï„ = Ï€ âŠš Ï„

ext-subst-to-term : (Ï„ : Î” â‡’ Î“ ,, T) â†’ Tm Î” (T [ Ï€ âŠš Ï„ ])
ext-subst-to-term {T = T} Ï„ = Î¹â»Â¹[ ty-subst-comp T Ï€ Ï„ ] (Î¾ [ Ï„ ]')

to-ext-subst : (T : Ty Î“) (Ïƒ : Î” â‡’ Î“) â†’ Tm Î” (T [ Ïƒ ]) â†’ (Î” â‡’ Î“ ,, T)
func (to-ext-subst T Ïƒ t) Î´ = [ func Ïƒ Î´ , t âŸ¨ _ , Î´ âŸ©' ]
naturality (to-ext-subst {Î” = Î”} T Ïƒ t) {Î´ = Î´} = to-Î£-ty-eq T (naturality Ïƒ)
                                                               (trans (ty-cong-2-1 T hom-idÊ³) (naturality t _ refl))

syntax to-ext-subst T Ïƒ t = âŸ¨ Ïƒ , t âˆˆ T âŸ©

to-ext-subst-syntax : {T : Ty Î“} (Ïƒ : Î” â‡’ Î“) â†’ Tm Î” (T [ Ïƒ ]) â†’ (Î” â‡’ Î“ ,, T)
to-ext-subst-syntax Ïƒ t = to-ext-subst _ Ïƒ t
infixl 4 to-ext-subst-syntax
syntax to-ext-subst-syntax Ïƒ t = Ïƒ âˆ·Ë¢ t

ctx-ext-subst-Î²â‚ : (Ïƒ : Î” â‡’ Î“) (t : Tm Î” (T [ Ïƒ ])) â†’ Ï€ âŠš âŸ¨ Ïƒ , t âˆˆ T âŸ© â‰…Ë¢ Ïƒ
eq (ctx-ext-subst-Î²â‚ Ïƒ t) Î´ = refl

ctx-ext-subst-Î²â‚‚ : (Ïƒ : Î” â‡’ Î“) (t : Tm Î” (T [ Ïƒ ])) â†’
                   Î¾ [ âŸ¨ Ïƒ , t âˆˆ T âŸ© ]' â‰…áµ—áµ Î¹[ ty-subst-cong-subst-2-1 T (ctx-ext-subst-Î²â‚ Ïƒ t) ] t
eq (ctx-ext-subst-Î²â‚‚ {T = T} Ïƒ t) _ = sym (strong-ty-id T)

ctx-ext-subst-Î· : (Ï„ : Î” â‡’ Î“ ,, T) â†’ âŸ¨ Ï€ âŠš Ï„ , ext-subst-to-term Ï„ âˆˆ T âŸ© â‰…Ë¢ Ï„
eq (ctx-ext-subst-Î· Ï„) Î´ = refl

-- Some consequences of the properties above
ctx-ext-subst-cong-subst : {Ïƒ Ïƒ' : Î” â‡’ Î“} (Îµ : Ïƒ â‰…Ë¢ Ïƒ') (t : Tm Î” (T [ Ïƒ' ])) â†’
                           âŸ¨ Ïƒ , Î¹[ ty-subst-cong-subst Îµ T ] t âˆˆ T âŸ© â‰…Ë¢ âŸ¨ Ïƒ' , t âˆˆ T âŸ©
eq (ctx-ext-subst-cong-subst {T = T} Îµ t) Î´ = to-Î£-ty-eq T (eq Îµ Î´) (trans (ty-cong-2-1 T hom-idË¡) (ty-id T))

ctx-ext-subst-cong-tm : (Ïƒ : Î” â‡’ Î“) {t t' : Tm Î” (T [ Ïƒ ])} â†’ t â‰…áµ—áµ t' â†’ âŸ¨ Ïƒ , t âˆˆ T âŸ© â‰…Ë¢ âŸ¨ Ïƒ , t' âˆˆ T âŸ©
eq (ctx-ext-subst-cong-tm Ïƒ ğ’†) Î´ = cong [ _ ,_] (eq ğ’† Î´)

ctx-ext-subst-projâ‚‚ : (Ïƒ : Î” â‡’ Î“) (t : Tm Î” (T [ Ïƒ ])) â†’
                      ext-subst-to-term âŸ¨ Ïƒ , t âˆˆ T âŸ© â‰…áµ—áµ Î¹[ ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ t) T ] t
eq (ctx-ext-subst-projâ‚‚ {Î“ = Î“}{T = T} Ïƒ t) Î´ = sym (strong-ty-id T)

ctx-ext-subst-comp : (Ïƒ : Î“ â‡’ Î˜) (t : Tm Î“ (T [ Ïƒ ])) (Ï„ : Î” â‡’ Î“) â†’
                     âŸ¨ Ïƒ , t âˆˆ T âŸ© âŠš Ï„ â‰…Ë¢ âŸ¨ Ïƒ âŠš Ï„ , Î¹â»Â¹[ ty-subst-comp T Ïƒ Ï„ ] (t [ Ï„ ]') âˆˆ T âŸ©
eq (ctx-ext-subst-comp Ïƒ t Ï„) Î´ = refl

-- Substitution of the last variable in context Î“ ,, T with a term in Tm Î“ T.
tm-to-subst : Tm Î“ T â†’ Î“ â‡’ Î“ ,, T
tm-to-subst {Î“ = Î“}{T = T} t = âŸ¨ id-subst Î“ , t [ id-subst Î“ ]' âˆˆ T âŸ©

_/v = tm-to-subst

/v-cong : {t t' : Tm Î“ T} â†’ t â‰…áµ—áµ t' â†’ t /v â‰…Ë¢ t' /v
/v-cong e = ctx-ext-subst-cong-tm _ (tm-subst-cong-tm _ e)

_âŠ¹ : (Ïƒ : Î” â‡’ Î“) â†’ Î” ,, T [ Ïƒ ] â‡’ Î“ ,, T
_âŠ¹ {Î” = Î”} {T = T} Ïƒ = âŸ¨ Ïƒ âŠš Ï€ , Î¹â»Â¹[ ty-subst-comp T Ïƒ Ï€ ] Î¾ âˆˆ T âŸ©

âŠ¹-Ï€-comm : (Ïƒ : Î” â‡’ Î“) â†’ Ï€ {T = T} âŠš (Ïƒ âŠ¹) â‰…Ë¢ Ïƒ âŠš Ï€
eq (âŠ¹-Ï€-comm Ïƒ) Î´ = refl

ty-eq-to-ext-subst : (Î“ : Ctx C) {T : Ty Î“} {T' : Ty Î“} â†’
                     T â‰…áµ—Ê¸ T' â†’ Î“ ,, T â‡’ Î“ ,, T'
ty-eq-to-ext-subst Î“ {T = T}{T'} T=T' = âŸ¨ Ï€ , Î¹â»Â¹[ ty-subst-cong-ty Ï€ T=T' ] Î¾ âˆˆ T' âŸ©

Ï€-ext-comp-ty-subst : (Ïƒ : Î” â‡’ Î“ ) (t : Tm Î” (T [ Ïƒ ])) (S : Ty Î“) â†’
                      S [ Ï€ ] [ âŸ¨ Ïƒ , t âˆˆ T âŸ© ] â‰…áµ—Ê¸ S [ Ïƒ ]
Ï€-ext-comp-ty-subst Ïƒ t S = transáµ—Ê¸ (ty-subst-comp S Ï€ âŸ¨ Ïƒ , t âˆˆ _ âŸ©) (ty-subst-cong-subst (ctx-ext-subst-Î²â‚ Ïƒ t) S)

ty-weaken-subst : (t : Tm Î“ T) â†’ S [ Ï€ ] [ t /v ] â‰…áµ—Ê¸ S
ty-weaken-subst t = transáµ—Ê¸ (Ï€-ext-comp-ty-subst _ _ _) (ty-subst-id _)

-- Extending a context with two equivalent types leads to equivalent contexts.
,,-map : (T â†£ S) â†’ (Î“ ,, T â‡’ Î“ ,, S)
,,-map Î· = Ï€ âˆ·Ë¢ convert-tm (ty-subst-map Ï€ Î·) Î¾

,,-map-cong : {Î· Ï† : T â†£ S} â†’ Î· â‰…â¿ Ï† â†’ ,,-map Î· â‰…Ë¢ ,,-map Ï†
,,-map-cong ğ”¢ = ctx-ext-subst-cong-tm Ï€ (convert-tm-cong-trans (ty-subst-map-cong ğ”¢))

,,-map-id : {T : Ty Î“} â†’ ,,-map (id-trans T) â‰…Ë¢ id-subst (Î“ ,, T)
eq ,,-map-id _ = refl

,,-map-comp : (Î· : S â†£ T) (Ï† : R â†£ S) â†’ ,,-map (Î· âŠ™ Ï†) â‰…Ë¢ ,,-map Î· âŠš ,,-map Ï†
eq (,,-map-comp Î· Ï†) _ = refl

,,-cong : T â‰…áµ—Ê¸ S â†’ Î“ ,, T â‰…á¶œ Î“ ,, S
from (,,-cong T=S) = ,,-map (from T=S)
to (,,-cong T=S) = ,,-map (to T=S)
eq (isoË¡ (,,-cong T=S)) [ Î³ , t ] = cong [ Î³ ,_] (eq (isoË¡ T=S) t)
eq (isoÊ³ (,,-cong T=S)) [ Î³ , s ] = cong [ Î³ ,_] (eq (isoÊ³ T=S) s)

,,-map-Ï€ : (Ï† : T â†£ S) â†’ Ï€ âŠš ,,-map Ï† â‰…Ë¢ Ï€
,,-map-Ï€ Ï† = ctx-ext-subst-Î²â‚ Ï€ _

Î¾-convert : (Ï† : T â†£ S) â†’ convert-tm (ty-subst-map Ï€ Ï†) Î¾ â‰…áµ—áµ Î¹â»Â¹[ ty-subst-cong-subst-2-1 S (,,-map-Ï€ Ï†) ] (Î¾ [ ,,-map Ï† ]')
eq (Î¾-convert {S = S} Ï†) _ = sym (strong-ty-id S)

,,-cong-Î¾ : (e : T â‰…áµ—Ê¸ S) â†’
  Î¾ [ from (,,-cong e) ]'
    â‰…áµ—áµ
  Î¹[ ty-subst-comp S Ï€ (from (,,-cong e)) ] Î¹[ ty-subst-cong-subst (,,-map-Ï€ (from e)) S ]  Î¹â»Â¹[ ty-subst-cong-ty Ï€ e ] Î¾
eq (,,-cong-Î¾ {S = S} e) _ = sym (strong-ty-id S)

,,-cong-ext-subst : (e : T â‰…áµ—Ê¸ S) {Ïƒ : Î“ â‡’ Î”} {t : Tm Î“ (T [ Ïƒ ])} â†’
                    from (,,-cong e) âŠš to-ext-subst T Ïƒ t â‰…Ë¢ to-ext-subst S Ïƒ (Î¹â»Â¹[ ty-subst-cong-ty Ïƒ e ] t)
eq (,,-cong-ext-subst e) _ = refl

-- Context extension which includes a variable name
_,,_âˆˆ_ : (Î“ : Ctx C) â†’ String â†’ (T : Ty Î“) â†’ Ctx C
Î“ ,, v âˆˆ T = Î“ ,, T
