{-# OPTIONS --omega-in-omega #-}

--------------------------------------------------
-- An example of representation independence using
-- unary parametricity
--------------------------------------------------

module Parametricity.Unary where

open import Data.Nat
open import Data.Product renaming (_,_ to [_,_])
open import Data.Sum hiding ([_,_])
open import Function using (id)
open import Level using (Level; SetÏ‰; 0â„“)
open import Relation.Binary.PropositionalEquality hiding ([_])
open import Relation.Unary hiding (_â‡’_)

open import Categories
open import CwF-Structure
open import Types.Functions
open import Types.Products
open import Reflection.Naturality
open import Reflection.Naturality.Instances
open import Reflection.Tactic.Lambda
open import Reflection.SubstitutionSequence

private
  variable
    â„“ â„“' â„“'' : Level
    fâ„“ : Level â†’ Level
    Î“ : Ctx ğŸš â„“


--------------------------------------------------
-- Constructing an embedded type in base category ğŸš
-- using an Agda type and a predicate

PrimFromPred : (A : Set â„“) â†’ Pred A â„“ â†’ Ty {C = ğŸš} â—‡ â„“
type (PrimFromPred A P) type-obj _ = A
type (PrimFromPred A P) pred-obj _ = Î£[ a âˆˆ A ] P a
morph (PrimFromPred A P) type-id _ = id
morph (PrimFromPred A P) pred-id _ = id
morph (PrimFromPred A P) type-pred _ = projâ‚
morph-cong (PrimFromPred A P) refl {eÎ³ = refl} {eÎ³' = refl} = refl
morph-id (PrimFromPred A P) {x = type-obj} _ = refl
morph-id (PrimFromPred A P) {x = pred-obj} _ = refl
morph-comp (PrimFromPred A P) type-id g refl refl _ = refl
morph-comp (PrimFromPred A P) pred-id g refl refl _ = refl
morph-comp (PrimFromPred A P) type-pred pred-id _ _ _ = refl

FromPred : (A : Set â„“) â†’ Pred A â„“ â†’ NullaryTypeOp ğŸš (Î» _ â†’ â„“)
FromPred A P {Î“ = Î“} = PrimFromPred A P [ !â—‡ Î“ ]

instance
  frompred-natural : {A : Set â„“} {P : Pred A â„“} â†’ IsNullaryNatural (FromPred A P)
  natural-nul {{frompred-natural}} Ïƒ = ty-subst-seq-cong (!â—‡ _ âˆ· Ïƒ â—¼) (!â—‡ _ â—¼) (PrimFromPred _ _) (â—‡-terminal _ _ _)

from-pred : {A : Set â„“} {P : Pred A â„“} (a : A) â†’ P a â†’ Tm Î“ (FromPred A P)
term (from-pred a p) type-obj _ = a
term (from-pred a p) pred-obj _ = [ a , p ]
Tm.naturality (from-pred a p) type-id _ = refl
Tm.naturality (from-pred a p) pred-id _ = refl
Tm.naturality (from-pred a p) type-pred _ = refl

from-pred1 : {A : Set â„“}  {P : Pred A â„“}
             {B : Set â„“'} {Q : Pred B â„“'}
             (f : A â†’ B) â†’ (P âŸ¨â†’âŸ© Q) f â†’
             Tm (Î“ ,, FromPred A P) (FromPred B Q)
term (from-pred1 f g) type-obj [ _ , a ] = f a
term (from-pred1 f g) pred-obj [ _ , [ a , p ] ] = [ f a , g p ]
Tm.naturality (from-pred1 f g) type-id refl = refl
Tm.naturality (from-pred1 f g) pred-id refl = refl
Tm.naturality (from-pred1 f g) type-pred refl = refl

from-pred2 : âˆ€ {â„“a â„“b â„“c}
             {A : Set â„“a} {P : Pred A â„“a}
             {B : Set â„“b} {Q : Pred B â„“b}
             {C : Set â„“c} {R : Pred C â„“c}
             (f : A â†’ B â†’ C) â†’ (P âŸ¨â†’âŸ© Q âŸ¨â†’âŸ© R) f â†’
             Tm (Î“ ,, FromPred A P âŠ  FromPred B Q) (FromPred C R)
term (from-pred2 f g) type-obj [ _ , [ a , b ] ] = f a b
term (from-pred2 f g) pred-obj [ _ , [ [ a , p ] , [ b , q ] ] ] = [ f a b , g p q ]
Tm.naturality (from-pred2 f g) type-id refl = refl
Tm.naturality (from-pred2 f g) pred-id refl = refl
Tm.naturality (from-pred2 f g) type-pred refl = refl


--------------------------------------------------
-- Example: types representing booleans

record BoolStructure (B : NullaryTypeOp ğŸš fâ„“) {{_ : IsNullaryNatural B}} : SetÏ‰ where
  field
    prim-and : Tm (Î“ ,, B âŠ  B) B
    prim-not : Tm (Î“ ,, B) B

  and : Tm Î“ (B âŠ  B â‡› B)
  and = lamÎ¹ (B âŠ  B) prim-and
  
  not : Tm Î“ (B â‡› B)
  not = lamÎ¹ B prim-not

open BoolStructure {{...}}

or : (B : NullaryTypeOp ğŸš fâ„“) {{_ : IsNullaryNatural B}} {{_ : BoolStructure B}} â†’ Tm Î“ (B â‡› B â‡› B)
or B = lamÎ¹[ "b1" âˆˆ B ] lamÎ¹[ "b2" âˆˆ B ] not $ (and $ pair (not $ varÎ¹ "b1") (not $ varÎ¹ "b2"))

-- Representing booleans as natural numbers (0 = false, 1 = true)
data IsBit : Pred â„• 0â„“ where
  0-bit : IsBit 0
  1-bit : IsBit 1

PrimBinaryBool : Ty {C = ğŸš} â—‡ 0â„“
PrimBinaryBool = PrimFromPred â„• IsBit

BinaryBool : NullaryTypeOp ğŸš (Î» _ â†’ 0â„“)
BinaryBool = FromPred â„• IsBit

instance
  binarybool-is-bool : BoolStructure BinaryBool
  prim-and {{binarybool-is-bool}} = from-pred2 _âŠ“_ âŠ“-preserves-bitness
    where
      âŠ“-preserves-bitness : (IsBit âŸ¨â†’âŸ© IsBit âŸ¨â†’âŸ© IsBit) _âŠ“_
      âŠ“-preserves-bitness 0-bit _     = 0-bit
      âŠ“-preserves-bitness 1-bit 0-bit = 0-bit
      âŠ“-preserves-bitness 1-bit 1-bit = 1-bit
  prim-not {{binarybool-is-bool}} = from-pred1 (1 âˆ¸_) 1âˆ¸-preserves-bitness
    where
      1âˆ¸-preserves-bitness : (IsBit âŸ¨â†’âŸ© IsBit) (1 âˆ¸_)
      1âˆ¸-preserves-bitness 0-bit = 1-bit
      1âˆ¸-preserves-bitness 1-bit = 0-bit

âŠ-trans : {A : Set â„“} {x y z w : A} â†’ x â‰¡ y â†’ y â‰¡ z âŠ y â‰¡ w â†’ x â‰¡ z âŠ x â‰¡ w
âŠ-trans e = Data.Sum.map (trans e) (trans e)

module _ (b : Tm â—‡ BinaryBool) where
  translate-b : â„•
  translate-b = b âŸ¨ type-obj , _ âŸ©'

  type-pred-result : (x : PrimBinaryBool âŸ¨ pred-obj , _ âŸ©) â†’
                     PrimBinaryBool âŸª type-pred , refl âŸ« x â‰¡ 0 âŠ PrimBinaryBool âŸª type-pred , refl âŸ« x â‰¡ 1
  type-pred-result [ .0 , 0-bit ] = injâ‚ refl
  type-pred-result [ .1 , 1-bit ] = injâ‚‚ refl

  result : translate-b â‰¡ 0 âŠ translate-b â‰¡ 1
  result = âŠ-trans (sym (Tm.naturality b type-pred refl)) (type-pred-result (b âŸ¨ pred-obj , _ âŸ©'))

  result' : IsBit translate-b
  result' with b âŸ¨ pred-obj , _ âŸ©' | Tm.naturality b type-pred refl
  result' | [ _ , p ] | refl = p


open import Data.Unit
open import Data.Empty.Polymorphic

always-false : Ctx â˜… â„“ â†’ Ctx ğŸš â„“
set (always-false Î“) type-obj = Î“ âŸ¨ tt âŸ©
set (always-false Î“) pred-obj = âŠ¥
rel (always-false Î“) type-id = id
rel (always-false Î“) pred-id = id
rel (always-false Î“) type-pred = âŠ¥-elim
rel-id (always-false Î“) {x = type-obj} _ = refl
rel-comp (always-false Î“) type-id g _ = refl
rel-comp (always-false Î“) pred-id g _ = refl
rel-comp (always-false Î“) type-pred pred-id _ = refl

always-false-subst : {Î” : Ctx â˜… â„“} {Î“ : Ctx â˜… â„“'} â†’ Î” â‡’ Î“ â†’ always-false Î” â‡’ always-false Î“
func (always-false-subst Ïƒ) {x = type-obj} = func Ïƒ
func (always-false-subst Ïƒ) {x = pred-obj} = âŠ¥-elim
_â‡’_.naturality (always-false-subst Ïƒ) {f = type-id} _ = refl

always-false-subst-id : {Î“ : Ctx â˜… â„“} â†’ always-false-subst (id-subst Î“) â‰…Ë¢ id-subst (always-false Î“)
eq always-false-subst-id {x = type-obj} _ = refl

always-false-subst-âŠš : {Î” : Ctx â˜… â„“} {Î“ : Ctx â˜… â„“'} {Î˜ : Ctx â˜… â„“''} (Ïƒ : Î“ â‡’ Î˜) (Ï„ : Î” â‡’ Î“) â†’
                       always-false-subst (Ïƒ âŠš Ï„) â‰…Ë¢ always-false-subst Ïƒ âŠš always-false-subst Ï„
eq (always-false-subst-âŠš Ïƒ Ï„) {x = type-obj} _ = refl

forget : {Î“ : Ctx â˜… â„“} â†’ Ty (always-false Î“) â„“' â†’ Ty Î“ â„“'
type (forget T) tt Î³ = T âŸ¨ type-obj , Î³ âŸ©
morph (forget {Î“ = Î“} T) tt eÎ³ = T âŸª type-id , trans (sym (rel-id Î“ _ )) eÎ³ âŸ«
morph-cong (forget T) refl {eÎ³ = refl} {eÎ³' = refl} = refl
morph-id (forget T) t = trans (morph-cong T refl) (morph-id T t)
morph-comp (forget T) _ _ _ _ t = sym (morph-cong-2-1 T refl)

module _ {Î“ : Ctx â˜… â„“} {T : Ty (always-false Î“) â„“'} where
  forget-intro : Tm (always-false Î“) T â†’ Tm Î“ (forget T)
  term (forget-intro t) tt Î³ = t âŸ¨ type-obj , Î³ âŸ©'
  Tm.naturality (forget-intro t) tt _ = Tm.naturality t type-id _

  forget-elim : Tm Î“ (forget T) â†’ Tm (always-false Î“) T
  term (forget-elim t) type-obj Î³ = t âŸ¨ tt , Î³ âŸ©'
  Tm.naturality (forget-elim t) type-id eÎ³ = trans (morph-cong T refl) (Tm.naturality t tt (trans (rel-id Î“ _) eÎ³))

forget-natural : {Î” : Ctx â˜… â„“} {Î“ : Ctx â˜… â„“'} (Ïƒ : Î” â‡’ Î“)
                 {T : Ty (always-false Î“) â„“''} â†’
                 (forget T) [ Ïƒ ] â‰…áµ—Ê¸ forget (T [ always-false-subst Ïƒ ])
func (from (forget-natural Ïƒ)) = id
CwF-Structure.naturality (from (forget-natural Ïƒ {T = T})) _ = morph-cong T refl
func (to (forget-natural Ïƒ)) = id
CwF-Structure.naturality (to (forget-natural Ïƒ {T = T})) _ = morph-cong T refl
eq (isoË¡ (forget-natural Ïƒ)) _ = refl
eq (isoÊ³ (forget-natural Ïƒ)) _ = refl

forget-cong : {Î“ : Ctx â˜… â„“} {T : Ty (always-false Î“) â„“'} {T' : Ty (always-false Î“) â„“''} â†’
              T â‰…áµ—Ê¸ T' â†’ forget T â‰…áµ—Ê¸ forget T'
func (from (forget-cong T=T')) = func (from T=T')
CwF-Structure.naturality (from (forget-cong T=T')) = CwF-Structure.naturality (from T=T')
func (to (forget-cong T=T')) = func (to T=T')
CwF-Structure.naturality (to (forget-cong T=T')) = CwF-Structure.naturality (to T=T')
eq (isoË¡ (forget-cong T=T')) = eq (isoË¡ T=T')
eq (isoÊ³ (forget-cong T=T')) = eq (isoÊ³ T=T')

instance
  always-false-functor : IsCtxFunctor always-false
  ctx-map {{always-false-functor}} = always-false-subst
  ctx-map-id {{always-false-functor}} = always-false-subst-id
  ctx-map-âŠš {{always-false-functor}} = always-false-subst-âŠš

  forget-unarynat : IsUnaryNatural forget
  natural-un {{forget-unarynat}} = forget-natural
  cong-un {{forget-unarynat}} = forget-cong


infixl 12 _âŠ›_
_âŠ›_ : {Î“ : Ctx â˜… â„“} {A B : Ty (always-false Î“) â„“'} â†’
      Tm Î“ (forget (A â‡› B)) â†’ Tm Î“ (forget A) â†’ Tm Î“ (forget B)
f âŠ› a = forget-intro (forget-elim f $ forget-elim a)

binary-or : Tm Î“ (BinaryBool â‡› BinaryBool â‡› BinaryBool)
binary-or = or BinaryBool

binary-orâ˜… : {Î“ : Ctx â˜… 0â„“} â†’ Tm Î“ (forget BinaryBool â‡› forget BinaryBool â‡› forget BinaryBool)
binary-orâ˜… = lamÎ¹[ "x" âˆˆ forget BinaryBool ] lamÎ¹[ "y" âˆˆ forget BinaryBool ]
             forget-intro binary-or âŠ› varÎ¹ "x" âŠ› varÎ¹ "y"

open import Translation

instance
  forget-pred : {A : Set â„“} {P : Pred A â„“} â†’ Translatable (forget (FromPred A P))
  Translatable.translated-type (forget-pred {A = A}) = A
  Translatable.translate-term forget-pred t = t âŸ¨ tt , tt âŸ©'
  Translatable.translate-back forget-pred a = MkTm (Î» _ _ â†’ a) (Î» _ _ â†’ refl)
  Translatable.translate-cong forget-pred t=s = eq t=s tt
{-
binary-or-agda : â„• â†’ â„• â†’ â„•
binary-or-agda = translate-term binary-orâ˜…

translate-result : (IsBit âŸ¨â†’âŸ© IsBit âŸ¨â†’âŸ© IsBit) binary-or-agda
translate-result {m} x {n} y = projâ‚‚ ((binary-or {Î“ = â—‡} â‚¬âŸ¨ pred-obj , tt âŸ© [ m , x ]) $âŸ¨ pred-id , refl âŸ© [ n , y ])
-}
