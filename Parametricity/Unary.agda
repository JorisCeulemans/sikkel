--------------------------------------------------
-- An example of representation independence using
-- unary parametricity
--------------------------------------------------

module Parametricity.Unary where

open import Data.Empty
open import Data.Nat
open import Data.Product renaming (_,_ to [_,_])
open import Data.Sum hiding ([_,_])
open import Data.Unit
open import Function using (id)
open import Level using (0â„“)
open import Relation.Binary.PropositionalEquality hiding ([_])
open import Relation.Unary hiding (_â‡’_)

open import Categories
open import CwF-Structure
open import Types.Functions
open import Types.Products
open import Types.Instances
open import Modalities
open import Translation
open import Reflection.Naturality.TypeOperations
open import Reflection.Tactic.Lambda
open import Reflection.SubstitutionSequence

private
  variable
    Î“ : Ctx ğŸš


--------------------------------------------------
-- Constructing an embedded type in base category ğŸš
-- using an Agda type and a predicate

PrimFromPred : (A : Set) â†’ Pred A 0â„“ â†’ Ty {C = ğŸš} â—‡
PrimFromPred A P âŸ¨ type-obj , _ âŸ© = A
PrimFromPred A P âŸ¨ pred-obj , _ âŸ© = Î£[ a âˆˆ A ] P a
PrimFromPred A P âŸª type-id , _ âŸ« a = a
PrimFromPred A P âŸª pred-id , _ âŸ« x = x
PrimFromPred A P âŸª type-pred , _ âŸ« [ a , p ] = a
ty-cong (PrimFromPred A P) refl {eÎ³ = refl} {eÎ³' = refl} = refl
ty-id (PrimFromPred A P) {x = type-obj} _ = refl
ty-id (PrimFromPred A P) {x = pred-obj} _ = refl
ty-comp (PrimFromPred A P) type-id g refl refl _ = refl
ty-comp (PrimFromPred A P) pred-id g refl refl _ = refl
ty-comp (PrimFromPred A P) type-pred pred-id _ _ _ = refl

FromPred : (A : Set) â†’ Pred A 0â„“ â†’ ClosedType ğŸš
FromPred A P {Î“ = Î“} = PrimFromPred A P [ !â—‡ Î“ ]

instance
  frompred-natural : {A : Set} {P : Pred A 0â„“} â†’ IsClosedNatural (FromPred A P)
  natural-nul {{frompred-natural}} Ïƒ = ty-subst-seq-cong (!â—‡ _ âˆ· Ïƒ â—¼) (!â—‡ _ â—¼) (PrimFromPred _ _) (â—‡-terminal _ _ _)

from-pred : {A : Set} {P : Pred A 0â„“} (a : A) â†’ P a â†’ Tm Î“ (FromPred A P)
from-pred a p âŸ¨ type-obj , _ âŸ©' = a
from-pred a p âŸ¨ pred-obj , _ âŸ©' = [ a , p ]
Tm.naturality (from-pred a p) type-id _ = refl
Tm.naturality (from-pred a p) pred-id _ = refl
Tm.naturality (from-pred a p) type-pred _ = refl

from-pred1 : {A B : Set} {P : Pred A 0â„“} {Q : Pred B 0â„“}
             (f : A â†’ B) â†’ (P âŸ¨â†’âŸ© Q) f â†’
             Tm (Î“ ,, FromPred A P) (FromPred B Q)
from-pred1 f g âŸ¨ type-obj , [ _ , a ] âŸ©' = f a
from-pred1 f g âŸ¨ pred-obj , [ _ , [ a , p ] ] âŸ©' = [ f a , g p ]
Tm.naturality (from-pred1 f g) type-id refl = refl
Tm.naturality (from-pred1 f g) pred-id refl = refl
Tm.naturality (from-pred1 f g) type-pred refl = refl

from-pred2 : {A : Set} {P : Pred A 0â„“}
             {B : Set} {Q : Pred B 0â„“}
             {C : Set} {R : Pred C 0â„“}
             (f : A â†’ B â†’ C) â†’ (P âŸ¨â†’âŸ© Q âŸ¨â†’âŸ© R) f â†’
             Tm (Î“ ,, FromPred A P âŠ  FromPred B Q) (FromPred C R)
from-pred2 f g âŸ¨ type-obj , [ _ , [ a , b ] ] âŸ©' = f a b
from-pred2 f g âŸ¨ pred-obj , [ _ , [ [ a , p ] , [ b , q ] ] ] âŸ©' = [ f a b , g p q ]
Tm.naturality (from-pred2 f g) type-id refl = refl
Tm.naturality (from-pred2 f g) pred-id refl = refl
Tm.naturality (from-pred2 f g) type-pred refl = refl


--------------------------------------------------
-- Example: types representing booleans

record BoolStructure (B : ClosedType ğŸš) {{_ : IsClosedNatural B}} : Setâ‚ where
  field
    prim-and : Tm (Î“ ,, B âŠ  B) B
    prim-not : Tm (Î“ ,, B) B

  and : Tm Î“ (B âŠ  B â‡› B)
  and = lamÎ¹ (B âŠ  B) prim-and
  
  not : Tm Î“ (B â‡› B)
  not = lamÎ¹ B prim-not

open BoolStructure {{...}}

or : (B : ClosedType ğŸš) {{_ : IsClosedNatural B}} {{_ : BoolStructure B}} â†’ Tm Î“ (B â‡› B â‡› B)
or B = lamÎ¹[ "b1" âˆˆ B ] lamÎ¹[ "b2" âˆˆ B ] not $ (and $ pair (not $ varÎ¹ "b1") (not $ varÎ¹ "b2"))

-- Representing booleans as natural numbers (0 = false, 1 = true)
data IsBit : Pred â„• 0â„“ where
  0-bit : IsBit 0
  1-bit : IsBit 1

PrimBinaryBool : Ty {C = ğŸš} â—‡
PrimBinaryBool = PrimFromPred â„• IsBit

BinaryBool : ClosedType ğŸš
BinaryBool = FromPred â„• IsBit

instance
  binarybool-is-bool : BoolStructure BinaryBool
  prim-and {{binarybool-is-bool}} = from-pred2 _âŠ“_ âŠ“-preserves-bitness
    where
      âŠ“-preserves-bitness : (IsBit âŸ¨â†’âŸ© IsBit âŸ¨â†’âŸ© IsBit) _âŠ“_
      âŠ“-preserves-bitness 0-bit _     = 0-bit
      âŠ“-preserves-bitness 1-bit 0-bit = 0-bit
      âŠ“-preserves-bitness 1-bit 1-bit = 1-bit
  prim-not {{binarybool-is-bool}} = from-pred1 (1 âˆ¸_) 1âˆ¸-preserves-bitness
    where
      1âˆ¸-preserves-bitness : (IsBit âŸ¨â†’âŸ© IsBit) (1 âˆ¸_)
      1âˆ¸-preserves-bitness 0-bit = 1-bit
      1âˆ¸-preserves-bitness 1-bit = 0-bit

âŠ-trans : {A : Set} {x y z w : A} â†’ x â‰¡ y â†’ y â‰¡ z âŠ y â‰¡ w â†’ x â‰¡ z âŠ x â‰¡ w
âŠ-trans e = Data.Sum.map (trans e) (trans e)

module _ (b : Tm â—‡ BinaryBool) where
  translate-b : â„•
  translate-b = b âŸ¨ type-obj , _ âŸ©'

  type-pred-result : (x : PrimBinaryBool âŸ¨ pred-obj , _ âŸ©) â†’
                     PrimBinaryBool âŸª type-pred , refl âŸ« x â‰¡ 0 âŠ PrimBinaryBool âŸª type-pred , refl âŸ« x â‰¡ 1
  type-pred-result [ .0 , 0-bit ] = injâ‚ refl
  type-pred-result [ .1 , 1-bit ] = injâ‚‚ refl

  translated-binary-is-0-or-1 : translate-b â‰¡ 0 âŠ translate-b â‰¡ 1
  translated-binary-is-0-or-1 = âŠ-trans (sym (Tm.naturality b type-pred refl)) (type-pred-result (b âŸ¨ pred-obj , _ âŸ©'))

  translated-binary-is-bit : IsBit translate-b
  translated-binary-is-bit with b âŸ¨ pred-obj , _ âŸ©' | Tm.naturality b type-pred refl
  translated-binary-is-bit | [ _ , p ] | refl = p


--------------------------------------------------
-- Definition of a modality from ğŸš to â˜….

always-false : Ctx â˜… â†’ Ctx ğŸš
always-false Î“ âŸ¨ type-obj âŸ© = Î“ âŸ¨ tt âŸ©
always-false Î“ âŸ¨ pred-obj âŸ© = âŠ¥
always-false Î“ âŸª type-id âŸ« Î³ = Î³
always-false Î“ âŸª pred-id âŸ« x = x
always-false Î“ âŸª type-pred âŸ« x = âŠ¥-elim x
ctx-id (always-false Î“) {x = type-obj} _ = refl
ctx-comp (always-false Î“) type-id g _ = refl
ctx-comp (always-false Î“) pred-id g _ = refl
ctx-comp (always-false Î“) type-pred pred-id _ = refl

always-false-subst : {Î” : Ctx â˜…} {Î“ : Ctx â˜…} â†’ Î” â‡’ Î“ â†’ always-false Î” â‡’ always-false Î“
func (always-false-subst Ïƒ) {x = type-obj} = func Ïƒ
func (always-false-subst Ïƒ) {x = pred-obj} = âŠ¥-elim
_â‡’_.naturality (always-false-subst Ïƒ) {f = type-id} _ = refl

always-false-subst-id : {Î“ : Ctx â˜…} â†’ always-false-subst (id-subst Î“) â‰…Ë¢ id-subst (always-false Î“)
eq always-false-subst-id {x = type-obj} _ = refl

always-false-subst-âŠš : {Î” : Ctx â˜…} {Î“ : Ctx â˜…} {Î˜ : Ctx â˜…} (Ïƒ : Î“ â‡’ Î˜) (Ï„ : Î” â‡’ Î“) â†’
                       always-false-subst (Ïƒ âŠš Ï„) â‰…Ë¢ always-false-subst Ïƒ âŠš always-false-subst Ï„
eq (always-false-subst-âŠš Ïƒ Ï„) {x = type-obj} _ = refl

forget : {Î“ : Ctx â˜…} â†’ Ty (always-false Î“) â†’ Ty Î“
forget T âŸ¨ tt , Î³ âŸ© = T âŸ¨ type-obj , Î³ âŸ©
forget {Î“ = Î“} T âŸª tt , eÎ³ âŸ« t = T âŸª type-id , trans (sym (ctx-id Î“ _ )) eÎ³ âŸ« t
ty-cong (forget T) refl {eÎ³ = refl} {eÎ³' = refl} = refl
ty-id (forget T) t = trans (ty-cong T refl) (ty-id T t)
ty-comp (forget T) _ _ _ _ t = sym (ty-cong-2-1 T refl)

module _ {Î“ : Ctx â˜…} {T : Ty (always-false Î“)} where
  forget-intro : Tm (always-false Î“) T â†’ Tm Î“ (forget T)
  forget-intro t âŸ¨ tt , Î³ âŸ©' = t âŸ¨ type-obj , Î³ âŸ©'
  Tm.naturality (forget-intro t) tt _ = Tm.naturality t type-id _

  forget-elim : Tm Î“ (forget T) â†’ Tm (always-false Î“) T
  forget-elim t âŸ¨ type-obj , Î³ âŸ©' = t âŸ¨ tt , Î³ âŸ©'
  Tm.naturality (forget-elim t) type-id eÎ³ = trans (ty-cong T refl) (Tm.naturality t tt (trans (ctx-id Î“ _) eÎ³))

module _ {Î” : Ctx â˜…} {Î“ : Ctx â˜…} (Ïƒ : Î” â‡’ Î“) {T : Ty (always-false Î“)} where
  forget-natural : (forget T) [ Ïƒ ] â‰…áµ—Ê¸ forget (T [ always-false-subst Ïƒ ])
  func (from forget-natural) = id
  CwF-Structure.naturality (from forget-natural) _ = ty-cong T refl
  func (to forget-natural) = id
  CwF-Structure.naturality (to forget-natural) _ = ty-cong T refl
  eq (isoË¡ forget-natural) _ = refl
  eq (isoÊ³ forget-natural) _ = refl

  forget-intro-natural : (t : Tm (always-false Î“) T) â†’
                         forget-intro t [ Ïƒ ]' â‰…áµ—áµ Î¹[ forget-natural ] forget-intro (t [ always-false-subst Ïƒ ]')
  eq (forget-intro-natural t) _ = refl

  forget-elim-natural : (t : Tm Î“ (forget T)) â†’
                        forget-elim t [ always-false-subst Ïƒ ]' â‰…áµ—áµ forget-elim (Î¹â»Â¹[ forget-natural ] (t [ Ïƒ ]'))
  eq (forget-elim-natural t) {x = type-obj} _ = refl

forget-cong : {Î“ : Ctx â˜…} {T : Ty (always-false Î“)} {T' : Ty (always-false Î“)} â†’
              T â‰…áµ—Ê¸ T' â†’ forget T â‰…áµ—Ê¸ forget T'
func (from (forget-cong T=T')) = func (from T=T')
CwF-Structure.naturality (from (forget-cong T=T')) = CwF-Structure.naturality (from T=T')
func (to (forget-cong T=T')) = func (to T=T')
CwF-Structure.naturality (to (forget-cong T=T')) = CwF-Structure.naturality (to T=T')
eq (isoË¡ (forget-cong T=T')) = eq (isoË¡ T=T')
eq (isoÊ³ (forget-cong T=T')) = eq (isoÊ³ T=T')

module _ {Î“ : Ctx â˜…} {T : Ty (always-false Î“)} where
  forget-intro-cong : {t t' : Tm (always-false Î“) T} â†’ t â‰…áµ—áµ t' â†’ forget-intro t â‰…áµ—áµ forget-intro t'
  eq (forget-intro-cong t=t') Î³ = eq t=t' Î³

  forget-elim-cong : {t t' : Tm Î“ (forget T)} â†’ t â‰…áµ—áµ t' â†’ forget-elim t â‰…áµ—áµ forget-elim t'
  eq (forget-elim-cong t=t') {x = type-obj} Î³ = eq t=t' Î³

  forget-Î² : (t : Tm (always-false Î“) T) â†’ forget-elim (forget-intro t) â‰…áµ—áµ t
  eq (forget-Î² t) {x = type-obj} _ = refl

  forget-Î· : (t : Tm Î“ (forget T)) â†’ forget-intro (forget-elim t) â‰…áµ—áµ t
  eq (forget-Î· t) _ = refl

instance
  always-false-functor : IsCtxFunctor always-false
  ctx-map {{always-false-functor}} = always-false-subst
  ctx-map-id {{always-false-functor}} = always-false-subst-id
  ctx-map-âŠš {{always-false-functor}} = always-false-subst-âŠš

  forget-unarynat : IsUnaryNatural forget
  natural-un {{forget-unarynat}} = forget-natural
  cong-un {{forget-unarynat}} = forget-cong

forget-mod : Modality ğŸš â˜…
forget-mod = record
   { ctx-op = always-false
   ; mod = forget
   ; mod-cong = forget-cong
   ; mod-natural = forget-natural
   ; mod-intro = forget-intro
   ; mod-intro-cong = forget-intro-cong
   ; mod-intro-natural = forget-intro-natural
   ; mod-elim = forget-elim
   ; mod-elim-cong = forget-elim-cong
   ; mod-elim-natural = forget-elim-natural
   ; mod-Î² = forget-Î²
   ; mod-Î· = forget-Î·
   }


--------------------------------------------------
-- Continuing the example of binary numbers representing booleans

binary-or : Tm Î“ (BinaryBool â‡› BinaryBool â‡› BinaryBool)
binary-or = or BinaryBool

binary-orâ˜… : {Î“ : Ctx â˜…} â†’ Tm Î“ (forget BinaryBool â‡› forget BinaryBool â‡› forget BinaryBool)
binary-orâ˜… = lamÎ¹[ "x" âˆˆ forget BinaryBool ] lamÎ¹[ "y" âˆˆ forget BinaryBool ]
             forget-intro binary-or âŠ›âŸ¨ forget-mod âŸ© varÎ¹ "x" âŠ›âŸ¨ forget-mod âŸ© varÎ¹ "y"

instance
  forget-pred : {A : Set} {P : Pred A 0â„“} â†’ Translatable (forget (FromPred A P))
  Translatable.translated-type (forget-pred {A = A}) = A
  Translatable.translate-term forget-pred t = t âŸ¨ tt , tt âŸ©'
  Translatable.translate-back forget-pred a = MkTm (Î» _ _ â†’ a) (Î» _ _ â†’ refl)

binary-or-agda : â„• â†’ â„• â†’ â„•
binary-or-agda = translate-term binary-orâ˜…

translate-result : (IsBit âŸ¨â†’âŸ© IsBit âŸ¨â†’âŸ© IsBit) binary-or-agda
translate-result {m} x {n} y = projâ‚‚ ((binary-or {Î“ = â—‡} â‚¬âŸ¨ pred-obj , tt âŸ© [ m , x ]) $âŸ¨ pred-id , refl âŸ© [ n , y ])
