--------------------------------------------------
-- Many example programs dealing with streams (guarded and standard)
--------------------------------------------------

module Applications.GuardedRecursion.StreamsExamples where

open import Data.Bool
open import Data.Nat
open import Data.Unit
open import Data.Vec hiding (take; head; tail)
open import Relation.Binary.PropositionalEquality

open import Model.CwF-Structure renaming (â—‡ to â€²â—‡)
open import Model.Modality renaming (âŸ¨_âˆ£_âŸ© to â€²âŸ¨_âˆ£_âŸ©) using ()
open import Model.Type.Discrete renaming (Nat' to â€²Nat'; Bool' to â€²Bool')
open import Model.Type.Function hiding (lam; lam[_âˆˆ_]_) renaming (_â‡›_ to _â€²â‡›_)
open import Model.Type.Product hiding (pair; fst; snd) renaming (_âŠ _ to _â€²âŠ _)
open import Applications.GuardedRecursion.Model.Modalities
  hiding (next; lÃ¶b; liftâ–»; lift2â–»; ğŸ™â‰¤later) renaming (â–» to â€²â–»; constantly to â€²constantly; forever to â€²forever; later to â€²later)
open import Applications.GuardedRecursion.Model.Streams.Guarded hiding (g-cons; g-head; g-tail) renaming (GStream to â€²GStream)
open import Applications.GuardedRecursion.Model.Streams.Standard renaming (Stream' to â€²Stream')
open import Extraction

open import Applications.GuardedRecursion.MSTT


--------------------------------------------------
--------------------------------------------------
-- Abbreviations for frequently used combinations

g-consN = g-cons Nat'
g-headN = g-head Nat'
g-tailN = g-tail Nat'

g-consB = g-cons Bool'
g-headB = g-head Bool'
g-tailB = g-tail Bool'


--------------------------------------------------
--------------------------------------------------
-- Definition of some helper functions that are used a lot
--   in the example programs with streams.

-- If Î“ âŠ¢ f : âŸ¨ Î¼ âˆ£ A â‡› B âŸ© and Î“ âŠ¢ t : âŸ¨ Î¼ âˆ£ A âŸ©, then Î“ âŠ¢ f âŠ›âŸ¨ Î¼ âŸ© t : âŸ¨ Î¼ âˆ£ B âŸ©.
infixl 5 _âŠ›âŸ¨_âŸ©_
_âŠ›âŸ¨_âŸ©_ : âˆ€ {m m'} â†’ TmExpr m â†’ ModalityExpr m' m â†’ TmExpr m â†’ TmExpr m
f âŠ›âŸ¨ Î¼ âŸ© t = mod-intro Î¼ (mod-elim Î¼ f âˆ™ mod-elim Î¼ t)

-- If Î“ ,lockâŸ¨ Î¼ âŸ© âŠ¢ f : A â‡› B and Î“ âŠ¢ t : âŸ¨ Î¼ âˆ£ A âŸ©, then Î“ âŠ¢ f âŸ¨$- Î¼ âŸ© t : âŸ¨ Î¼ âˆ£ B âŸ©.
infixl 5 _âŸ¨$-_âŸ©_
_âŸ¨$-_âŸ©_ : âˆ€ {m m'} â†’ TmExpr m' â†’ ModalityExpr m' m â†’ TmExpr m â†’ TmExpr m
f âŸ¨$- Î¼ âŸ© t = mod-intro Î¼ (f âˆ™ mod-elim Î¼ t)

-- If Î“ âŠ¢ t : T, then Î“ âŠ¢ next t : â–» T.
-- Note that this is different from (mod-intro later t), where t would be type-checked
--   in context Î“ ,lockâŸ¨ later âŸ©.
next : TmExpr Ï‰ â†’ TmExpr Ï‰
next t = coe[ ğŸ™â‰¤later âˆˆ ğŸ™ â‡’ later ] mod-intro ğŸ™ t

-- If Î“ âŠ¢ f : A â‡› B and Î“ âŠ¢ t : â–» A, then Î“ âŠ¢ f âŸ¨$-laterâŸ©' t : â–» B.
-- The difference with f âŸ¨$- later âŸ© t is that f is type-checked in Î“ and not Î“ ,lockâŸ¨ later âŸ©.
infixl 5 _âŸ¨$-laterâŸ©'_
_âŸ¨$-laterâŸ©'_ : TmExpr Ï‰ â†’ TmExpr Ï‰ â†’ TmExpr Ï‰
f âŸ¨$-laterâŸ©' t = next f âŠ›âŸ¨ later âŸ© t

-- Î“ âŠ¢ liftâ–» T S : (T â‡› S) â‡› â–» T â‡› â–» S.
liftâ–» : TyExpr Ï‰ â†’ TyExpr Ï‰ â†’ TmExpr Ï‰
liftâ–» T S = lam[ "f" âˆˆ T â‡› S ] lam[ "t" âˆˆ â–» T ] (var "f" âŸ¨$-laterâŸ©' var "t")

-- Î“ âŠ¢ lift2â–» T S R : (T â‡› S â‡› R) â‡› â–» T â‡› â–» S â‡› â–» R.
lift2â–» : TyExpr Ï‰ â†’ TyExpr Ï‰ â†’ TyExpr Ï‰ â†’ TmExpr Ï‰
lift2â–» T S R =
  lam[ "f" âˆˆ T â‡› S â‡› R ] lam[ "t" âˆˆ â–» T ] lam[ "s" âˆˆ â–» S ] (var "f" âŸ¨$-laterâŸ©' var "t" âŠ›âŸ¨ later âŸ© var "s")


--------------------------------------------------
--------------------------------------------------
-- Examples involving guarded streams

--------------------------------------------------
-- The following is the example that is worked out in Section 3 of the CPP submission.

-- Î“ âŠ¢ g-map A B : âŸ¨ constantly âˆ£ A â‡› B âŸ© â‡› GStream A â‡› GStream B
g-map : TyExpr â˜… â†’ TyExpr â˜… â†’ TmExpr Ï‰
g-map A B =
  lam[ "f" âˆˆ âŸ¨ constantly âˆ£ A â‡› B âŸ© ]
    lÃ¶b[ "m" âˆˆâ–» (GStream A â‡› GStream B) ]
      lam[ "s" âˆˆ GStream A ]
        g-cons B âˆ™ (var "f" âŠ›âŸ¨ constantly âŸ© g-head A âˆ™ var "s")
                 âˆ™ (var "m" âŠ›âŸ¨ later âŸ© g-tail A âˆ™ var "s")

g-map-sem : Tm â€²â—‡ (â€²âŸ¨ â€²constantly âˆ£ â€²Nat' â€²â‡› â€²Nat' âŸ© â€²â‡› â€²GStream â€²Nat' â€²â‡› â€²GStream â€²Nat')
g-map-sem = âŸ¦ g-map Nat' Nat' âŸ§tm

-- Î“ âŠ¢ g-nats : GStream Nat'
g-nats : TmExpr Ï‰
g-nats =
  lÃ¶b[ "s" âˆˆâ–» (GStream Nat') ]
    g-consN âˆ™ mod-intro constantly (lit 0)
            âˆ™ (g-map Nat' Nat' âˆ™ mod-intro constantly suc âŸ¨$-laterâŸ©' var "s")

g-nats-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-nats-sem = âŸ¦ g-nats âŸ§tm


--------------------------------------------------
-- The follwing definitions are an implementation of all examples involving streams on pages 8-10 of the paper
--   Ranald Clouston, AleÅ¡ Bizjak, Hans Bugge Grathwohl, and Lars Birkedal.
--   The Guarded Lambda-Calculus: Programming and Reasoning with Guarded Recursion for Coinductive Types.
--   Logical Methods of Computer Science (LMCS), 12(3), 2016.
--   https://doi.org/10.2168/LMCS-12(3:7)2016

-- Î“ âŠ¢ g-snd A : GStream A â‡› â–» âŸ¨ constantly âˆ£ A âŸ©
g-snd : TyExpr â˜… â†’ TmExpr Ï‰
g-snd A = lam[ "s" âˆˆ GStream A ] g-head A âŸ¨$-laterâŸ©' g-tail A âˆ™ var "s"

g-snd-sem : Tm â€²â—‡ (â€²GStream â€²Nat' â€²â‡› â€²â–» â€²âŸ¨ â€²constantly âˆ£ â€²Nat' âŸ©)
g-snd-sem = âŸ¦ g-snd Nat' âŸ§tm

-- Î“ âŠ¢ g-thrd A : GStream A â‡› â–» (â–» âŸ¨ constantly âˆ£ A âŸ©)
g-thrd : TyExpr â˜… â†’ TmExpr Ï‰
g-thrd A = lam[ "s" âˆˆ GStream A ] g-snd A âŸ¨$-laterâŸ©' g-tail A âˆ™ var "s"

g-thrd-sem : Tm â€²â—‡ (â€²GStream â€²Bool' â€²â‡› â€²â–» (â€²â–» â€²âŸ¨ â€²constantly âˆ£ â€²Bool' âŸ©))
g-thrd-sem = âŸ¦ g-thrd Bool' âŸ§tm

-- Î“ âŠ¢ g-zeros : GStream Nat'
g-zeros : TmExpr Ï‰
g-zeros = lÃ¶b[ "s" âˆˆâ–» (GStream Nat') ] g-consN âˆ™ mod-intro constantly (lit 0) âˆ™ var "s"

g-zeros-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-zeros-sem = âŸ¦ g-zeros âŸ§tm

-- Î“ âŠ¢ g-iterate' A : âŸ¨ constantly | A â‡› A âŸ© â‡› âŸ¨ constantly âˆ£ A âŸ© â‡› GStream A
g-iterate' : TyExpr â˜… â†’ TmExpr Ï‰
g-iterate' A =
  lam[ "f" âˆˆ âŸ¨ constantly âˆ£ A â‡› A âŸ© ]
    lÃ¶b[ "g" âˆˆâ–» (âŸ¨ constantly âˆ£ A âŸ© â‡› GStream A) ]
      lam[ "x" âˆˆ âŸ¨ constantly âˆ£ A âŸ© ]
        g-cons A âˆ™ var "x"
                 âˆ™ (var "g" âŠ›âŸ¨ later âŸ© (next (var "f" âŠ›âŸ¨ constantly âŸ© var "x")))

g-iterate'-sem : Tm â€²â—‡ (â€²âŸ¨ â€²constantly âˆ£ â€²Nat' â€²â‡› â€²Nat' âŸ© â€²â‡› â€²âŸ¨ â€²constantly âˆ£ â€²Nat' âŸ© â€²â‡› â€²GStream â€²Nat')
g-iterate'-sem = âŸ¦ g-iterate' Nat' âŸ§tm

-- This is a more general definition of iterate since the generating function of type
-- âŸ¨ constantly âˆ£ A â‡› A âŸ© appears under â–». The implementation itself applies g-map to
-- its corecursive call (represented by the variable "s"), which would not be allowed
-- in a definition of standard Agda streams by copattern matching.
-- Î“ âŠ¢ g-iterate A : âŸ¨ constantly | A â‡› A âŸ© â‡› âŸ¨ constantly âˆ£ A âŸ© â‡› GStream A
g-iterate : TyExpr â˜… â†’ TmExpr Ï‰
g-iterate A =
  lam[ "f" âˆˆ â–» âŸ¨ constantly âˆ£ A â‡› A âŸ© ]
    lam[ "a" âˆˆ âŸ¨ constantly âˆ£ A âŸ© ]
      lÃ¶b[ "s" âˆˆâ–» (GStream A) ]
        g-cons A âˆ™ var "a"
                 âˆ™ (g-map A A âŸ¨$-laterâŸ©' var "f" âŠ›âŸ¨ later âŸ© var "s")

g-iterate-sem : Tm â€²â—‡ (â€²â–» â€²âŸ¨ â€²constantly âˆ£ â€²Bool' â€²â‡› â€²Bool' âŸ© â€²â‡› â€²âŸ¨ â€²constantly âˆ£ â€²Bool' âŸ© â€²â‡› â€²GStream â€²Bool')
g-iterate-sem = âŸ¦ g-iterate Bool' âŸ§tm

-- Î“ âŠ¢ g-nats' : GStream Nat'
g-nats' : TmExpr Ï‰
g-nats' = g-iterate Nat' âˆ™ next (mod-intro constantly suc) âˆ™ mod-intro constantly (lit 0)

g-nats'-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-nats'-sem = âŸ¦ g-nats' âŸ§tm

-- Î“ âŠ¢ g-interleave A : GStream A â‡› â–» (GStream A) â‡› GStream A
g-interleave : TyExpr â˜… â†’ TmExpr Ï‰
g-interleave A =
  lÃ¶b[ "g" âˆˆâ–» (GStream A â‡› â–» (GStream A) â‡› GStream A) ]
    lam[ "s" âˆˆ GStream A ]
      lam[ "t" âˆˆ â–» (GStream A) ]
        g-cons A âˆ™ (g-head A âˆ™ var "s")
                 âˆ™ (var "g" âŠ›âŸ¨ later âŸ© var "t" âŠ›âŸ¨ later âŸ© next (g-tail A âˆ™ var "s"))

g-interleave-sem : Tm â€²â—‡ (â€²GStream â€²Nat' â€²â‡› â€²â–» (â€²GStream â€²Nat') â€²â‡› â€²GStream â€²Nat')
g-interleave-sem = âŸ¦ g-interleave Nat' âŸ§tm

-- Î“ âŠ¢ g-toggle : GStream Nat'
g-toggle : TmExpr Ï‰
g-toggle = lÃ¶b[ "s" âˆˆâ–» (GStream Nat') ]
             g-consN âˆ™ (mod-intro constantly (lit 1))
                     âˆ™ (next (g-consN âˆ™ mod-intro constantly (lit 0) âˆ™ var "s"))

g-toggle-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-toggle-sem = âŸ¦ g-toggle âŸ§tm

-- Î“ âŠ¢ g-paperfolds : GStream Nat'
g-paperfolds : TmExpr Ï‰
g-paperfolds = lÃ¶b[ "s" âˆˆâ–» (GStream Nat') ] g-interleave Nat' âˆ™ g-toggle âˆ™ var "s"

g-paperfolds-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-paperfolds-sem = âŸ¦ g-paperfolds âŸ§tm

-- Î“ âŠ¢ g-initial : ((âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T)) â‡› T) â‡› GStream A â‡› T
g-initial : TyExpr â˜… â†’ TyExpr Ï‰ â†’ TmExpr Ï‰
g-initial A T =
  lÃ¶b[ "g" âˆˆâ–» (((âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T)) â‡› T) â‡› GStream A â‡› T) ]
    lam[ "f" âˆˆ (âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T)) â‡› T ]
      lam[ "s" âˆˆ GStream A ]
        var "f" âˆ™ (pair (g-head A âˆ™ (var "s"))
                        (var "g" âŠ›âŸ¨ later âŸ© next (var "f") âŠ›âŸ¨ later âŸ© g-tail A âˆ™ var "s"))

g-initial-sem : Tm â€²â—‡ (((â€²âŸ¨ â€²constantly âˆ£ â€²Nat' âŸ© â€²âŠ  â€²â–» â€²Bool') â€²â‡› â€²Bool') â€²â‡› â€²GStream â€²Nat' â€²â‡› â€²Bool')
g-initial-sem = âŸ¦ g-initial Nat' Bool' âŸ§tm

-- Î“ âŠ¢ g-final : (T â‡› (âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T))) â‡› T â‡› GStream A
g-final : TyExpr â˜… â†’ TyExpr Ï‰ â†’ TmExpr Ï‰
g-final A T =
  lÃ¶b[ "g" âˆˆâ–» ((T â‡› (âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T))) â‡› T â‡› GStream A) ]
    lam[ "f" âˆˆ T â‡› (âŸ¨ constantly âˆ£ A âŸ© âŠ  (â–» T)) ]
      lam[ "x" âˆˆ T ]
        g-cons A âˆ™ (fst (var "f" âˆ™ var "x"))
                 âˆ™ (var "g" âŠ›âŸ¨ later âŸ© next (var "f") âŠ›âŸ¨ later âŸ© snd (var "f" âˆ™ var "x"))

g-final-sem : Tm â€²â—‡ ((â€²Bool' â€²â‡› (â€²âŸ¨ â€²constantly âˆ£ â€²Nat' âŸ© â€²âŠ  â€²â–» â€²Bool')) â€²â‡› â€²Bool' â€²â‡› â€²GStream â€²Nat')
g-final-sem = âŸ¦ g-final Nat' Bool' âŸ§tm


--------------------------------------------------
-- Implementation of the examples on page 12 of the paper by Clouston et al. cited above.

-- Î“ âŠ¢ g-thumorse : GStream Bool'
g-thumorse : TmExpr Ï‰
g-thumorse =
  let liftSBâ–» = liftâ–» (GStream Bool') (GStream Bool')
      liftLSBâ–» = liftâ–» (â–» (GStream Bool')) (â–» (GStream Bool'))
  in
  lÃ¶b[ "t-m" âˆˆâ–» (GStream Bool') ]
    g-consB âˆ™ mod-intro constantly false
            âˆ™ (g-consB âˆ™ (mod-intro constantly true)
                       âŸ¨$-laterâŸ©' (liftLSBâ–» âˆ™ (liftSBâ–» âˆ™ h)) âˆ™
                            (g-tailB âŸ¨$-laterâŸ©' liftSBâ–» âˆ™ h âˆ™ var "t-m"))
  where
    h : TmExpr Ï‰
    h =
      lÃ¶b[ "g" âˆˆâ–» (GStream Bool' â‡› GStream Bool') ]
        lam[ "s" âˆˆ GStream Bool' ]
          constantly-if (g-headB âˆ™ var "s")
                        (g-consB âˆ™ mod-intro constantly true
                                 âˆ™ (next (g-consB âˆ™ mod-intro constantly false âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tailB âˆ™ var "s"))))
                        (g-consB âˆ™ mod-intro constantly false
                                 âˆ™ (next (g-consB âˆ™ mod-intro constantly true  âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tailB âˆ™ var "s"))))

g-thumorse-sem : Tm â€²â—‡ (â€²GStream â€²Bool')
g-thumorse-sem = âŸ¦ g-thumorse âŸ§tm

-- Î“ âŠ¢ g-fibonacci-word : GStream Bool'
g-fibonacci-word : TmExpr Ï‰
g-fibonacci-word =
  let liftSBâ–» = liftâ–» (GStream Bool') (GStream Bool')
      liftLSBâ–» = liftâ–» (â–» (GStream Bool')) (â–» (GStream Bool'))
  in
  lÃ¶b[ "fw" âˆˆâ–» (GStream Bool') ]
    g-consB âˆ™ mod-intro constantly false
            âˆ™ (g-consB âˆ™ mod-intro constantly true
                       âŸ¨$-laterâŸ©' (liftLSBâ–» âˆ™ (liftSBâ–» âˆ™ f)) âˆ™
                            (g-tailB âŸ¨$-laterâŸ©' liftSBâ–» âˆ™ f âˆ™ var "fw"))
  where
    f : TmExpr Ï‰
    f =
      lÃ¶b[ "g" âˆˆâ–» (GStream Bool' â‡› GStream Bool') ]
        lam[ "s" âˆˆ GStream Bool' ]
          constantly-if (g-headB âˆ™ var "s")
                        (g-consB âˆ™ mod-intro constantly false âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tailB âˆ™ var "s"))
                        (g-consB âˆ™ mod-intro constantly false âˆ™ next (
                                 g-consB âˆ™ mod-intro constantly true âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tailB âˆ™ var "s")))

g-fibonacci-word-sem : Tm â€²â—‡ (â€²GStream â€²Bool')
g-fibonacci-word-sem = âŸ¦ g-fibonacci-word âŸ§tm


--------------------------------------------------
-- This is an implementation of an example from the end of section 1.1 of the paper
--   Robert Atkey, and Conor McBride.
--   Productive Coprogramming with Guarded Recursion.
--   ICFP 2013.
--   https://doi.org/10.1145/2544174.2500597

-- Î“ âŠ¢ g-mergef A B C : (âŸ¨ constantly âˆ£ A âŸ© â‡› âŸ¨ constantly âˆ£ B âŸ© â‡› â–» (GStream C) â‡› GStream C) â‡› GStream A â‡› GStream B â‡› GStream C
g-mergef : (A B C : TyExpr â˜…) â†’ TmExpr Ï‰
g-mergef A B C =
  lam[ "f" âˆˆ âŸ¨ constantly âˆ£ A âŸ© â‡› âŸ¨ constantly âˆ£ B âŸ© â‡› â–» (GStream C) â‡› GStream C ]
    lÃ¶b[ "g" âˆˆâ–» (GStream A â‡› GStream B â‡› GStream C) ]
      lam[ "xs" âˆˆ GStream A ]
        lam[ "ys" âˆˆ GStream B ]
          var "f" âˆ™ (g-head A âˆ™ var "xs")
                  âˆ™ (g-head B âˆ™ var "ys")
                  âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tail A âˆ™ var "xs" âŠ›âŸ¨ later âŸ© g-tail B âˆ™ var "ys")

g-mergef-sem : Tm â€²â—‡ ((â€²âŸ¨ â€²constantly âˆ£ â€²Nat' âŸ© â€²â‡› â€²âŸ¨ â€²constantly âˆ£ â€²Bool' âŸ© â€²â‡› â€²â–» (â€²GStream â€²Nat') â€²â‡› â€²GStream â€²Nat') â€²â‡› â€²GStream â€²Nat' â€²â‡› â€²GStream â€²Bool' â€²â‡› â€²GStream â€²Nat')
g-mergef-sem = âŸ¦ g-mergef Nat' Bool' Nat' âŸ§tm


--------------------------------------------------
-- Examples that are not taken from a paper

-- Î“ âŠ¢ g-zipWith A B C : âŸ¨ constantly âˆ£ A â‡› B â‡› C âŸ© â‡› GStream A â‡› GStream B â‡› GStream C
g-zipWith : (A B C : TyExpr â˜…) â†’ TmExpr Ï‰
g-zipWith A B C =
  lam[ "f" âˆˆ âŸ¨ constantly âˆ£ A â‡› B â‡› C âŸ© ]
    lÃ¶b[ "g" âˆˆâ–» (GStream A â‡› GStream B â‡› GStream C) ]
      lam[ "as" âˆˆ GStream A ]
        lam[ "bs" âˆˆ GStream B ]
          g-cons C âˆ™ (var "f" âŠ›âŸ¨ constantly âŸ© g-head A âˆ™ var "as" âŠ›âŸ¨ constantly âŸ© g-head B âˆ™ var "bs")
                   âˆ™ (var "g" âŠ›âŸ¨ later âŸ© g-tail A âˆ™ var "as" âŠ›âŸ¨ later âŸ© g-tail B âˆ™ var "bs")

g-zipWith-sem : Tm â€²â—‡ (â€²âŸ¨ â€²constantly âˆ£ â€²Bool' â€²â‡› â€²Nat' â€²â‡› â€²Bool' âŸ© â€²â‡› â€²GStream â€²Bool' â€²â‡› â€²GStream â€²Nat' â€²â‡› â€²GStream â€²Bool')
g-zipWith-sem = âŸ¦ g-zipWith Bool' Nat' Bool' âŸ§tm

-- Î“ âŠ¢ g-fibs : GStream Nat'
g-fibs : TmExpr Ï‰
g-fibs =
  let lift2SNâ–» = lift2â–» (GStream Nat') (GStream Nat') (GStream Nat')
  in
  lÃ¶b[ "s" âˆˆâ–» (GStream Nat') ]
    g-consN âˆ™ mod-intro constantly (lit 1)
            âˆ™ (g-consN âˆ™ mod-intro constantly (lit 1)
                       âŸ¨$-laterâŸ©' (lift2SNâ–» âˆ™ (g-zipWith Nat' Nat' Nat' âˆ™ mod-intro constantly plus)
                                            âˆ™ var "s"
                                            âŸ¨$-laterâŸ©' (g-tailN âŸ¨$-laterâŸ©' var "s")))

g-fibs-sem : Tm â€²â—‡ (â€²GStream â€²Nat')
g-fibs-sem = âŸ¦ g-fibs âŸ§tm

-- Î“ âŠ¢ g-flipFst A : GStream A â‡› â–» (GStream A)
g-flipFst : TyExpr â˜… â†’ TmExpr Ï‰
g-flipFst A =
  lam[ "s" âˆˆ GStream A ]
    g-cons A âŸ¨$-laterâŸ©' g-snd A âˆ™ var "s" âŠ›âŸ¨ later âŸ© next (
    g-cons A âŸ¨$-laterâŸ©' next (g-head A âˆ™ var "s") âŠ›âŸ¨ later âŸ© (g-tail A âŸ¨$-laterâŸ©' g-tail A âˆ™ var "s"))

g-flipFst-sem : Tm â€²â—‡ (â€²GStream â€²Bool' â€²â‡› â€²â–» (â€²GStream â€²Bool'))
g-flipFst-sem = âŸ¦ g-flipFst Bool' âŸ§tm


--------------------------------------------------
--------------------------------------------------
-- Examples involving standard streams and the extraction
--   to Agda streams

Stream' : TyExpr â˜… â†’ TyExpr â˜…
Stream' A = âŸ¨ forever âˆ£ GStream A âŸ©


--------------------------------------------------
-- Continuation of the example worked out in Sections 3, 5 and 6 of the CPP submission

-- Î“ âŠ¢ nats : Stream' Nat'
nats : TmExpr â˜…
nats = mod-intro forever g-nats

nats-sem : Tm â€²â—‡ (â€²Stream' â€²Nat')
nats-sem = âŸ¦ nats âŸ§tm

nats-agda : Stream â„•
nats-agda = extract-term nats-sem

nats-test : take 10 nats-agda â‰¡ 0 âˆ· 1 âˆ· 2 âˆ· 3 âˆ· 4 âˆ· 5 âˆ· 6 âˆ· 7 âˆ· 8 âˆ· 9 âˆ· []
nats-test = refl


--------------------------------------------------
-- The following are implementations of all examples involving streams on page 11 of the paper
--   by Clouston et al. cited above.

-- Î“ âŠ¢ paperfolds : Stream' Nat'
paperfolds : TmExpr â˜…
paperfolds = mod-intro forever g-paperfolds

paperfolds-sem : Tm â€²â—‡ (â€²Stream' â€²Nat')
paperfolds-sem = âŸ¦ paperfolds âŸ§tm

paperfolds-agda : Stream â„•
paperfolds-agda = extract-term paperfolds-sem

paperfolds-test : take 10 paperfolds-agda â‰¡ 1 âˆ· 1 âˆ· 0 âˆ· 1 âˆ· 1 âˆ· 0 âˆ· 0 âˆ· 1 âˆ· 1 âˆ· 1 âˆ· []
paperfolds-test = refl

-- Î“ âŠ¢ thue-morse : Stream' Bool'
thue-morse : TmExpr â˜…
thue-morse = mod-intro forever g-thumorse

thue-morse-sem : Tm â€²â—‡ (â€²Stream' â€²Bool')
thue-morse-sem = âŸ¦ thue-morse âŸ§tm

thue-morse-agda : Stream Bool
thue-morse-agda = extract-term thue-morse-sem

thue-morse-test : take 10 thue-morse-agda â‰¡ false âˆ· true âˆ· true âˆ· false âˆ· true âˆ· false âˆ· false âˆ· true âˆ· true âˆ· false âˆ· []
thue-morse-test = refl

-- Î“ âŠ¢ fibonacci-word : Stream' Bool'
fibonacci-word : TmExpr â˜…
fibonacci-word = mod-intro forever g-fibonacci-word

fibonacci-word-sem : Tm â€²â—‡ (â€²Stream' â€²Bool')
fibonacci-word-sem = âŸ¦ fibonacci-word âŸ§tm

fibonacci-word-agda : Stream Bool
fibonacci-word-agda = extract-term fibonacci-word-sem

fibonacci-word-test : take 10 fibonacci-word-agda â‰¡ false âˆ· true âˆ· false âˆ· false âˆ· true âˆ· false âˆ· true âˆ· false âˆ· false âˆ· true âˆ· []
fibonacci-word-test = refl

-- Î“ âŠ¢ head' A : Stream' A â‡› A
head' : TyExpr â˜… â†’ TmExpr â˜…
head' A = ann
  lam[ "s" âˆˆ Stream' A ] g-head A âŸ¨$- forever âŸ© var "s"
  âˆˆ (Stream' A â‡› A)

head-nats : TmExpr â˜…
head-nats = head' Nat' âˆ™ nats

head-nats-agda : â„•
head-nats-agda = extract-term (âŸ¦ head-nats âŸ§tm)

head-nats-test : head-nats-agda â‰¡ 0
head-nats-test = refl

-- Î“ âŠ¢ tail' A : Stream' A â‡› Stream' A
tail' : TyExpr â˜… â†’ TmExpr â˜…
tail' A = ann
  lam[ "s" âˆˆ Stream' A ] g-tail A âŸ¨$- forever âŸ© var "s"
  âˆˆ (Stream' A â‡› Stream' A)

tailN-sem : Tm â€²â—‡ (â€²Stream' â€²Nat' â€²â‡› â€²Stream' â€²Nat')
tailN-sem = âŸ¦ tail' Nat' âŸ§tm

-- Î“ âŠ¢ cons' A : A â‡› Stream' A â‡› Stream' A
cons' : TyExpr â˜… â†’ TmExpr â˜…
cons' A = ann
  lam[ "a" âˆˆ A ] lam[ "as" âˆˆ Stream' A ]
    g-cons A âŸ¨$- forever âŸ© (ann (var "a") âˆˆ âŸ¨ forever âˆ£ âŸ¨ constantly âˆ£ A âŸ© âŸ©)
             âŠ›âŸ¨ forever âŸ© (ann (var "as") âˆˆ âŸ¨ forever âˆ£ âŸ¨ later âˆ£ GStream A âŸ© âŸ©)
  âˆˆ (A â‡› Stream' A â‡› Stream' A)

consB-sem : Tm â€²â—‡ (â€²Bool' â€²â‡› â€²Stream' â€²Bool' â€²â‡› â€²Stream' â€²Bool')
consB-sem = âŸ¦ cons' Bool' âŸ§tm

-- Î“ âŠ¢ map' A B : (A â‡› B) â‡› Stream' A â‡› Stream' B
map' : TyExpr â˜… â†’ TyExpr â˜… â†’ TmExpr â˜…
map' A B =
  lam[ "f" âˆˆ A â‡› B ]
    lam[ "s" âˆˆ Stream' A ]
      g-map A B âŸ¨$- forever âŸ© ann (var "f") âˆˆ âŸ¨ forever âˆ£ âŸ¨ constantly âˆ£ A â‡› B âŸ© âŸ©
                âŠ›âŸ¨ forever âŸ© var "s"

map'-sem : Tm â€²â—‡ ((â€²Nat' â€²â‡› â€²Nat') â€²â‡› â€²Stream' â€²Nat' â€²â‡› â€²Stream' â€²Nat')
map'-sem = âŸ¦ map' Nat' Nat' âŸ§tm

-- Î“ âŠ¢ g-every2nd A : âŸ¨ constantly âˆ£ Stream' A âŸ© â‡› GStream A
g-every2nd : TyExpr â˜… â†’ TmExpr Ï‰
g-every2nd A =
  lÃ¶b[ "g" âˆˆâ–» (âŸ¨ constantly âˆ£ Stream' A âŸ© â‡› GStream A) ]
    lam[ "s" âˆˆ âŸ¨ constantly âˆ£ Stream' A âŸ© ]
      g-cons A âˆ™ (head' A âŸ¨$- constantly âŸ© var "s")
               âˆ™ (var "g" âŠ›âŸ¨ later âŸ© next (tail' A âŸ¨$- constantly âŸ© (tail' A âŸ¨$- constantly âŸ© var "s")))

g-every2ndB-sem : Tm â€²â—‡ (â€²âŸ¨ â€²constantly âˆ£ â€²Stream' â€²Bool' âŸ© â€²â‡› â€²GStream â€²Bool')
g-every2ndB-sem = âŸ¦ g-every2nd Bool' âŸ§tm

-- Î“ âŠ¢ every2nd A : Stream' A â‡› Stream' A
every2nd : TyExpr â˜… â†’ TmExpr â˜…
every2nd A =
  lam[ "s" âˆˆ Stream' A ]
    g-every2nd A âŸ¨$- forever âŸ© ann (var "s") âˆˆ âŸ¨ forever âˆ£ âŸ¨ constantly âˆ£ Stream' A âŸ© âŸ©

every2ndN-sem : Tm â€²â—‡ (â€²Stream' â€²Nat' â€²â‡› â€²Stream' â€²Nat')
every2ndN-sem = âŸ¦ every2nd Nat' âŸ§tm

every2nd-test : take 6 (extract-term (every2ndN-sem $ nats-sem))
                â‰¡ 0 âˆ· 2 âˆ· 4 âˆ· 6 âˆ· 8 âˆ· 10 âˆ· []
every2nd-test = refl

-- Î“ âŠ¢ g-diag : âŸ¨ constantly âˆ£ Stream' (Stream' A) âŸ© â‡› GStream A
g-diag : TyExpr â˜… â†’ TmExpr Ï‰
g-diag A =
  lÃ¶b[ "g" âˆˆâ–» (âŸ¨ constantly âˆ£ Stream' (Stream' A) âŸ© â‡› GStream A) ]
    lam[ "xss" âˆˆ âŸ¨ constantly âˆ£ Stream' (Stream' A) âŸ© ]
      g-cons A âˆ™ (head' A âŸ¨$- constantly âŸ© (head' (Stream' A) âŸ¨$- constantly âŸ© var "xss"))
               âˆ™ (var "g" âŠ›âŸ¨ later âŸ© next (map' (Stream' A) (Stream' A) âˆ™ tail' A
                                                âŸ¨$- constantly âŸ© (tail' (Stream' A) âŸ¨$- constantly âŸ© var "xss")))

g-diagB-sem : Tm â€²â—‡ (â€²âŸ¨ â€²constantly âˆ£ â€²Stream' (â€²Stream' â€²Bool') âŸ© â€²â‡› â€²GStream â€²Bool')
g-diagB-sem = âŸ¦ g-diag Bool' âŸ§tm

-- Î“ âŠ¢ diag : Stream' (Stream' A) â‡› Stream' A
diag : TyExpr â˜… â†’ TmExpr â˜…
diag A =
  lam[ "s" âˆˆ Stream' (Stream' A) ]
    g-diag A âŸ¨$- forever âŸ© ann (var "s") âˆˆ âŸ¨ forever âˆ£ âŸ¨ constantly âˆ£ Stream' (Stream' A) âŸ© âŸ©

diagB-sem : Tm â€²â—‡ (â€²Stream' (â€²Stream' â€²Bool') â€²â‡› â€²Stream' â€²Bool')
diagB-sem = âŸ¦ diag Bool' âŸ§tm


--------------------------------------------------
-- Example not taken from a paper

-- Î“ âŠ¢ fibs : Stream' Nat'
fibs : TmExpr â˜…
fibs = mod-intro forever g-fibs

fibs-sem : Tm â€²â—‡ (â€²Stream' â€²Nat')
fibs-sem = âŸ¦ fibs âŸ§tm

fibs-agda : Stream â„•
fibs-agda = extract-term fibs-sem

fibs-test : take 10 fibs-agda â‰¡ 1 âˆ· 1 âˆ· 2 âˆ· 3 âˆ· 5 âˆ· 8 âˆ· 13 âˆ· 21 âˆ· 34 âˆ· 55 âˆ· []
fibs-test = refl
