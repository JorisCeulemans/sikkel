module Experimental.DeepEmbedding.GuardedRecursion.TypeChecker where

open import Data.Maybe
open import Data.Nat hiding (_â‰Ÿ_)
open import Data.Product hiding (map)
open import Data.Unit hiding (_â‰Ÿ_)
open import Relation.Binary.PropositionalEquality

open import Categories
open import CwF-Structure
open import Modalities
open Modality
open import Types.Functions
open import Types.Discrete
open import Types.Products
open import Types.Instances
open import GuardedRecursion.Modalities
open import GuardedRecursion.Streams.Guarded


--------------------------------------------------
-- Expressions representing modes, modalities, types, contexts and terms

data ModeExpr : Set where
  e-â˜… e-Ï‰ : ModeExpr

private
  variable
    m m' m'' : ModeExpr

data ModalityExpr : ModeExpr â†’ ModeExpr â†’ Set where
  e-ğŸ™ : ModalityExpr m m
  _e-â“œ_ : ModalityExpr m' m'' â†’ ModalityExpr m m' â†’ ModalityExpr m m''
  e-timeless : ModalityExpr e-â˜… e-Ï‰
  e-allnow : ModalityExpr e-Ï‰ e-â˜…
  e-later : ModalityExpr e-Ï‰ e-Ï‰


infixr 5 _eâ†’_
data TyExpr : ModeExpr â†’ Set where
  e-Nat : TyExpr m
  e-Bool : TyExpr m
  _eâ†’_ : TyExpr m â†’ TyExpr m â†’ TyExpr m
  _e-âŠ _ : TyExpr m â†’ TyExpr m â†’ TyExpr m
  e-mod : ModalityExpr m' m â†’ TyExpr m' â†’ TyExpr m
  e-â–»' : TyExpr e-Ï‰ â†’ TyExpr e-Ï‰
  e-GStream : TyExpr e-â˜… â†’ TyExpr e-Ï‰

data CtxExpr (m : ModeExpr) : Set where
  e-â—‡ : CtxExpr m
  _,_ : (Î“ : CtxExpr m) (T : TyExpr m) â†’ CtxExpr m
  _,lockâŸ¨_âŸ© : (Î“ : CtxExpr m') â†’ ModalityExpr m m' â†’ CtxExpr m

infixl 5 _e-âŠ›'_
data TmExpr : ModeExpr â†’ Set where
  e-ann_âˆˆ_ : TmExpr m â†’ TyExpr m â†’ TmExpr m   -- term with type annotation
  e-var : â„• â†’ TmExpr m
  e-lam : TyExpr m â†’ TmExpr m â†’ TmExpr m
  e-app : TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-lit : â„• â†’ TmExpr m
  e-suc e-plus : TmExpr m
  e-true e-false : TmExpr m
  e-if : TmExpr m â†’ TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-timeless-if : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-pair : TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-fst e-snd : TmExpr m â†’ TmExpr m
  e-mod-intro : ModalityExpr m m' â†’ TmExpr m â†’ TmExpr m'
  e-mod-elim : ModalityExpr m m' â†’ TmExpr m' â†’ TmExpr m
  e-next' : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  _e-âŠ›'_ : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-lÃ¶b : TyExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-cons e-head e-tail : TyExpr e-â˜… â†’ TmExpr e-Ï‰


-- Deciding whether a type expression is a function type, a product type or
--   a modal type and whether a context is of the form Î“ ,lockâŸ¨ Î¼ âŸ©.

record IsFuncTyExpr (T : TyExpr m) : Set where
  constructor func-ty
  field
    dom cod : TyExpr m
    is-func : T â‰¡ dom eâ†’ cod

is-func-ty : (T : TyExpr m) â†’ Maybe (IsFuncTyExpr T)
is-func-ty e-Nat = nothing
is-func-ty e-Bool = nothing
is-func-ty (T1 eâ†’ T2) = just (func-ty T1 T2 refl)
is-func-ty (T1 e-âŠ  T2) = nothing
is-func-ty (e-mod Î¼ T) = nothing
is-func-ty (e-â–»' T) = nothing
is-func-ty (e-GStream T) = nothing

record IsProdTyExpr (T : TyExpr m) : Set where
  constructor prod-ty
  field
    compâ‚ compâ‚‚ : TyExpr m
    is-prod : T â‰¡ compâ‚ e-âŠ  compâ‚‚

is-prod-ty : (T : TyExpr m) â†’ Maybe (IsProdTyExpr T)
is-prod-ty e-Nat = nothing
is-prod-ty e-Bool = nothing
is-prod-ty (T1 eâ†’ T2) = nothing
is-prod-ty (T1 e-âŠ  T2) = just (prod-ty T1 T2 refl)
is-prod-ty (e-mod Î¼ T) = nothing
is-prod-ty (e-â–»' T) = nothing
is-prod-ty (e-GStream T) = nothing

record IsModalTyExpr (T : TyExpr m) : Set where
  constructor modal-ty
  field
    {n} : ModeExpr
    S : TyExpr n
    Î¼ : ModalityExpr n m
    is-modal : T â‰¡ e-mod Î¼ S

is-modal-ty : (T : TyExpr m) â†’ Maybe (IsModalTyExpr T)
is-modal-ty e-Nat = nothing
is-modal-ty e-Bool = nothing
is-modal-ty (T1 eâ†’ T2) = nothing
is-modal-ty (T1 e-âŠ  T2) = nothing
is-modal-ty (e-mod Î¼ T) = just (modal-ty T Î¼ refl)
is-modal-ty (e-â–»' T) = nothing
is-modal-ty (e-GStream T) = nothing

record IsLaterTyExpr (T : TyExpr e-Ï‰) : Set where
  constructor later-ty
  field
    S : TyExpr e-Ï‰
    is-later : T â‰¡ e-â–»' S

is-later-ty : (T : TyExpr e-Ï‰) â†’ Maybe (IsLaterTyExpr T)
is-later-ty e-Nat = nothing
is-later-ty e-Bool = nothing
is-later-ty (T1 eâ†’ T2) = nothing
is-later-ty (T1 e-âŠ  T2) = nothing
is-later-ty (e-mod Î¼ T) = nothing
is-later-ty (e-â–»' T) = just (later-ty T refl)
is-later-ty (e-GStream T) = nothing

record IsModalCtxExpr (Î“ : CtxExpr m) : Set where
  constructor modal-ctx
  field
    {n} : ModeExpr
    Î“' : CtxExpr n
    Î¼ : ModalityExpr m n
    is-modal : Î“ â‰¡ (Î“' ,lockâŸ¨ Î¼ âŸ©)

is-modal-ctx : (Î“ : CtxExpr m) â†’ Maybe (IsModalCtxExpr Î“)
is-modal-ctx e-â—‡ = nothing
is-modal-ctx (Î“ , T) = nothing
is-modal-ctx (Î“ ,lockâŸ¨ Î¼ âŸ©) = just (modal-ctx Î“ Î¼ refl)


-- Checking equality for mode, modality and type expressions.

_â‰Ÿmode_ : (m1 m2 : ModeExpr) â†’ Maybe (m1 â‰¡ m2)
e-â˜… â‰Ÿmode e-â˜… = just refl
e-Ï‰ â‰Ÿmode e-Ï‰ = just refl
_ â‰Ÿmode _ = nothing

_â‰Ÿmodality_ : (Î¼ Ï : ModalityExpr m m') â†’ Maybe (Î¼ â‰¡ Ï)
e-ğŸ™ â‰Ÿmodality e-ğŸ™ = just refl
e-timeless â‰Ÿmodality e-timeless = just refl
e-allnow â‰Ÿmodality e-allnow = just refl
e-later â‰Ÿmodality e-later = just refl
_ â‰Ÿmodality _ = nothing

_â‰Ÿty_ : (T1 T2 : TyExpr m) â†’ Maybe (T1 â‰¡ T2)
e-Nat â‰Ÿty e-Nat = just refl
(T1 eâ†’ T2) â‰Ÿty (T3 eâ†’ T4) = do
  refl â† T1 â‰Ÿty T3
  refl â† T2 â‰Ÿty T4
  just refl
(T1 e-âŠ  T2) â‰Ÿty (T3 e-âŠ  T4) = do
  refl â† T1 â‰Ÿty T3
  refl â† T2 â‰Ÿty T4
  just refl
(e-mod {m1} Î¼1 T1) â‰Ÿty (e-mod {m2} Î¼2 T2) = do
  refl â† m1 â‰Ÿmode m2
  refl â† Î¼1 â‰Ÿmodality Î¼2
  refl â† T1 â‰Ÿty T2
  just refl
(e-â–»' T) â‰Ÿty (e-â–»' S) = map (cong e-â–»') (T â‰Ÿty S)
(e-GStream T) â‰Ÿty (e-GStream S) = map (cong e-GStream) (T â‰Ÿty S)
_ â‰Ÿty _ = nothing


--------------------------------------------------
-- Interpretation of modes, modalities, types and contexts in a presheaf model

âŸ¦_âŸ§mode : ModeExpr â†’ Category
âŸ¦ e-â˜… âŸ§mode = â˜…
âŸ¦ e-Ï‰ âŸ§mode = Ï‰

âŸ¦_âŸ§modality : ModalityExpr m m' â†’ Modality âŸ¦ m âŸ§mode âŸ¦ m' âŸ§mode
âŸ¦ e-ğŸ™ âŸ§modality = ğŸ™
âŸ¦ Î¼ e-â“œ Ï âŸ§modality = âŸ¦ Î¼ âŸ§modality â“œ âŸ¦ Ï âŸ§modality
âŸ¦ e-timeless âŸ§modality = timeless
âŸ¦ e-allnow âŸ§modality = allnow
âŸ¦ e-later âŸ§modality = later

âŸ¦_âŸ§ty : TyExpr m â†’ ClosedType âŸ¦ m âŸ§mode
âŸ¦ e-Nat âŸ§ty = Nat'
âŸ¦ e-Bool âŸ§ty = Bool'
âŸ¦ T1 eâ†’ T2 âŸ§ty = âŸ¦ T1 âŸ§ty â‡› âŸ¦ T2 âŸ§ty
âŸ¦ T1 e-âŠ  T2 âŸ§ty = âŸ¦ T1 âŸ§ty âŠ  âŸ¦ T2 âŸ§ty
âŸ¦ e-mod Î¼ T âŸ§ty = mod âŸ¦ Î¼ âŸ§modality âŸ¦ T âŸ§ty
âŸ¦ e-â–»' T âŸ§ty = â–»' âŸ¦ T âŸ§ty
âŸ¦ e-GStream T âŸ§ty = GStream âŸ¦ T âŸ§ty

âŸ¦_âŸ§ctx : CtxExpr m â†’ Ctx âŸ¦ m âŸ§mode
âŸ¦ e-â—‡ âŸ§ctx = â—‡
âŸ¦ Î“ , T âŸ§ctx = âŸ¦ Î“ âŸ§ctx ,, âŸ¦ T âŸ§ty
âŸ¦ Î“ ,lockâŸ¨ Î¼ âŸ© âŸ§ctx = ctx-op âŸ¦ Î¼ âŸ§modality âŸ¦ Î“ âŸ§ctx

âŸ¦âŸ§ty-natural : (T : TyExpr m) â†’ IsClosedNatural âŸ¦ T âŸ§ty
âŸ¦âŸ§ty-natural e-Nat = discr-closed
âŸ¦âŸ§ty-natural e-Bool = discr-closed
âŸ¦âŸ§ty-natural (T1 eâ†’ T2) = fun-closed {{âŸ¦âŸ§ty-natural T1}} {{âŸ¦âŸ§ty-natural T2}}
âŸ¦âŸ§ty-natural (T1 e-âŠ  T2) = prod-closed {{âŸ¦âŸ§ty-natural T1}} {{âŸ¦âŸ§ty-natural T2}}
âŸ¦âŸ§ty-natural (e-mod Î¼ T) = record { closed-natural = Î» Ïƒ â†’ â‰…áµ—Ê¸-trans (mod-natural âŸ¦ Î¼ âŸ§modality Ïƒ) (mod-cong âŸ¦ Î¼ âŸ§modality (closed-natural {{âŸ¦âŸ§ty-natural T}} _)) }
âŸ¦âŸ§ty-natural (e-â–»' T) = â–»'-closed {{âŸ¦âŸ§ty-natural T}}
âŸ¦âŸ§ty-natural (e-GStream T) = gstream-closed {{âŸ¦âŸ§ty-natural T}}


--------------------------------------------------
-- Definition of a typechecker for the deeply embedded language
--   and interpretaion of well-typed terms in a presheaf model

record InferInterpretResult (Î“ : CtxExpr m) : Set where
  constructor _,_
  field
    type : TyExpr m
    interpretation : Tm âŸ¦ Î“ âŸ§ctx âŸ¦ type âŸ§ty

infer-interpret-var : â„• â†’ (Î“ : CtxExpr m) â†’ Maybe (InferInterpretResult Î“)
infer-interpret-var x       e-â—‡ = nothing
infer-interpret-var zero    (Î“ , T) = just (T , Î¹â»Â¹[ closed-natural {{âŸ¦âŸ§ty-natural T}} Ï€ ] Î¾)
infer-interpret-var (suc x) (Î“ , T) = do
  S , âŸ¦xâŸ§ â† infer-interpret-var x Î“
  just (S , Î¹â»Â¹[ closed-natural {{âŸ¦âŸ§ty-natural S}} Ï€ ] (âŸ¦xâŸ§ [ Ï€ ]'))
infer-interpret-var x       (Î“ ,lockâŸ¨ Î¼ âŸ©) = nothing

infer-interpret : TmExpr m â†’ (Î“ : CtxExpr m) â†’ Maybe (InferInterpretResult Î“)
infer-interpret (e-ann t âˆˆ T) Î“ = do
  T' , âŸ¦tâŸ§ â† infer-interpret t Î“
  refl â† T â‰Ÿty T'
  just (T , âŸ¦tâŸ§)
infer-interpret (e-var x) Î“ = infer-interpret-var x Î“
infer-interpret (e-lam T b) Î“ = do
  S , âŸ¦bâŸ§ â† infer-interpret b (Î“ , T)
  just (T eâ†’ S , lam âŸ¦ T âŸ§ty (Î¹[ closed-natural {{âŸ¦âŸ§ty-natural S}} Ï€ ] âŸ¦bâŸ§))
infer-interpret (e-app t1 t2) Î“ = do
  T1 , âŸ¦t1âŸ§ â† infer-interpret t1 Î“
  func-ty dom cod refl â† is-func-ty T1
  T2 , âŸ¦t2âŸ§ â† infer-interpret t2 Î“
  refl â† dom â‰Ÿty T2
  just (cod , app âŸ¦t1âŸ§ âŸ¦t2âŸ§)
infer-interpret (e-lit n) Î“ = just (e-Nat , discr n)
infer-interpret e-suc Î“ = just (e-Nat eâ†’ e-Nat , suc')
infer-interpret e-plus Î“ = just (e-Nat eâ†’ e-Nat eâ†’ e-Nat , nat-sum)
infer-interpret e-true Î“ = just (e-Bool , true')
infer-interpret e-false Î“ = just (e-Bool , false')
infer-interpret (e-if c t f) Î“ = do
  C , âŸ¦câŸ§ â† infer-interpret c Î“
  refl â† C â‰Ÿty e-Bool
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  F , âŸ¦fâŸ§ â† infer-interpret f Î“
  refl â† T â‰Ÿty F
  just (T , if' âŸ¦câŸ§ then' âŸ¦tâŸ§ else' âŸ¦fâŸ§)
infer-interpret (e-timeless-if c t f) Î“ = do
  C , âŸ¦câŸ§ â† infer-interpret c Î“
  modal-ty {m} B Î¼ refl â† is-modal-ty C
  refl â† m â‰Ÿmode e-â˜…
  refl â† Î¼ â‰Ÿmodality e-timeless
  refl â† B â‰Ÿty e-Bool
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  F , âŸ¦fâŸ§ â† infer-interpret f Î“
  refl â† T â‰Ÿty F
  just (T , timeless-if' âŸ¦câŸ§ then' âŸ¦tâŸ§ else' âŸ¦fâŸ§)
infer-interpret (e-pair t s) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  S , âŸ¦sâŸ§ â† infer-interpret s Î“
  just (T e-âŠ  S , pair $ âŸ¦tâŸ§ $ âŸ¦sâŸ§)
infer-interpret (e-fst p) Î“ = do
  P , âŸ¦pâŸ§ â† infer-interpret p Î“
  prod-ty T S refl â† is-prod-ty P
  just (T , fst $ âŸ¦pâŸ§)
infer-interpret (e-snd p) Î“ = do
  P , âŸ¦pâŸ§ â† infer-interpret p Î“
  prod-ty T S refl â† is-prod-ty P
  just (S , snd $ âŸ¦pâŸ§)
infer-interpret (e-mod-intro Î¼ t) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t (Î“ ,lockâŸ¨ Î¼ âŸ©)
  just (e-mod Î¼ T , mod-intro âŸ¦ Î¼ âŸ§modality âŸ¦tâŸ§)
infer-interpret (e-mod-elim {m} {mÎ¼} Î¼ t) Î“ = do
  modal-ctx {mÏ} Î“' Ï refl â† is-modal-ctx Î“
  refl â† mÎ¼ â‰Ÿmode mÏ
  refl â† Î¼ â‰Ÿmodality Ï
  S , âŸ¦tâŸ§ â† infer-interpret t Î“'
  modal-ty {mÎº} T Îº refl â† is-modal-ty S
  refl â† m â‰Ÿmode mÎº
  refl â† Î¼ â‰Ÿmodality Îº
  just (T , mod-elim âŸ¦ Î¼ âŸ§modality âŸ¦tâŸ§)
infer-interpret (e-next' t) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  just (e-â–»' T , next' âŸ¦tâŸ§)
infer-interpret (f e-âŠ›' t) Î“ = do
  T-f , âŸ¦fâŸ§ â† infer-interpret f Î“
  later-ty S refl â† is-later-ty T-f
  func-ty dom cod refl â† is-func-ty S
  T-t , âŸ¦tâŸ§ â† infer-interpret t Î“
  later-ty R refl â† is-later-ty T-t
  refl â† R â‰Ÿty dom
  just (e-â–»' cod , âŸ¦fâŸ§ âŠ›' âŸ¦tâŸ§)
infer-interpret (e-lÃ¶b T t) Î“ = do
  S , âŸ¦tâŸ§ â† infer-interpret t (Î“ , e-â–»' T)
  refl â† T â‰Ÿty S
  just (T , lÃ¶b' âŸ¦ T âŸ§ty (Î¹[ closed-natural {{âŸ¦âŸ§ty-natural T}} Ï€ ] âŸ¦tâŸ§))
infer-interpret (e-cons T) Î“ = just (e-mod e-timeless T eâ†’ e-â–»' (e-GStream T) eâ†’ e-GStream T , g-cons)
infer-interpret (e-head T) Î“ = just (e-GStream T eâ†’ e-mod e-timeless T , g-head)
infer-interpret (e-tail T) Î“ = just (e-GStream T eâ†’ e-â–»' (e-GStream T) , g-tail)

infer-type : TmExpr m â†’ CtxExpr m â†’ Maybe (TyExpr m)
infer-type t Î“ = map InferInterpretResult.type (infer-interpret t Î“)

âŸ¦_âŸ§tm-in_ : (t : TmExpr m) (Î“ : CtxExpr m) â†’ maybeâ€² (Î» T â†’ Tm âŸ¦ Î“ âŸ§ctx âŸ¦ T âŸ§ty) âŠ¤ (infer-type t Î“)
âŸ¦ t âŸ§tm-in Î“ with infer-interpret t Î“
âŸ¦ t âŸ§tm-in Î“ | just (T , âŸ¦tâŸ§) = âŸ¦tâŸ§
âŸ¦ t âŸ§tm-in Î“ | nothing = tt
