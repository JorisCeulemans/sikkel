module Experimental.DeepEmbedding.GuardedRecursion.TypeChecker where

open import Category.Monad
open import Data.Nat hiding (_â‰Ÿ_)
open import Data.Product hiding (map)
open import Data.Sum hiding (map) renaming ([_,_]â€² to âŠ-elim)
import Data.Sum.Categorical.Left as Sum
open import Data.String
open import Data.Unit hiding (_â‰Ÿ_)
open import Level
open import Relation.Binary.PropositionalEquality

open import Categories
open import CwF-Structure hiding (_++_)
open import Modalities
open Modality
open import Types.Functions
open import Types.Discrete
open import Types.Products
open import Types.Instances
open import GuardedRecursion.Modalities
open import GuardedRecursion.Streams.Guarded


--------------------------------------------------
-- Expressions representing modes, modalities, types, contexts and terms

data ModeExpr : Set where
  e-â˜… e-Ï‰ : ModeExpr

private
  variable
    m m' m'' : ModeExpr

data ModalityExpr : ModeExpr â†’ ModeExpr â†’ Set where
  e-ğŸ™ : ModalityExpr m m
  _e-â“œ_ : ModalityExpr m' m'' â†’ ModalityExpr m m' â†’ ModalityExpr m m''
  e-timeless : ModalityExpr e-â˜… e-Ï‰
  e-allnow : ModalityExpr e-Ï‰ e-â˜…
  e-later : ModalityExpr e-Ï‰ e-Ï‰


infixr 5 _eâ†’_
data TyExpr : ModeExpr â†’ Set where
  e-Nat : TyExpr m
  e-Bool : TyExpr m
  _eâ†’_ : TyExpr m â†’ TyExpr m â†’ TyExpr m
  _e-âŠ _ : TyExpr m â†’ TyExpr m â†’ TyExpr m
  e-mod : ModalityExpr m' m â†’ TyExpr m' â†’ TyExpr m
  e-â–»' : TyExpr e-Ï‰ â†’ TyExpr e-Ï‰
  e-GStream : TyExpr e-â˜… â†’ TyExpr e-Ï‰

data CtxExpr (m : ModeExpr) : Set where
  e-â—‡ : CtxExpr m
  _,_ : (Î“ : CtxExpr m) (T : TyExpr m) â†’ CtxExpr m
  _,lockâŸ¨_âŸ© : (Î“ : CtxExpr m') â†’ ModalityExpr m m' â†’ CtxExpr m

infixl 5 _e-âŠ›'_
data TmExpr : ModeExpr â†’ Set where
  e-ann_âˆˆ_ : TmExpr m â†’ TyExpr m â†’ TmExpr m   -- term with type annotation
  e-var : â„• â†’ TmExpr m
  e-lam : TyExpr m â†’ TmExpr m â†’ TmExpr m
  e-app : TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-lit : â„• â†’ TmExpr m
  e-suc e-plus : TmExpr m
  e-true e-false : TmExpr m
  e-if : TmExpr m â†’ TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-timeless-if : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-pair : TmExpr m â†’ TmExpr m â†’ TmExpr m
  e-fst e-snd : TmExpr m â†’ TmExpr m
  e-mod-intro : ModalityExpr m m' â†’ TmExpr m â†’ TmExpr m'
  e-mod-elim : ModalityExpr m m' â†’ TmExpr m' â†’ TmExpr m
  e-next' : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  _e-âŠ›'_ : TmExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-lÃ¶b : TyExpr e-Ï‰ â†’ TmExpr e-Ï‰ â†’ TmExpr e-Ï‰
  e-cons e-head e-tail : TyExpr e-â˜… â†’ TmExpr e-Ï‰

show-mode : ModeExpr â†’ String
show-mode e-â˜… = "â˜…"
show-mode e-Ï‰ = "Ï‰"

show-modality : ModalityExpr m m' â†’ String
show-modality e-ğŸ™ = "ğŸ™"
show-modality (Î¼ e-â“œ Ï) = show-modality Î¼ ++ " â“œ " ++ show-modality Ï
show-modality e-timeless = "timeless"
show-modality e-allnow = "allnow"
show-modality e-later = "later"

show-type : TyExpr m â†’ String
show-type e-Nat = "Nat"
show-type e-Bool = "Bool"
show-type (T1 eâ†’ T2) = show-type T1 ++ " â†’ " ++ show-type T2
show-type (T1 e-âŠ  T2) = show-type T1 ++ " âŠ  " ++ show-type T2
show-type (e-mod Î¼ T) = "âŸ¨ " ++ show-modality Î¼ ++ " | " ++ show-type T ++ " âŸ©"
show-type (e-â–»' T) = "â–»' " ++ show-type T
show-type (e-GStream T) = "GStream " ++ show-type T

show-ctx : CtxExpr m â†’ String
show-ctx e-â—‡ = "â—‡"
show-ctx (Î“ , T) = show-ctx Î“ ++ " . " ++ show-type T
show-ctx (Î“ ,lockâŸ¨ Î¼ âŸ©) = show-ctx Î“ ++ ".lockâŸ¨ " ++ show-modality Î¼ ++ " âŸ©"


--------------------------------------------------
-- The type checking monad

-- The type checking monad currently only allows for simple strings as error messages.
TCM : Set â†’ Set
TCM A = String âŠ A

type-error : {A : Set} â†’ String â†’ TCM A
type-error = injâ‚

map : âˆ€ {A B} â†’ (A â†’ B) â†’ TCM A â†’ TCM B
map = Data.Sum.mapâ‚‚

open RawMonad (Sum.monad String 0â„“)


--------------------------------------------------
-- Deciding various properties of expressions

-- Deciding whether a type expression is a function type, a product type or
--   a modal type and whether a context is of the form Î“ ,lockâŸ¨ Î¼ âŸ©.

record IsFuncTyExpr (T : TyExpr m) : Set where
  constructor func-ty
  field
    dom cod : TyExpr m
    is-func : T â‰¡ dom eâ†’ cod

is-func-ty : (T : TyExpr m) â†’ TCM (IsFuncTyExpr T)
is-func-ty (T1 eâ†’ T2) = return (func-ty T1 T2 refl)
is-func-ty T = type-error ("Expected a function type but received instead: " ++ show-type T)

record IsProdTyExpr (T : TyExpr m) : Set where
  constructor prod-ty
  field
    compâ‚ compâ‚‚ : TyExpr m
    is-prod : T â‰¡ compâ‚ e-âŠ  compâ‚‚

is-prod-ty : (T : TyExpr m) â†’ TCM (IsProdTyExpr T)
is-prod-ty (T1 e-âŠ  T2) = return (prod-ty T1 T2 refl)
is-prod-ty T = type-error ("Expected a product type but received instead: " ++ show-type T)

record IsModalTyExpr (T : TyExpr m) : Set where
  constructor modal-ty
  field
    {n} : ModeExpr
    S : TyExpr n
    Î¼ : ModalityExpr n m
    is-modal : T â‰¡ e-mod Î¼ S

is-modal-ty : (T : TyExpr m) â†’ TCM (IsModalTyExpr T)
is-modal-ty (e-mod Î¼ T) = return (modal-ty T Î¼ refl)
is-modal-ty T = type-error ("Expected a modal type but received instead: " ++ show-type T)

record IsLaterTyExpr (T : TyExpr e-Ï‰) : Set where
  constructor later-ty
  field
    S : TyExpr e-Ï‰
    is-later : T â‰¡ e-â–»' S

is-later-ty : (T : TyExpr e-Ï‰) â†’ TCM (IsLaterTyExpr T)
is-later-ty (e-â–»' T) = return (later-ty T refl)
is-later-ty T = type-error ("Expected a type of the form â–»' T but received instead: " ++ show-type T)

record IsModalCtxExpr (Î“ : CtxExpr m) : Set where
  constructor modal-ctx
  field
    {n} : ModeExpr
    Î“' : CtxExpr n
    Î¼ : ModalityExpr m n
    is-modal : Î“ â‰¡ (Î“' ,lockâŸ¨ Î¼ âŸ©)

is-modal-ctx : (Î“ : CtxExpr m) â†’ TCM (IsModalCtxExpr Î“)
is-modal-ctx (Î“ ,lockâŸ¨ Î¼ âŸ©) = return (modal-ctx Î“ Î¼ refl)
is-modal-ctx Î“ = type-error ("Expected a context with a lock applied but received instead: " ++ show-ctx Î“)


-- Checking equality for mode, modality and type expressions.

_â‰Ÿmode_ : (m1 m2 : ModeExpr) â†’ TCM (m1 â‰¡ m2)
e-â˜… â‰Ÿmode e-â˜… = return refl
e-Ï‰ â‰Ÿmode e-Ï‰ = return refl
m â‰Ÿmode m' = type-error ("Mode " ++ show-mode m ++ " is not equal to " ++ show-mode m')

_â‰Ÿmodality_ : (Î¼ Ï : ModalityExpr m m') â†’ TCM (Î¼ â‰¡ Ï)
e-ğŸ™ â‰Ÿmodality e-ğŸ™ = return refl
e-timeless â‰Ÿmodality e-timeless = return refl
e-allnow â‰Ÿmodality e-allnow = return refl
e-later â‰Ÿmodality e-later = return refl
Î¼ â‰Ÿmodality Ï = type-error ("Modality " ++ show-modality Î¼ ++ " is not equal to " ++ show-modality Ï)

_â‰Ÿty_ : (T1 T2 : TyExpr m) â†’ TCM (T1 â‰¡ T2)
e-Nat â‰Ÿty e-Nat = return refl
(T1 eâ†’ T2) â‰Ÿty (T3 eâ†’ T4) = do
  refl â† T1 â‰Ÿty T3
  refl â† T2 â‰Ÿty T4
  return refl
(T1 e-âŠ  T2) â‰Ÿty (T3 e-âŠ  T4) = do
  refl â† T1 â‰Ÿty T3
  refl â† T2 â‰Ÿty T4
  return refl
(e-mod {m1} Î¼1 T1) â‰Ÿty (e-mod {m2} Î¼2 T2) = do
  refl â† m1 â‰Ÿmode m2
  refl â† Î¼1 â‰Ÿmodality Î¼2
  refl â† T1 â‰Ÿty T2
  return refl
(e-â–»' T) â‰Ÿty (e-â–»' S) = map (cong e-â–»') (T â‰Ÿty S)
(e-GStream T) â‰Ÿty (e-GStream S) = map (cong e-GStream) (T â‰Ÿty S)
T â‰Ÿty S = type-error ("Type " ++ show-type T ++ " is not equal to " ++ show-type S)


--------------------------------------------------
-- Interpretation of modes, modalities, types and contexts in a presheaf model

âŸ¦_âŸ§mode : ModeExpr â†’ Category
âŸ¦ e-â˜… âŸ§mode = â˜…
âŸ¦ e-Ï‰ âŸ§mode = Ï‰

âŸ¦_âŸ§modality : ModalityExpr m m' â†’ Modality âŸ¦ m âŸ§mode âŸ¦ m' âŸ§mode
âŸ¦ e-ğŸ™ âŸ§modality = ğŸ™
âŸ¦ Î¼ e-â“œ Ï âŸ§modality = âŸ¦ Î¼ âŸ§modality â“œ âŸ¦ Ï âŸ§modality
âŸ¦ e-timeless âŸ§modality = timeless
âŸ¦ e-allnow âŸ§modality = allnow
âŸ¦ e-later âŸ§modality = later

âŸ¦_âŸ§ty : TyExpr m â†’ ClosedType âŸ¦ m âŸ§mode
âŸ¦ e-Nat âŸ§ty = Nat'
âŸ¦ e-Bool âŸ§ty = Bool'
âŸ¦ T1 eâ†’ T2 âŸ§ty = âŸ¦ T1 âŸ§ty â‡› âŸ¦ T2 âŸ§ty
âŸ¦ T1 e-âŠ  T2 âŸ§ty = âŸ¦ T1 âŸ§ty âŠ  âŸ¦ T2 âŸ§ty
âŸ¦ e-mod Î¼ T âŸ§ty = mod âŸ¦ Î¼ âŸ§modality âŸ¦ T âŸ§ty
âŸ¦ e-â–»' T âŸ§ty = â–»' âŸ¦ T âŸ§ty
âŸ¦ e-GStream T âŸ§ty = GStream âŸ¦ T âŸ§ty

âŸ¦_âŸ§ctx : CtxExpr m â†’ Ctx âŸ¦ m âŸ§mode
âŸ¦ e-â—‡ âŸ§ctx = â—‡
âŸ¦ Î“ , T âŸ§ctx = âŸ¦ Î“ âŸ§ctx ,, âŸ¦ T âŸ§ty
âŸ¦ Î“ ,lockâŸ¨ Î¼ âŸ© âŸ§ctx = ctx-op âŸ¦ Î¼ âŸ§modality âŸ¦ Î“ âŸ§ctx

âŸ¦âŸ§ty-natural : (T : TyExpr m) â†’ IsClosedNatural âŸ¦ T âŸ§ty
âŸ¦âŸ§ty-natural e-Nat = discr-closed
âŸ¦âŸ§ty-natural e-Bool = discr-closed
âŸ¦âŸ§ty-natural (T1 eâ†’ T2) = fun-closed {{âŸ¦âŸ§ty-natural T1}} {{âŸ¦âŸ§ty-natural T2}}
âŸ¦âŸ§ty-natural (T1 e-âŠ  T2) = prod-closed {{âŸ¦âŸ§ty-natural T1}} {{âŸ¦âŸ§ty-natural T2}}
âŸ¦âŸ§ty-natural (e-mod Î¼ T) = record { closed-natural = Î» Ïƒ â†’ â‰…áµ—Ê¸-trans (mod-natural âŸ¦ Î¼ âŸ§modality Ïƒ) (mod-cong âŸ¦ Î¼ âŸ§modality (closed-natural {{âŸ¦âŸ§ty-natural T}} _)) }
âŸ¦âŸ§ty-natural (e-â–»' T) = â–»'-closed {{âŸ¦âŸ§ty-natural T}}
âŸ¦âŸ§ty-natural (e-GStream T) = gstream-closed {{âŸ¦âŸ§ty-natural T}}


--------------------------------------------------
-- Definition of a typechecker for the deeply embedded language
--   and interpretaion of well-typed terms in a presheaf model

record InferInterpretResult (Î“ : CtxExpr m) : Set where
  constructor _,_
  field
    type : TyExpr m
    interpretation : Tm âŸ¦ Î“ âŸ§ctx âŸ¦ type âŸ§ty

infer-interpret-var : â„• â†’ (Î“ : CtxExpr m) â†’ TCM (InferInterpretResult Î“)
infer-interpret-var x       e-â—‡ = type-error "There is a reference to a variable that does not exist in this context."
infer-interpret-var zero    (Î“ , T) = return (T , Î¹â»Â¹[ closed-natural {{âŸ¦âŸ§ty-natural T}} Ï€ ] Î¾)
infer-interpret-var (suc x) (Î“ , T) = do
  S , âŸ¦xâŸ§ â† infer-interpret-var x Î“
  return (S , Î¹â»Â¹[ closed-natural {{âŸ¦âŸ§ty-natural S}} Ï€ ] (âŸ¦xâŸ§ [ Ï€ ]'))
infer-interpret-var x       (Î“ ,lockâŸ¨ Î¼ âŸ©) = type-error "Impossible to directly use a variable from a locked context."

infer-interpret : TmExpr m â†’ (Î“ : CtxExpr m) â†’ TCM (InferInterpretResult Î“)
infer-interpret (e-ann t âˆˆ T) Î“ = do
  T' , âŸ¦tâŸ§ â† infer-interpret t Î“
  refl â† T â‰Ÿty T'
  return (T , âŸ¦tâŸ§)
infer-interpret (e-var x) Î“ = infer-interpret-var x Î“
infer-interpret (e-lam T b) Î“ = do
  S , âŸ¦bâŸ§ â† infer-interpret b (Î“ , T)
  return (T eâ†’ S , lam âŸ¦ T âŸ§ty (Î¹[ closed-natural {{âŸ¦âŸ§ty-natural S}} Ï€ ] âŸ¦bâŸ§))
infer-interpret (e-app t1 t2) Î“ = do
  T1 , âŸ¦t1âŸ§ â† infer-interpret t1 Î“
  func-ty dom cod refl â† is-func-ty T1
  T2 , âŸ¦t2âŸ§ â† infer-interpret t2 Î“
  refl â† dom â‰Ÿty T2
  return (cod , app âŸ¦t1âŸ§ âŸ¦t2âŸ§)
infer-interpret (e-lit n) Î“ = return (e-Nat , discr n)
infer-interpret e-suc Î“ = return (e-Nat eâ†’ e-Nat , suc')
infer-interpret e-plus Î“ = return (e-Nat eâ†’ e-Nat eâ†’ e-Nat , nat-sum)
infer-interpret e-true Î“ = return (e-Bool , true')
infer-interpret e-false Î“ = return (e-Bool , false')
infer-interpret (e-if c t f) Î“ = do
  C , âŸ¦câŸ§ â† infer-interpret c Î“
  refl â† C â‰Ÿty e-Bool
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  F , âŸ¦fâŸ§ â† infer-interpret f Î“
  refl â† T â‰Ÿty F
  return (T , if' âŸ¦câŸ§ then' âŸ¦tâŸ§ else' âŸ¦fâŸ§)
infer-interpret (e-timeless-if c t f) Î“ = do
  C , âŸ¦câŸ§ â† infer-interpret c Î“
  modal-ty {m} B Î¼ refl â† is-modal-ty C
  refl â† m â‰Ÿmode e-â˜…
  refl â† Î¼ â‰Ÿmodality e-timeless
  refl â† B â‰Ÿty e-Bool
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  F , âŸ¦fâŸ§ â† infer-interpret f Î“
  refl â† T â‰Ÿty F
  return (T , timeless-if' âŸ¦câŸ§ then' âŸ¦tâŸ§ else' âŸ¦fâŸ§)
infer-interpret (e-pair t s) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  S , âŸ¦sâŸ§ â† infer-interpret s Î“
  return (T e-âŠ  S , pair $ âŸ¦tâŸ§ $ âŸ¦sâŸ§)
infer-interpret (e-fst p) Î“ = do
  P , âŸ¦pâŸ§ â† infer-interpret p Î“
  prod-ty T S refl â† is-prod-ty P
  return (T , fst $ âŸ¦pâŸ§)
infer-interpret (e-snd p) Î“ = do
  P , âŸ¦pâŸ§ â† infer-interpret p Î“
  prod-ty T S refl â† is-prod-ty P
  return (S , snd $ âŸ¦pâŸ§)
infer-interpret (e-mod-intro Î¼ t) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t (Î“ ,lockâŸ¨ Î¼ âŸ©)
  return (e-mod Î¼ T , mod-intro âŸ¦ Î¼ âŸ§modality âŸ¦tâŸ§)
infer-interpret (e-mod-elim {m} {mÎ¼} Î¼ t) Î“ = do
  modal-ctx {mÏ} Î“' Ï refl â† is-modal-ctx Î“
  refl â† mÎ¼ â‰Ÿmode mÏ
  refl â† Î¼ â‰Ÿmodality Ï
  S , âŸ¦tâŸ§ â† infer-interpret t Î“'
  modal-ty {mÎº} T Îº refl â† is-modal-ty S
  refl â† m â‰Ÿmode mÎº
  refl â† Î¼ â‰Ÿmodality Îº
  return (T , mod-elim âŸ¦ Î¼ âŸ§modality âŸ¦tâŸ§)
infer-interpret (e-next' t) Î“ = do
  T , âŸ¦tâŸ§ â† infer-interpret t Î“
  return (e-â–»' T , next' âŸ¦tâŸ§)
infer-interpret (f e-âŠ›' t) Î“ = do
  T-f , âŸ¦fâŸ§ â† infer-interpret f Î“
  later-ty S refl â† is-later-ty T-f
  func-ty dom cod refl â† is-func-ty S
  T-t , âŸ¦tâŸ§ â† infer-interpret t Î“
  later-ty R refl â† is-later-ty T-t
  refl â† R â‰Ÿty dom
  return (e-â–»' cod , âŸ¦fâŸ§ âŠ›' âŸ¦tâŸ§)
infer-interpret (e-lÃ¶b T t) Î“ = do
  S , âŸ¦tâŸ§ â† infer-interpret t (Î“ , e-â–»' T)
  refl â† T â‰Ÿty S
  return (T , lÃ¶b' âŸ¦ T âŸ§ty (Î¹[ closed-natural {{âŸ¦âŸ§ty-natural T}} Ï€ ] âŸ¦tâŸ§))
infer-interpret (e-cons T) Î“ = return (e-mod e-timeless T eâ†’ e-â–»' (e-GStream T) eâ†’ e-GStream T , g-cons)
infer-interpret (e-head T) Î“ = return (e-GStream T eâ†’ e-mod e-timeless T , g-head)
infer-interpret (e-tail T) Î“ = return (e-GStream T eâ†’ e-â–»' (e-GStream T) , g-tail)

infer-type : TmExpr m â†’ CtxExpr m â†’ TCM (TyExpr m)
infer-type t Î“ = map InferInterpretResult.type (infer-interpret t Î“)

âŸ¦_âŸ§tm-in_ : (t : TmExpr m) (Î“ : CtxExpr m) â†’ âŠ-elim (Î» _ â†’ âŠ¤) (Î» T â†’ Tm âŸ¦ Î“ âŸ§ctx âŸ¦ T âŸ§ty) (infer-type t Î“)
âŸ¦ t âŸ§tm-in Î“ with infer-interpret t Î“
âŸ¦ t âŸ§tm-in Î“ | injâ‚ _ = tt
âŸ¦ t âŸ§tm-in Î“ | injâ‚‚ (T , âŸ¦tâŸ§)  = âŸ¦tâŸ§
