--------------------------------------------------
-- Definition of MSTT terms
--------------------------------------------------

open import Experimental.LogicalFramework.MSTT.Parameter.ModeTheory
open import Experimental.LogicalFramework.MSTT.Parameter.TypeExtension
open import Experimental.LogicalFramework.MSTT.Parameter.TermExtension using (TmExt)

module Experimental.LogicalFramework.MSTT.Syntax.Terms
  (â„³ : ModeTheory) (ğ’¯ : TyExt â„³) (ğ“‰ : TmExt â„³ ğ’¯)
  where

open import Data.Empty
open import Data.List
open import Data.Maybe as Maybe hiding (Is-just; to-witness)
open import Data.Product
import Data.String as Name
open import Data.Unit
open import Relation.Nullary
import Relation.Binary.PropositionalEquality as PropEq
open PropEq hiding (refl)
open PropEq using (refl) public
-- ^ refl is re-exported so that it becomes available for instance
--   search when using the functions var and svar defined in this
--   module.

open ModeTheory â„³
open TmExt ğ“‰

open Experimental.LogicalFramework.MSTT.Parameter.TermExtension â„³ ğ’¯ hiding (TmExt)
open import Experimental.LogicalFramework.MSTT.Syntax.Types â„³ ğ’¯
open import Experimental.LogicalFramework.MSTT.Syntax.Contexts â„³ ğ’¯

private variable
  m n o p : Mode
  Î¼ Ï Îº Ï† : Modality m n
  T S : Ty m
  x y : Name
  Î“ Î” Î˜ : Ctx m
  Î› : LockTele m n


--------------------------------------------------
-- Definition of MSTT variables

-- A value of type Var x T Î“ Î› expresses that there is a valid
-- variable (including the necessary 2-cell) with name x and type T in
-- the context Î“ ,Ë¡áµ— Î›. We explicitly keep track of the lock telescope
-- Î› in order to match the recursion structure of many of the
-- variable-manipulating functions further in this file. Note that Î›
-- is required to be empty when constructing a term.
data Var (x : Name) (T : Ty n) : Ctx m â†’ LockTele m n â†’ Set where
  vzero : {Î¼ : Modality n m} {Î“ : Ctx m} {Î› : LockTele m n} â†’
          TwoCell Î¼ (locksË¡áµ— Î›) â†’
          Var x T (Î“ ,, Î¼ âˆ£ x âˆˆ T) Î›
  vsuc : {Î“ : Ctx m} {Î› : LockTele m n} {Ï : Modality o m} {y : Name} {S : Ty o} â†’
         Var x T Î“ Î› â†’
         Var x T (Î“ ,, Ï âˆ£ y âˆˆ S) Î›
  vlock : {Î“ : Ctx o} {Ï : Modality m o} {Î› : LockTele m n} â†’
          Var x T Î“ (lockâŸ¨ Ï âŸ©, Î›) â†’
          Var x T (Î“ ,lockâŸ¨ Ï âŸ©) Î›

vlocks : {x : Name} {T : Ty n} {Î“ : Ctx o} (Î˜ : LockTele o m) {Î› : LockTele m n} â†’
         Var x T Î“ (Î˜ ++Ë¡áµ— Î›) â†’
         Var x T (Î“ ,Ë¡áµ— Î˜) Î›
vlocks â—‡             v = v
vlocks (lockâŸ¨ Î¼ âŸ©, Î˜) v = vlocks Î˜ (vlock v)

unvlock : {x : Name} {T : Ty n} {Î“ : Ctx o} {Î¼ : Modality m o} {Î› : LockTele m n} â†’
          Var x T (Î“ ,lockâŸ¨ Î¼ âŸ©) Î› â†’
          Var x T Î“ (lockâŸ¨ Î¼ âŸ©, Î›)
unvlock (vlock v) = v

unvlocks : {x : Name} {T : Ty n} {Î“ : Ctx o} (Î˜ : LockTele o m) {Î› : LockTele m n} â†’
           Var x T (Î“ ,Ë¡áµ— Î˜) Î› â†’
           Var x T Î“ (Î˜ ++Ë¡áµ— Î›)
unvlocks â—‡             v = v
unvlocks (lockâŸ¨ Î¼ âŸ©, Î˜) v = unvlock (unvlocks Î˜ v)


--------------------------------------------------
-- Definition of MSTT terms

TmArgBoundNames : List (TmArgInfo m) â†’ Set
TmArgBoundNames []                   = âŠ¤
TmArgBoundNames (arginfo âˆ· arginfos) = Names (tmarg-tel arginfo) Ã— TmArgBoundNames arginfos

infixl 50 _âˆ™_
data Tm : Ctx m â†’ Ty m â†’ Set
ExtTmArgs : {m : Mode} (arginfos : List (TmArgInfo m)) â†’ TmArgBoundNames arginfos â†’ Ctx m â†’ Set

data Tm where
  var' : (x : Name) {v : Var x T Î“ â—‡} â†’ Tm Î“ T
    -- ^ When writing programs, one should not directly use var' but rather combine
    --   it with a decision procedure for Var, which will resolve the name. See below.
  modâŸ¨_âŸ©_ : (Î¼ : Modality n m) â†’ Tm (Î“ ,lockâŸ¨ Î¼ âŸ©) T â†’ Tm Î“ âŸ¨ Î¼ âˆ£ T âŸ©
  mod-elim : (Ï : Modality o m) (Î¼ : Modality n o) (x : Name)
             (t : Tm (Î“ ,lockâŸ¨ Ï âŸ©) âŸ¨ Î¼ âˆ£ T âŸ©) (s : Tm (Î“ ,, Ï â“œ Î¼ âˆ£ x âˆˆ T) S) â†’
             Tm Î“ S
  lam[_âˆ£_âˆˆ_]_ : (Î¼ : Modality n m) (x : Name) (T : Ty n) â†’ Tm (Î“ ,, Î¼ âˆ£ x âˆˆ T) S â†’ Tm Î“ (âŸ¨ Î¼ âˆ£ T âŸ©â‡› S)
  _âˆ™_ : {Î¼ : Modality n m} â†’ Tm Î“ (âŸ¨ Î¼ âˆ£ T âŸ©â‡› S) â†’ Tm (Î“ ,lockâŸ¨ Î¼ âŸ©) T â†’ Tm Î“ S
  zero : Tm Î“ Nat'
  suc : Tm Î“ Nat' â†’ Tm Î“ Nat'
  nat-rec : {A : Ty m} â†’ Tm Î“ A â†’ Tm Î“ (A â‡› A) â†’ Tm Î“ Nat' â†’ Tm Î“ A
  true false : Tm Î“ Bool'
  if : {A : Ty m} â†’ Tm Î“ Bool' â†’ (t f : Tm Î“ A) â†’ Tm Î“ A
  pair : Tm Î“ T â†’ Tm Î“ S â†’ Tm Î“ (T âŠ  S)
  fst : Tm Î“ (T âŠ  S) â†’ Tm Î“ T
  snd : Tm Î“ (T âŠ  S) â†’ Tm Î“ S
  ext : (c : TmExtCode m) (names : TmArgBoundNames (tm-code-arginfos c)) â†’
        ExtTmArgs (tm-code-arginfos c) names Î“ â†’
        T â‰¡ tm-code-ty c â†’
        Tm Î“ T
    -- ^ This constructor is not intended for direct use. An instantiation of MSTT with
    --   specific term extensions should rather provide more convenient term formers via pattern synonyms.

ExtTmArgs []                   _                        Î“ = âŠ¤
ExtTmArgs (arginfo âˆ· arginfos) (arg-names , args-names) Î“ =
  Tm (Î“ ++tel add-names (tmarg-tel arginfo) arg-names) (tmarg-ty arginfo) Ã— ExtTmArgs arginfos args-names Î“


vzero-id : Var x T (Î“ ,, Î¼ âˆ£ x âˆˆ T) (lockâŸ¨ Î¼ âŸ©, â—‡)
vzero-id = vzero id-cell

v0 : Tm (Î“ ,, Î¼ âˆ£ x âˆˆ T ,lockâŸ¨ Î¼ âŸ©) T
v0 = var' _ {vlock vzero-id}

v1 : Tm (Î“ ,, Î¼ âˆ£ x âˆˆ T ,, Îº âˆ£ y âˆˆ S ,lockâŸ¨ Î¼ âŸ©) T
v1 = var' _ {vlock (vsuc vzero-id)}

v0-nolock : Tm (Î“ ,, ğŸ™ âˆ£ x âˆˆ T) T
v0-nolock = var' _ {vzero id-cell}

v1-nolock : Tm (Î“ ,, ğŸ™ âˆ£ x âˆˆ T ,, Î¼ âˆ£ y âˆˆ S) T
v1-nolock = var' _ {vsuc (vzero id-cell)}

syntax mod-elim Ï Î¼ x t s = letâŸ¨ Ï âŸ© modâŸ¨ Î¼ âŸ© x â† t in' s

var-lt : (Î› : LockTele n m) â†’ Var x T Î“ Î› â†’ Tm (Î“ ,Ë¡áµ— Î›) T
var-lt â—‡              v = var' _ {v}
var-lt (lockâŸ¨ Î¼ âŸ©, Î›) v = var-lt Î› (vlock v)


--------------------------------------------------
-- Constructing a variable term by only mentioning the variable name
--   (i.e. resolving the De Bruijn index automatically).
--   This works via a (semi)decision procedure for RawVar x Î“ Î›.


-- Preliminary work: we have to reimplement Is-just and from-witness
-- from Data.Maybe so that the definition does not use a data type and
-- hence satisfies Î· equality.
Is-just' : âˆ€ {â„“} {A : Set â„“} â†’ Maybe A â†’ Set _
Is-just' (just _) = âŠ¤
Is-just' nothing  = âŠ¥

to-witness' : âˆ€ {â„“} {A : Set â„“} {m : Maybe A} â†’ Is-just' m â†’ A
to-witness' {m = just a} _ = a


-- A value of type RawVar x Î“ Î› means that there is an entry in
-- context Î“ ,Ë¡áµ— Î› with the name x. It does however not provide the
-- necessary 2-cell, so this does not necessarily give rise to a valid
-- variable (we actually want to provide this *after* the right raw
-- variable has been found). Moreover, in the case of rvzero the
-- domain of Î¼ does not necessarily correspond to the codomain of
-- Î›. This will be tested later, but it has to be done via instance
-- resolution rather than applying a (semi)decision procedure in order
-- to accomodate some form of mode polymorphism (m â‰Ÿmode m will be
-- stuck when m contains an Agda variable).
data RawVar (x : Name) : Ctx m â†’ LockTele m n â†’ Set where
  rvzero : {Î“ : Ctx m} {Î¼ : Modality o m} {T : Ty o} {Î› : LockTele m n} â†’
           RawVar x (Î“ ,, Î¼ âˆ£ x âˆˆ T) Î›
  rvsuc : {Î“ : Ctx m} {Î› : LockTele m n} {Ï : Modality o m} {y : Name} {S : Ty o} â†’
          RawVar x Î“ Î› â†’
          RawVar x (Î“ ,, Ï âˆ£ y âˆˆ S) Î›
  rvlock : {Î“ : Ctx o} {Ï : Modality m o} {Î› : LockTele m n} â†’
           RawVar x Î“ (lockâŸ¨ Ï âŸ©, Î›) â†’
           RawVar x (Î“ ,lockâŸ¨ Ï âŸ©) Î›

rawvar? : (x : Name) (Î“ : Ctx m) (Î› : LockTele m n) â†’ Maybe (RawVar x Î“ Î›)
rawvar? x â—‡                 Î› = nothing
rawvar? x (Î“ ,, Î¼ âˆ£ y âˆˆ T)  Î› with x Name.â‰Ÿ y
rawvar? x (Î“ ,, Î¼ âˆ£ .x âˆˆ T) Î› | yes refl = just rvzero
rawvar? x (Î“ ,, Î¼ âˆ£ y âˆˆ T)  Î› | no _ = Maybe.map rvsuc (rawvar? x Î“ Î›)
rawvar? x (Î“ ,lockâŸ¨ Î¼ âŸ©)    Î› = Maybe.map rvlock (rawvar? x Î“ (lockâŸ¨ Î¼ âŸ©, Î›))

rv-dom-mode : RawVar x Î“ Î› â†’ Mode
rv-dom-mode (rvzero {o = o}) = o
rv-dom-mode (rvsuc rv)       = rv-dom-mode rv
rv-dom-mode (rvlock rv)      = rv-dom-mode rv

rv-cod-mode : RawVar x Î“ Î› â†’ Mode
rv-cod-mode (rvzero {m = m}) = m
rv-cod-mode (rvsuc rv)       = rv-cod-mode rv
rv-cod-mode (rvlock rv)      = rv-cod-mode rv

rv-mod : (rv : RawVar x Î“ Î›) â†’ Modality (rv-dom-mode rv) (rv-cod-mode rv)
rv-mod (rvzero {Î¼ = Î¼}) = Î¼
rv-mod (rvsuc rv)       = rv-mod rv
rv-mod (rvlock rv)      = rv-mod rv

rv-ty : (rv : RawVar x Î“ Î›) â†’ Ty (rv-dom-mode rv)
rv-ty (rvzero {T = T}) = T
rv-ty (rvsuc rv)       = rv-ty rv
rv-ty (rvlock rv)      = rv-ty rv

rv-lt : {Î› : LockTele m n} (rv : RawVar x Î“ Î›) â†’ LockTele (rv-cod-mode rv) n
rv-lt (rvzero {Î› = Î›}) = Î›
rv-lt (rvsuc rv)       = rv-lt rv
rv-lt (rvlock rv)      = rv-lt rv

create-var : {Î“ : Ctx m} {Î› : LockTele m n}
             (rv : RawVar x Î“ Î›)
             (e : n â‰¡ rv-dom-mode rv) â†’
             TwoCell (rv-mod rv) (subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv))) â†’
             Var x (subst Ty (sym e) (rv-ty rv)) Î“ Î›
create-var rvzero      refl Î± = vzero Î±
create-var (rvsuc rv)  e    Î± = vsuc (create-var rv e Î±)
create-var (rvlock rv) e    Î± = vlock (create-var rv e Î±)

-- The instance arguments in var and svar are intended to be solved as
-- refl by instance search.
var : (x : Name) {Î“ : Ctx m} {i : Is-just' (rawvar? x Î“ â—‡)} â†’
      let rv = to-witness' i
      in {{ e : m â‰¡ rv-dom-mode rv }} â†’
         TwoCell (rv-mod rv) (subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv))) â†’
         Tm Î“ (subst Ty (sym e) (rv-ty rv))
var x {i = i} {{e}} Î± = var' _ {create-var (to-witness' i) e Î±}

svar : (x : Name) {Î“ : Ctx m} {i : Is-just' (rawvar? x Î“ â—‡)} â†’
       let rv = to-witness' i
       in {{ e : m â‰¡ rv-dom-mode rv }} â†’
          {{ rv-mod rv â‰¡ subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv)) }} â†’
          Tm Î“ (subst Ty (sym e) (rv-ty rv))
svar x {i = i} {{emode}} {{emod}} = var' _ {create-var (to-witness' i) emode (eq-cell emod)}
