--------------------------------------------------
-- Instantiation of the general MSTT syntax with strings as names
--------------------------------------------------

open import Experimental.LogicalFramework.MSTT.Parameter.ModeTheory
open import Experimental.LogicalFramework.MSTT.Parameter.TypeExtension
open import Experimental.LogicalFramework.MSTT.Parameter.TermExtension
open import Data.String as Str

module Experimental.LogicalFramework.MSTT.Syntax.Named
  (â„³ : ModeTheory) (ğ’¯ : TyExt â„³) (ğ“‰ : TmExt â„³ ğ’¯ String)
  where

open import Data.Empty
open import Data.Maybe as Maybe hiding (Is-just; to-witness)
open import Data.Product
open import Data.Unit
open import Relation.Nullary
import Relation.Binary.PropositionalEquality as PropEq
open PropEq hiding (refl)
open PropEq using (refl) public
-- ^ refl is re-exported so that it becomes available for instance
--   search when using the functions var and svar defined in this
--   module.

open ModeTheory â„³


--------------------------------------------------
-- Re-exporting the definitions of types, contexts, terms and associated operations.

open import Experimental.LogicalFramework.MSTT.Syntax.Types â„³ ğ’¯ public
open import Experimental.LogicalFramework.MSTT.Syntax.Contexts â„³ ğ’¯ String public
open import Experimental.LogicalFramework.MSTT.Syntax.General â„³ ğ’¯ String ğ“‰ public

private variable
  m n o : Mode
  Î¼ : Modality m n
  Î“ : Ctx m
  Î› : LockTele m n
  T S : Ty m
  x y : String


--------------------------------------------------
-- Preliminary work: we have to reimplement Is-just and from-witness
-- from Data.Maybe so that the definition does not use a data type and
-- hence satisfies Î· equality.

Is-just' : âˆ€ {â„“} {A : Set â„“} â†’ Maybe A â†’ Set _
Is-just' (just _) = âŠ¤
Is-just' nothing  = âŠ¥

to-witness' : âˆ€ {â„“} {A : Set â„“} {m : Maybe A} â†’ Is-just' m â†’ A
to-witness' {m = just a} _ = a


--------------------------------------------------
-- Constructing a variable term by only mentioning the variable name
--   (i.e. resolving the De Bruijn index automatically).
--   This works via a (semi)decision procedure for RawVar x Î“ Î›.


-- A value of type RawVar x Î“ Î› means that there is an entry in
-- context Î“ ,Ë¡áµ— Î› with the name x.  It does however not provide the
-- necessary 2-cell, so this does not necessarily give rise to a valid
-- variable. Moreover, in the case of rvzero the domain of Î¼ does not
-- necessarily correspond to the codomain of Î›. This will be tested
-- later, but it has to be done via instance resolution rather than
-- applying a (semi)decision procedure in order to accomodate some
-- form of mode polymorphism (m â‰Ÿmode m will be stuck when m contains
-- an Agda variable).
data RawVar (x : String) : Ctx m â†’ LockTele m n â†’ Set where
  rvzero : {Î“ : Ctx m} {Î¼ : Modality o m} {T : Ty o} {Î› : LockTele m n} â†’
           RawVar x (Î“ ,, Î¼ âˆ£ x âˆˆ T) Î›
  rvsuc : {Î“ : Ctx m} {Î› : LockTele m n} {Ï : Modality o m} {y : String} {S : Ty o} â†’
          RawVar x Î“ Î› â†’
          RawVar x (Î“ ,, Ï âˆ£ y âˆˆ S) Î›
  rvlock : {Î“ : Ctx o} {Ï : Modality m o} {Î› : LockTele m n} â†’
           RawVar x Î“ (lockâŸ¨ Ï âŸ©, Î›) â†’
           RawVar x (Î“ ,lockâŸ¨ Ï âŸ©) Î›


rawvar? : (x : String) (Î“ : Ctx m) (Î› : LockTele m n) â†’ Maybe (RawVar x Î“ Î›)
rawvar? x â—‡                 Î› = nothing
rawvar? x (Î“ ,, Î¼ âˆ£ y âˆˆ T)  Î› with x Str.â‰Ÿ y
rawvar? x (Î“ ,, Î¼ âˆ£ .x âˆˆ T) Î› | yes refl = just rvzero
rawvar? x (Î“ ,, Î¼ âˆ£ y âˆˆ T)  Î› | no _ = Maybe.map rvsuc (rawvar? x Î“ Î›)
rawvar? x (Î“ ,lockâŸ¨ Î¼ âŸ©)    Î› = Maybe.map rvlock (rawvar? x Î“ (lockâŸ¨ Î¼ âŸ©, Î›))

rv-dom-mode : RawVar x Î“ Î› â†’ Mode
rv-dom-mode (rvzero {o = o}) = o
rv-dom-mode (rvsuc rv)       = rv-dom-mode rv
rv-dom-mode (rvlock rv)      = rv-dom-mode rv

rv-cod-mode : RawVar x Î“ Î› â†’ Mode
rv-cod-mode (rvzero {m = m}) = m
rv-cod-mode (rvsuc rv)       = rv-cod-mode rv
rv-cod-mode (rvlock rv)      = rv-cod-mode rv

rv-mod : (rv : RawVar x Î“ Î›) â†’ Modality (rv-dom-mode rv) (rv-cod-mode rv)
rv-mod (rvzero {Î¼ = Î¼}) = Î¼
rv-mod (rvsuc rv)       = rv-mod rv
rv-mod (rvlock rv)      = rv-mod rv

rv-ty : (rv : RawVar x Î“ Î›) â†’ Ty (rv-dom-mode rv)
rv-ty (rvzero {T = T}) = T
rv-ty (rvsuc rv)       = rv-ty rv
rv-ty (rvlock rv)      = rv-ty rv

rv-lt : {Î› : LockTele m n} (rv : RawVar x Î“ Î›) â†’ LockTele (rv-cod-mode rv) n
rv-lt (rvzero {Î› = Î›}) = Î›
rv-lt (rvsuc rv)       = rv-lt rv
rv-lt (rvlock rv)      = rv-lt rv

create-var : {Î“ : Ctx m} {Î› : LockTele m n}
             (rv : RawVar x Î“ Î›)
             (e : n â‰¡ rv-dom-mode rv) â†’
             TwoCell (rv-mod rv) (subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv))) â†’
             Var x (subst Ty (sym e) (rv-ty rv)) Î“ Î›
create-var rvzero      refl Î± = vzero Î±
create-var (rvsuc rv)  e    Î± = vsuc (create-var rv e Î±)
create-var (rvlock rv) e    Î± = vlock (create-var rv e Î±)

-- The instance arguments in var and svar are intended to be solved as
-- refl by instance search.
var : (x : String) {Î“ : Ctx m} {i : Is-just' (rawvar? x Î“ â—‡)} â†’
      let rv = to-witness' i
      in {{ e : m â‰¡ rv-dom-mode rv }} â†’
         TwoCell (rv-mod rv) (subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv))) â†’
         Tm Î“ (subst Ty (sym e) (rv-ty rv))
var x {i = i} {{e}} Î± = var' _ {create-var (to-witness' i) e Î±}

svar : (x : String) {Î“ : Ctx m} {i : Is-just' (rawvar? x Î“ â—‡)} â†’
       let rv = to-witness' i
       in {{ e : m â‰¡ rv-dom-mode rv }} â†’
          {{ rv-mod rv â‰¡ subst (Î» - â†’ Modality - (rv-cod-mode rv)) e (locksË¡áµ— (rv-lt rv)) }} â†’
          Tm Î“ (subst Ty (sym e) (rv-ty rv))
svar x {i = i} {{emode}} {{emod}} = var' _ {create-var (to-witness' i) emode (eq-cell emod)}
