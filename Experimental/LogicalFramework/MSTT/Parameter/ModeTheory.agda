module Experimental.LogicalFramework.MSTT.Parameter.ModeTheory where

open import Data.Maybe
open import Relation.Binary.PropositionalEquality


record MTBasis : Setâ‚ where
  field
    Mode : Set
    NonTrivModality : Mode â†’ Mode â†’ Set
    mode-eq? : (m n : Mode) â†’ Maybe (m â‰¡ n)
    non-triv-mod-eq? : âˆ€ {m n} (Î¼ Îº : NonTrivModality m n) â†’ Maybe (Î¼ â‰¡ Îº)

  infix 50 â€µ_
  data Modality : Mode â†’ Mode â†’ Set where
    ğŸ™ : âˆ€ {m} â†’ Modality m m
    â€µ_ : âˆ€ {m n} â†’ NonTrivModality m n â†’ Modality m n

  mod-dom mod-cod : âˆ€ {m n} â†’  Modality m n â†’ Mode
  mod-dom {m}     Î¼ = m
  mod-cod {_} {n} Î¼ = n


record MTComposition (mtb : MTBasis) : Set where
  open MTBasis mtb

  field
    _â“œnon-triv_ : âˆ€ {m n o} â†’ NonTrivModality n o â†’ NonTrivModality m n â†’ Modality m o

  _â“œ_ : âˆ€ {m n o} â†’ Modality n o â†’ Modality m n â†’ Modality m o
  ğŸ™ â“œ Ï = Ï
  â€µ Î¼ â“œ ğŸ™ = â€µ Î¼
  â€µ Î¼ â“œ â€µ Ï = Î¼ â“œnon-triv Ï


record MTCompositionLaws (mtb : MTBasis) (mtc : MTComposition mtb) : Set where
  open MTBasis mtb
  open MTComposition mtc
  
  field
    mod-non-triv-assoc : âˆ€ {m n o p} â†’ (Î¼ : NonTrivModality o p) (Ï : NonTrivModality n o) (Îº : NonTrivModality m n) â†’
                         (Î¼ â“œnon-triv Ï) â“œ â€µ Îº â‰¡ â€µ Î¼ â“œ (Ï â“œnon-triv Îº)

  mod-unitË¡ : âˆ€ {m n} {Î¼ : Modality m n} â†’ ğŸ™ â“œ Î¼ â‰¡ Î¼
  mod-unitË¡  = refl

  mod-unitÊ³ : âˆ€ {m n} {Î¼ : Modality m n} â†’ Î¼ â“œ ğŸ™ â‰¡ Î¼
  mod-unitÊ³ {Î¼ = ğŸ™} = refl
  mod-unitÊ³ {Î¼ = â€µ Î¼} = refl

  mod-assoc : âˆ€ {m n o p} (Î¼ : Modality o p) {Ï : Modality n o} {Îº : Modality m n} â†’ (Î¼ â“œ Ï) â“œ Îº â‰¡ Î¼ â“œ (Ï â“œ Îº)
  mod-assoc ğŸ™ = refl
  mod-assoc (â€µ Î¼) {Ï = ğŸ™} = refl
  mod-assoc (â€µ Î¼) {Ï = â€µ Ï} {Îº = ğŸ™} = mod-unitÊ³
  mod-assoc (â€µ Î¼) {Ï = â€µ Ï} {Îº = â€µ Îº} = mod-non-triv-assoc Î¼ Ï Îº


record MTTwoCell (mtb : MTBasis) (mtc : MTComposition mtb) : Setâ‚ where
  open MTBasis mtb
  open MTComposition mtc

  infixl 6 _â“£-vert_
  infixl 5 _â“£-hor_
  field
    TwoCell : âˆ€ {m n} (Î¼ Ï : Modality m n) â†’ Set
    id-cell : âˆ€ {m n} {Î¼ : Modality m n} â†’ TwoCell Î¼ Î¼
    _â“£-vert_ : âˆ€ {m n} {Î¼ Ï Îº : Modality m n} â†’ TwoCell Ï Îº â†’ TwoCell Î¼ Ï â†’ TwoCell Î¼ Îº
    _â“£-hor_ : âˆ€ {m n o} {Î¼1 Ï1 : Modality n o} {Î¼2 Ï2 : Modality m n} â†’
              TwoCell Î¼1 Ï1 â†’ TwoCell Î¼2 Ï2 â†’ TwoCell (Î¼1 â“œ Î¼2) (Ï1 â“œ Ï2)
    two-cell-eq? : âˆ€ {m n} {Î¼ Ï : Modality m n} (Î± Î² : TwoCell Î¼ Ï) â†’ Maybe (Î± â‰¡ Î²)

  eq-cell : âˆ€ {m n} {Î¼ Ï : Modality m n} â†’ Î¼ â‰¡ Ï â†’ TwoCell Î¼ Ï
  eq-cell refl = id-cell

  transp-cellÊ³ : âˆ€ {m n} {Î¼ Ï Ï' : Modality m n} â†’ Ï â‰¡ Ï' â†’ TwoCell Î¼ Ï â†’ TwoCell Î¼ Ï'
  transp-cellÊ³ refl Î± = Î±

  transp-cellË¡ : âˆ€ {m n} {Î¼ Î¼' Ï : Modality m n} â†’ Î¼ â‰¡ Î¼' â†’ TwoCell Î¼ Ï â†’ TwoCell Î¼' Ï
  transp-cellË¡ refl Î± = Î±


record ModeTheory : Setâ‚ where
  field
    mtb : MTBasis
    mtc : MTComposition mtb
    mtc-laws : MTCompositionLaws mtb mtc
    mt2 : MTTwoCell mtb mtc

  open MTBasis mtb public
  open MTComposition mtc public
  open MTCompositionLaws mtc-laws public
  open MTTwoCell mt2 public
