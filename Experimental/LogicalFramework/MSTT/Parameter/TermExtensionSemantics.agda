open import Experimental.LogicalFramework.MSTT.Parameter.ModeTheory
open import Experimental.LogicalFramework.MSTT.Parameter.TypeExtension

module Experimental.LogicalFramework.MSTT.Parameter.TermExtensionSemantics
  (â„³ : ModeTheory) (ğ’¯ : TyExt â„³)
  where

open import Data.List
open import Data.Product
open import Data.Unit

open import Experimental.LogicalFramework.MSTT.Parameter.TermExtension â„³ ğ’¯
open TmExt
open import Experimental.LogicalFramework.MSTT.Syntax.Types â„³ ğ’¯
open import Experimental.LogicalFramework.MSTT.Syntax.Contexts â„³ ğ’¯
open import Experimental.LogicalFramework.MSTT.Interpretation.TypeContext â„³ ğ’¯

open import Model.CwF-Structure as M
  renaming (Ctx to SemCtx; Ty to SemTy; Tm to SemTm; ClosedTy to ClosedSemTy) using ()
import Model.DRA as DRA

open ModeTheory â„³

private variable
  m n : Mode


-- A SemTmConstructorLocal refers to an MSTT context and not a
-- semantic context. This has the advantage that it corresponds to the
-- arguments of the contructor Tm.ext.
SemTmConstructorLocal : List (TmArgInfo m) â†’ Ctx m â†’ Ty m â†’ Set
SemTmConstructorLocal []                   Î“ T = SemTm âŸ¦ Î“ âŸ§ctx âŸ¦ T âŸ§ty
SemTmConstructorLocal (arginfo âˆ· arginfos) Î“ T =
  SemTm (âŸ¦ Î“ âŸ§ctx ++âŸ¦ tmarg-tel arginfo âŸ§nltel) âŸ¦ tmarg-ty arginfo âŸ§ty â†’ SemTmConstructorLocal arginfos Î“ T

SemTmConstructor : List (TmArgInfo m) â†’ Ty m â†’ Set
SemTmConstructor {m = m} arginfos T = {Î“ : Ctx m} â†’ SemTmConstructorLocal arginfos Î“ T


SemTmConstructorLocalNatural : {arginfos : List (TmArgInfo m)} {Î“ Î” : Ctx m} {T : Ty m}
                               (fÎ” : SemTmConstructorLocal arginfos Î” T) (fÎ“ : SemTmConstructorLocal arginfos Î“ T)
                               (Ïƒ : âŸ¦ Î“ âŸ§ctx M.â‡’ âŸ¦ Î” âŸ§ctx) â†’
                               Set
SemTmConstructorLocalNatural {arginfos = []} {T = T} tÎ” tÎ“ Ïƒ =
  tÎ” M.[ ty-closed-natural T âˆ£ Ïƒ ]cl M.â‰…áµ—áµ tÎ“
SemTmConstructorLocalNatural {arginfos = arginfo âˆ· arginfos} {Î” = Î”} fÎ” fÎ“ Ïƒ =
  (t : SemTm (âŸ¦ Î” âŸ§ctx ++âŸ¦ tmarg-tel arginfo âŸ§nltel) âŸ¦ tmarg-ty arginfo âŸ§ty) â†’
  SemTmConstructorLocalNatural (fÎ” t) (fÎ“ (t M.[ ty-closed-natural (tmarg-ty arginfo) âˆ£ apply-nltel-sub Ïƒ (tmarg-tel arginfo) ]cl)) Ïƒ

SemTmConstructorNatural : {tmarg-infos : List (TmArgInfo m)} {T : Ty m} â†’ SemTmConstructor tmarg-infos T â†’ Set
SemTmConstructorNatural {m = m} f =
  {Î“ Î” : Ctx m} (Ïƒ : âŸ¦ Î“ âŸ§ctx M.â‡’ âŸ¦ Î” âŸ§ctx) â†’ SemTmConstructorLocalNatural (f {Î”}) (f {Î“}) Ïƒ


SemTmConstructorLocalEquiv : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} {T : Ty m}
                             (f g : SemTmConstructorLocal arginfos Î“ T) â†’
                             Set
SemTmConstructorLocalEquiv {arginfos = []} t s = t M.â‰…áµ—áµ s
SemTmConstructorLocalEquiv {arginfos = arginfo âˆ· arginfos} {Î“} f g =
  {t s : SemTm (âŸ¦ Î“ âŸ§ctx ++âŸ¦ tmarg-tel arginfo âŸ§nltel) âŸ¦ tmarg-ty arginfo âŸ§ty} â†’
  t M.â‰…áµ—áµ s â†’ SemTmConstructorLocalEquiv (f t) (g s)

SemTmConstructorLocalCong : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} {T : Ty m} â†’
                            SemTmConstructorLocal arginfos Î“ T â†’
                            Set
SemTmConstructorLocalCong f = SemTmConstructorLocalEquiv f f

SemTmConstructorCong : {arginfos : List (TmArgInfo m)} {T : Ty m} â†’
                       SemTmConstructor arginfos T â†’
                       Set
SemTmConstructorCong {m = m} f = {Î“ : Ctx m} â†’ SemTmConstructorLocalCong (f {Î“})


record TmExtSem (ğ“‰ : TmExt) : Set where
  no-eta-equality
  field
    âŸ¦_âŸ§tm-code : âˆ€ {m} â†’ (c : TmExtCode ğ“‰ m) â†’ SemTmConstructor (tm-code-arginfos ğ“‰ c) (tm-code-ty ğ“‰ c)
    âŸ¦âŸ§tm-code-natural : âˆ€ {m} (c : TmExtCode ğ“‰ m) â†’ SemTmConstructorNatural âŸ¦ c âŸ§tm-code
    âŸ¦âŸ§tm-code-cong : âˆ€ {m} (c : TmExtCode ğ“‰ m) â†’ SemTmConstructorCong âŸ¦ c âŸ§tm-code


SemTms : List (TmArgInfo m) â†’ Ctx m â†’ Set
SemTms []                   Î“ = âŠ¤
SemTms (arginfo âˆ· arginfos) Î“ = SemTm (âŸ¦ Î“ âŸ§ctx ++âŸ¦ tmarg-tel arginfo âŸ§nltel) âŸ¦ tmarg-ty arginfo âŸ§ty Ã— SemTms arginfos Î“

apply-sem-tm-constructor : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} {T : Ty m} â†’
                           SemTmConstructorLocal arginfos Î“ T â†’
                           SemTms arginfos Î“ â†’
                           SemTm âŸ¦ Î“ âŸ§ctx âŸ¦ T âŸ§ty
apply-sem-tm-constructor {arginfos = []}    t tms        = t
apply-sem-tm-constructor {arginfos = _ âˆ· _} f (tm , tms) = apply-sem-tm-constructor (f tm) tms

semtms-subst : {arginfos : List (TmArgInfo m)} {Î“ Î” : Ctx m} â†’
               SemTms arginfos Î” â†’
               (âŸ¦ Î“ âŸ§ctx M.â‡’ âŸ¦ Î” âŸ§ctx) â†’
               SemTms arginfos Î“
semtms-subst {arginfos = []} tms Ïƒ = tt
semtms-subst {arginfos = arginfo âˆ· arginfos} (tm , tms) Ïƒ =
  tm M.[ ty-closed-natural (tmarg-ty arginfo) âˆ£ apply-nltel-sub Ïƒ (tmarg-tel arginfo) ]cl , semtms-subst tms Ïƒ

_â‰…áµ—áµË¢_ : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} (tms tms' : SemTms arginfos Î“) â†’ Set
_â‰…áµ—áµË¢_ {arginfos = []} tms tms' = âŠ¤
_â‰…áµ—áµË¢_ {arginfos = _ âˆ· _} (tm , tms) (tm' , tms') = (tm M.â‰…áµ—áµ tm') Ã— (tms â‰…áµ—áµË¢ tms')

apply-sem-tm-constr-natural-local : {arginfos : List (TmArgInfo m)} {Î“ Î” : Ctx m} {T : Ty m}
                                    (Ïƒ : âŸ¦ Î“ âŸ§ctx M.â‡’ âŸ¦ Î” âŸ§ctx)
                                    (f : SemTmConstructorLocal arginfos Î” T) (g : SemTmConstructorLocal arginfos Î“ T) â†’
                                    SemTmConstructorLocalNatural f g Ïƒ â†’
                                    (tms : SemTms arginfos Î”) â†’
                                    (apply-sem-tm-constructor f tms) M.[ ty-closed-natural T âˆ£ Ïƒ ]cl
                                      M.â‰…áµ—áµ
                                    apply-sem-tm-constructor g (semtms-subst tms Ïƒ)
apply-sem-tm-constr-natural-local {arginfos = []} Ïƒ t s nat _ = nat
apply-sem-tm-constr-natural-local {arginfos = arginfo âˆ· arginfos} Ïƒ f g nat (tm , tms) =
  apply-sem-tm-constr-natural-local Ïƒ (f tm) (g _) (nat tm) tms

apply-sem-tm-constructor-natural : {arginfos : List (TmArgInfo m)} {Î“ Î” : Ctx m} {T : Ty m}
                                   (f : SemTmConstructor arginfos T) â†’ SemTmConstructorNatural f â†’
                                   (Ïƒ : âŸ¦ Î“ âŸ§ctx M.â‡’ âŸ¦ Î” âŸ§ctx)
                                   (tms : SemTms arginfos Î”) â†’
                                   (apply-sem-tm-constructor f tms) M.[ ty-closed-natural T âˆ£ Ïƒ ]cl
                                     M.â‰…áµ—áµ
                                   apply-sem-tm-constructor (f {Î“}) (semtms-subst tms Ïƒ)
apply-sem-tm-constructor-natural f nat Ïƒ tms = apply-sem-tm-constr-natural-local Ïƒ f f (nat Ïƒ) tms

apply-sem-tm-constr-local-equiv : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} {T : Ty m}
                                  (f g : SemTmConstructorLocal arginfos Î“ T) â†’ SemTmConstructorLocalEquiv f g â†’
                                  {tms tms' : SemTms arginfos Î“} â†’ tms â‰…áµ—áµË¢ tms' â†’
                                  apply-sem-tm-constructor f tms M.â‰…áµ—áµ apply-sem-tm-constructor g tms'
apply-sem-tm-constr-local-equiv {arginfos = []} f g equiv _ = equiv
apply-sem-tm-constr-local-equiv {arginfos = arginfo âˆ· arginfos} f g equiv (ğ’† , ğ’†s) =
  apply-sem-tm-constr-local-equiv (f _) (g _) (equiv ğ’†) ğ’†s

apply-sem-tm-constructor-cong : {arginfos : List (TmArgInfo m)} {Î“ : Ctx m} {T : Ty m}
                                (f : SemTmConstructor arginfos T) â†’ SemTmConstructorCong f â†’
                                {tms tms' : SemTms arginfos Î“} â†’ tms â‰…áµ—áµË¢ tms' â†’
                                apply-sem-tm-constructor f tms M.â‰…áµ—áµ apply-sem-tm-constructor f tms'
apply-sem-tm-constructor-cong f fcong ğ’† = apply-sem-tm-constr-local-equiv f f fcong ğ’†
