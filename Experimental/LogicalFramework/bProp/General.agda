--------------------------------------------------
-- Definition of BiSikkel propositions and their substitution
--   Just as MSTT syntax, the general definition of propositions is
--   parametrised by a type of names to represent variables. It is not
--   recommended to directly import this module, but rather use
--   bProp.Named.
--------------------------------------------------

open import Experimental.LogicalFramework.MSTT.Parameter.ModeTheory
open import Experimental.LogicalFramework.MSTT.Parameter.TypeExtension
open import Experimental.LogicalFramework.MSTT.Parameter.TermExtension using (TmExt)
open import Experimental.LogicalFramework.Parameter.bPropExtension

module Experimental.LogicalFramework.bProp.General
  (â„³ : ModeTheory) (ğ’¯ : TyExt â„³) (Name : Set) (ğ“‰ : TmExt â„³ ğ’¯ Name)
  (ğ’· : bPropExt â„³ ğ’¯ Name ğ“‰)
  where

open import Data.List
open import Data.Product renaming (_,_ to [_,_])
open import Data.Unit
open import Relation.Binary.PropositionalEquality

open ModeTheory â„³
open bPropExt ğ’·

open import Experimental.LogicalFramework.MSTT.Syntax.Types â„³ ğ’¯
open import Experimental.LogicalFramework.MSTT.Syntax.Contexts â„³ ğ’¯ Name
open import Experimental.LogicalFramework.MSTT.Syntax.General â„³ ğ’¯ Name ğ“‰

open import Experimental.LogicalFramework.MSTT.Parameter.TermExtension â„³ ğ’¯ Name
open import Experimental.LogicalFramework.Parameter.ArgInfo â„³ ğ’¯ Name

private variable
  m n : Mode
  Î¼ Ï : Modality m n
  x : Name
  Î“ Î” : Ctx m
  T : Ty m


infixl 3 âˆ€[_âˆ£_âˆˆ_]_
infixr 6 _âŠƒ_
infixl 9 _âˆ§_
infix 12 _â‰¡áµ‡_


bPropExtTmArgs : {m : Mode} â†’ List (TmArgInfo m) â†’ Ctx m â†’ Set
bPropExtTmArgs []               Î“ = âŠ¤
bPropExtTmArgs (info âˆ· tminfos) Î“ = Tm (Î“ ++tel tmarg-tel info) (tmarg-ty info) Ã— bPropExtTmArgs tminfos Î“

-- TODO: include connective for disjunction and existential quantification.
data bProp {m} (Î“ : Ctx m) : Set
bPropExtBPArgs : {m : Mode} â†’ List (ArgInfo m) â†’ Ctx m â†’ Set

data bProp {m} Î“ where
  âŠ¤áµ‡ âŠ¥áµ‡ : bProp Î“
  _â‰¡áµ‡_ : {T : Ty m} (t1 t2 : Tm Î“ T) â†’ bProp Î“
  âŸ¨_âˆ£_âŸ©âŠƒ_ : (Î¼ : Modality n m) (Ï† : bProp (Î“ ,lockâŸ¨ Î¼ âŸ©)) (Ïˆ : bProp Î“) â†’ bProp Î“
  _âˆ§_ : (Ï† Ïˆ : bProp Î“) â†’ bProp Î“
  âˆ€[_âˆ£_âˆˆ_]_ : (Î¼ : Modality n m) (x : Name) (T : Ty n) â†’ bProp (Î“ ,, Î¼ âˆ£ x âˆˆ T) â†’ bProp Î“
  âŸ¨_âˆ£_âŸ© : (Î¼ : Modality n m) â†’ bProp (Î“ ,lockâŸ¨ Î¼ âŸ©) â†’ bProp Î“
  ext : (c : bPropExtCode m) â†’ bPropExtTmArgs (bp-code-tmarg-infos c) Î“ â†’ bPropExtBPArgs (bp-code-bparg-infos c) Î“ â†’ bProp Î“
    -- ^ This constructor is not intended for direct use. An instantiation of BiSikkel with
    --   specific proposition extensions should rather provide more convenient bProp formers
    --   via pattern synonyms.

bPropExtBPArgs []               Î“ = âŠ¤
bPropExtBPArgs (info âˆ· bpinfos) Î“ = bProp (Î“ ++tel arg-tel info) Ã— bPropExtBPArgs bpinfos Î“


Â¬âŸ¨_âŸ©_ : (Î¼ : Modality m n) {Î“ : Ctx n} â†’ bProp (Î“ ,lockâŸ¨ Î¼ âŸ©) â†’ bProp Î“
Â¬âŸ¨ Î¼ âŸ© Ï† = âŸ¨ Î¼ âˆ£ Ï† âŸ©âŠƒ âŠ¥áµ‡


-- A proposition can be traversed whenever terms can be traversed
record bPropTravStruct (Trav : âˆ€ {m} â†’ Ctx m â†’ Ctx m â†’ Set) : Set where
  field
    trav-tm : Tm Î” T â†’ Trav Î“ Î” â†’ Tm Î“ T
    lift : Trav Î“ Î” â†’ Trav (Î“ ,, Î¼ âˆ£ x âˆˆ T) (Î” ,, Î¼ âˆ£ x âˆˆ T)
    lock : Trav Î“ Î” â†’ Trav (Î“ ,lockâŸ¨ Î¼ âŸ©) (Î” ,lockâŸ¨ Î¼ âŸ©)

  lift-trav-tel : Trav Î“ Î” â†’ (Î˜ : Telescope m n) â†’ Trav (Î“ ++tel Î˜) (Î” ++tel Î˜)
  lift-trav-tel Ïƒ â—‡ = Ïƒ
  lift-trav-tel Ïƒ (Î˜ ,, Î¼ âˆ£ x âˆˆ T) = lift (lift-trav-tel Ïƒ Î˜)
  lift-trav-tel Ïƒ (Î˜ ,lockâŸ¨ Î¼ âŸ©) = lock (lift-trav-tel Ïƒ Î˜)

  traverse-ext-tm-args : {tminfos : List (TmArgInfo m)} â†’ bPropExtTmArgs tminfos Î” â†’ Trav Î“ Î” â†’ bPropExtTmArgs tminfos Î“
  traverse-ext-tm-args {tminfos = []}               _                  Ïƒ = tt
  traverse-ext-tm-args {tminfos = tminfo âˆ· tminfos} [ tmarg , tmargs ] Ïƒ =
    [ trav-tm tmarg (lift-trav-tel Ïƒ (tmarg-tel tminfo)) , traverse-ext-tm-args tmargs Ïƒ ]

  traverse-bprop : bProp Î” â†’ Trav Î“ Î” â†’ bProp Î“
  traverse-ext-bp-args : {bpinfos : List (ArgInfo m)} â†’ bPropExtBPArgs bpinfos Î” â†’ Trav Î“ Î” â†’ bPropExtBPArgs bpinfos Î“

  traverse-bprop âŠ¤áµ‡ Ïƒ = âŠ¤áµ‡
  traverse-bprop âŠ¥áµ‡ Ïƒ = âŠ¥áµ‡
  traverse-bprop (t1 â‰¡áµ‡ t2) Ïƒ = trav-tm t1 Ïƒ â‰¡áµ‡ trav-tm t2 Ïƒ
  traverse-bprop (âŸ¨ Î¼ âˆ£ Ï† âŸ©âŠƒ Ïˆ) Ïƒ = âŸ¨ Î¼ âˆ£ traverse-bprop Ï† (lock Ïƒ) âŸ©âŠƒ traverse-bprop Ïˆ Ïƒ
  traverse-bprop (Ï† âˆ§ Ïˆ) Ïƒ = traverse-bprop Ï† Ïƒ âˆ§ traverse-bprop Ïˆ Ïƒ
  traverse-bprop (âˆ€[ Î¼ âˆ£ x âˆˆ T ] Ï†) Ïƒ = âˆ€[ Î¼ âˆ£ x âˆˆ T ] traverse-bprop Ï† (lift Ïƒ)
  traverse-bprop âŸ¨ Î¼ âˆ£ Ï† âŸ© Ïƒ = âŸ¨ Î¼ âˆ£ traverse-bprop Ï† (lock Ïƒ) âŸ©
  traverse-bprop (ext c tmargs bpargs) Ïƒ = ext c (traverse-ext-tm-args tmargs Ïƒ) (traverse-ext-bp-args bpargs Ïƒ)

  traverse-ext-bp-args {bpinfos = []}               _                  Ïƒ = tt
  traverse-ext-bp-args {bpinfos = bpinfo âˆ· bpinfos} [ bparg , bpargs ] Ïƒ =
    [ traverse-bprop bparg (lift-trav-tel Ïƒ (arg-tel bpinfo)) , traverse-ext-bp-args bpargs Ïƒ ]

open bPropTravStruct using (traverse-bprop)


renbPropTrav : bPropTravStruct Ren
bPropTravStruct.trav-tm renbPropTrav = rename-tm
bPropTravStruct.lift renbPropTrav = lift-ren
bPropTravStruct.lock renbPropTrav = Î» Ïƒ â†’ Ïƒ ,rlockâŸ¨ _ âŸ©

rename-bprop : bProp Î” â†’ Ren Î“ Î” â†’ bProp Î“
rename-bprop = traverse-bprop renbPropTrav


subbPropTrav : bPropTravStruct Sub
bPropTravStruct.trav-tm subbPropTrav = _[_]tm
bPropTravStruct.lift subbPropTrav = lift-sub
bPropTravStruct.lock subbPropTrav = Î» Ïƒ â†’ Ïƒ ,slockâŸ¨ _ âŸ©

_[_]bprop : bProp Î” â†’ Sub Î“ Î” â†’ bProp Î“
Ï† [ Ïƒ ]bprop = traverse-bprop subbPropTrav Ï† Ïƒ


-- Isomorphisms witnessing the functoriality of locks (wrt propositions)
lockğŸ™-bprop : bProp Î“ â†’ bProp (Î“ ,lockâŸ¨ ğŸ™ âŸ©)
lockğŸ™-bprop t = rename-bprop t (lockğŸ™-ren)

unlockğŸ™-bprop : bProp (Î“ ,lockâŸ¨ ğŸ™ âŸ©) â†’ bProp Î“
unlockğŸ™-bprop t = rename-bprop t (unlockğŸ™-ren)

fuselocks-bprop : bProp (Î“ ,lockâŸ¨ Î¼ âŸ© ,lockâŸ¨ Ï âŸ©) â†’ bProp (Î“ ,lockâŸ¨ Î¼ â“œ Ï âŸ©)
fuselocks-bprop t = rename-bprop t fuselocks-ren

unfuselocks-bprop : bProp (Î“ ,lockâŸ¨ Î¼ â“œ Ï âŸ©) â†’ bProp (Î“ ,lockâŸ¨ Î¼ âŸ© ,lockâŸ¨ Ï âŸ©)
unfuselocks-bprop t = rename-bprop t unfuselocks-ren


_âŠƒ_ : (Ï† Ïˆ : bProp Î“) â†’ bProp Î“
Ï† âŠƒ Ïˆ = âŸ¨ ğŸ™ âˆ£ lockğŸ™-bprop Ï† âŸ©âŠƒ Ïˆ

Â¬ : bProp Î“ â†’ bProp Î“
Â¬ Ï† = Ï† âŠƒ âŠ¥áµ‡
