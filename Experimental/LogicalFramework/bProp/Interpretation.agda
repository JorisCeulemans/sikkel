--------------------------------------------------
-- Interpretation of propositions in a presheaf model
--------------------------------------------------

open import Experimental.LogicalFramework.MSTT.Parameter
open import Experimental.LogicalFramework.Parameter.bPropExtension
open import Experimental.LogicalFramework.Parameter.bPropExtensionSemantics using (bPropExtSem)

module Experimental.LogicalFramework.bProp.Interpretation
  (ğ’« : MSTT-Parameter) (let open MSTT-Parameter ğ’«)
  (ğ’· : bPropExt â„³ ğ’¯ ğ“‰) (âŸ¦ğ’·âŸ§ : bPropExtSem â„³ ğ’¯ ğ“‰ ğ’·)
  where

open import Data.List
open import Data.Product
open import Data.Unit.Polymorphic

open import Model.CwF-Structure as M renaming (Ctx to SemCtx; Ty to SemTy) using (_â‰…áµ—Ê¸_)
import Model.DRA as DRA
import Model.Type.Function as M
import Model.Type.Product as M
import Model.Type.Constant as M
import Experimental.DependentTypes.Model.IdentityType.AlternativeTerm as M
import Experimental.DependentTypes.Model.Function as M

open import Experimental.LogicalFramework.MSTT ğ’«
open import Experimental.LogicalFramework.bProp.Syntax ğ’« ğ’·
open import Experimental.LogicalFramework.Parameter.ArgInfo â„³ ğ’¯
open Experimental.LogicalFramework.Parameter.bPropExtensionSemantics â„³ ğ’¯ ğ“‰ hiding (bPropExtSem)

open bPropExtSem âŸ¦ğ’·âŸ§

private variable
  m : Mode
  Î“ Î” : Ctx m


âŸ¦_âŸ§bprop : bProp Î“ â†’ SemTy âŸ¦ Î“ âŸ§ctx
âŸ¦_âŸ§bpextargs : âˆ€ {arginfos} {names : ArgBoundNames arginfos}â†’ ExtBPArgs arginfos names Î“ â†’ SemProps arginfos Î“

âŸ¦ âŠ¤áµ‡ âŸ§bprop = M.Unit'
âŸ¦ âŠ¥áµ‡ âŸ§bprop = M.Empty'
âŸ¦ t1 â‰¡áµ‡ t2 âŸ§bprop = M.Id âŸ¦ t1 âŸ§tm âŸ¦ t2 âŸ§tm
âŸ¦ âŸ¨ Î¼ âˆ£ Ï† âŸ©âŠƒ Ïˆ âŸ§bprop = DRA.âŸ¨ âŸ¦ Î¼ âŸ§mod âˆ£ âŸ¦ Ï† âŸ§bprop âŸ© M.â‡› âŸ¦ Ïˆ âŸ§bprop
âŸ¦ Ï† âˆ§ Ïˆ âŸ§bprop = âŸ¦ Ï† âŸ§bprop M.âŠ  âŸ¦ Ïˆ âŸ§bprop
âŸ¦ âˆ€[ Î¼ âˆ£ _ âˆˆ T ] Ï† âŸ§bprop = M.Pi âŸ¦ âŸ¨ Î¼ âˆ£ T âŸ© âŸ§ty âŸ¦ Ï† âŸ§bprop
âŸ¦ âŸ¨ Î¼ âˆ£ Ï† âŸ© âŸ§bprop = DRA.âŸ¨ âŸ¦ Î¼ âŸ§mod âˆ£ âŸ¦ Ï† âŸ§bprop âŸ©
âŸ¦ ext c _ tmargs _ bpargs âŸ§bprop = apply-sem-prop-constructor âŸ¦ c âŸ§bp-code âŸ¦ tmargs âŸ§tmextargs âŸ¦ bpargs âŸ§bpextargs

âŸ¦_âŸ§bpextargs         {arginfos = []}          args         = tt
âŸ¦_âŸ§bpextargs {Î“ = Î“} {arginfos = arginfo âˆ· _} (arg , args) =
  (âŸ¦ arg âŸ§bprop M.[ M.to (++tel-++âŸ¦âŸ§nltel Î“ (arg-tel arginfo) _) ]) , âŸ¦ args âŸ§bpextargs
